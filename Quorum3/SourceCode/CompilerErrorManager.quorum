package Libraries.Language.Compile

use Libraries.Containers.HashTable
use Libraries.Containers.List
use Libraries.System.File
use Libraries.Containers.Blueprints.Iterator

class CompilerErrorManager
     integer numberOfErrors = 0
    
    /*
     * Records the errorsPerFile using the full path of the file as
     * the key and the list for that file as the value.
     */
    HashTable<text, List<CompilerError>> errorsPerFile
    File currentFile = undefined
    //text errorKey = ""
    text fileNameKey = ""

    action Add(CompilerError error) 
        if error not= undefined
            numberOfErrors = numberOfErrors + 1
            List<CompilerError> l = errorsPerFile:GetValue(error:GetStaticKey())
            if(l not= undefined) 
                l:Add(error)
            else 
                List<CompilerError> l2
                l = l2
                l:Add(error)
                errorsPerFile:Add(error:GetStaticKey(), l)
            end
        end
    end

     action RemoveErrorsAtKey(text key) 
        if errorsPerFile:HasKey(key)
            List<CompilerError> list = errorsPerFile:RemoveKey(key)
            numberOfErrors = numberOfErrors - list:GetSize()
        end
    end

    action HasErrorsAtKey(text key) returns boolean
        if errorsPerFile:HasKey(key)
            return true
        end

        return false
    end

    action IsCompilationErrorFree returns boolean
        return numberOfErrors = 0
    end

    action GetErrorAmount returns integer
        return numberOfErrors
    end
    
    action GetIterator returns Iterator<CompilerError>
        HashListIterator<CompilerError> iterator
        iterator:SetHash(me:errorsPerFile)
        return iterator
    end

    /*
     * Empties the list of compiler errorsPerFile
     */
    action Empty
        numberOfErrors = 0
        errorsPerFile:Empty()
    end
end