package Libraries.Robots.Lego

/*
The Button class is an object representation of the LEGO Mindstorms EV3 button section located below the screen.
It includes functionality for the buttons as well as the LED that surrounds them.
The buttons and LED patterns available are represented by class constants.

Attribute: Author Evan Pierzina

Attribute: Example
use Libraries.Robots.Lego.Button
use Libraries.Robots.Lego.Motor

class Main
    action Main
        Button button
        Motor motor
        
        motor:SetSpeed("A", 720)    //two motors to move the robot
        motor:SetSpeed("B", 720)
        repeat while true
            button:WaitForButtonPress()
            if button:IsButtonPressed(button:UP_BUTTON)
                motor:RotateForward("A")
                motor:RotateForward("B")
                button:SetLightPattern(button:SOLID_GREEN_LIGHT)
            elseif button:IsButtonPressed(button:DOWN_BUTTON)
                motor:RotateBackward("A")
                motor:RotateBackward("B")
                button:SetLightPattern(button:FLASHING_RED_LIGHT)
            else
                motor:Stop("A")
                motor:Stop("B")
                button:SetLightPattern(button:NO_LIGHT)
            end
        end
    end
end
*/

class Button
    public constant text UP_BUTTON = "up"
    public constant text DOWN_BUTTON = "down"
    public constant text LEFT_BUTTON = "left"
    public constant text RIGHT_BUTTON = "right"
    public constant text CENTER_BUTTON = "enter"
    public constant text ESCAPE_BUTTON = "escape"

    public constant integer NO_LIGHT = 0
    public constant integer SOLID_GREEN_LIGHT = 1
    public constant integer SOLID_RED_LIGHT = 2
    public constant integer SOLID_ORANGE_LIGHT = 3
    public constant integer FLASHING_GREEN_LIGHT = 4
    public constant integer FLASHING_RED_LIGHT = 5
    public constant integer FLASHING_ORANGE_LIGHT = 6
    public constant integer PULSING_GREEN_LIGHT = 7
    public constant integer PULSING_RED_LIGHT = 8
    public constant integer PULSING_ORANGE_LIGHT = 9

    /*
    This action is used to find out if a certain button is currently being pressed.

    Attribute: Parameter button is used to specify the button ot be checked. The Button class has text constants that can be used for this parameter.

    Attribute: Returns a boolean value denoting whether the button specified in the parameter is being pressed down or not.

    Attribute: Author Evan Pierzina

    Attribute: Example
    use Libraries.Robots.Lego.Button
    use Libraries.Robots.Lego.Sound

    Button button
    Sound sound

    repeat until button:IsButtonPressed(button:ESCAPE_BUTTON)
        if button:IsButtonPressed(button:LEFT_BUTTON)
            sound:PlayTone(1000, 20)
        elseif button:IsButtonPressed(button:RIGHT_BUTTON)
            sound:PlayTone(2000, 20)
        end
    end
    */
    
    system action IsButtonPressed(text button) returns boolean

    /*
    This action displays a light pattern on the LED surrounding the EV3's buttons.

    Attribute: Parameter pattern specifies the light pattern to be displayed on the EV3's brick. The Button class has integer constants that can be used for this parameter.

    Attribute: Author Evan Pierzina

    Attribute: Example
    use Libraries.Robots.Lego.Button
    use Libraries.Robots.Lego.Utility

    Button button
    Utility utility

    button:SetLightPattern(button:SOLID_GREEN_LIGHT)
    utility:DelayMilliseconds(2000)
    button:SetLightPattern(button:FLASHING_RED_LIGHT)
    utility:DelayMilliseconds(2000)
    button:SetLightPattern(button:PULSING_ORANGE_LIGHT)
    utility:DelayMilliseconds(2000)
    */
    
    system action SetLightPattern(integer pattern)

    /*
    This action halts the current program until a button is pressed on the EV3 brick.
    
    Attribute: Author Evan Pierzina
    
    Attribute: Example
    use Libraries.Robots.Lego.Button
    use Libraries.Robots.Lego.Motor

    Button button
    Motor motor

    motor:SetSpeed("B", 1080)
    motor:RotateForward("B")
    button:WaitForButtonPress()
    motor:Stop("B")
    */

    system action WaitForButtonPress
end