package Libraries.Game.Graphics

use Libraries.System.File
use Libraries.Containers.HashTable
use Libraries.Containers.Array
use Libraries.Game.Graphics.Glyph
use Libraries.Game.Graphics.Color
use Libraries.System.Properties
use Libraries.Containers.Blueprints.Iterator

/*
The Font class is used to load a font from the system. 

Attribute: Example

    use Libraries.Game.Graphics.Font
    use Libraries.Game.Graphics.Label
    use Libraries.Game.Game

    class Main is Game

        action Main
            StartGame()
        end

        action CreateGame
            Font font
            Label label
            if not font:FontIsAvailable("Veranda")
                output "The font could not be found!"
                return now
            end
            font:LoadFont("Veranda")
            font:SetSize(18)
            label:SetFont(font)
            Add(label)
        end

    end
*/
class Font 
    
    private integer size = 14
    private number angle = 0
    private integer height = 0
    private Color color
    private boolean initialized = false
    private HashTable<text, Glyph> glyphTable

    on create
        color = color:Black()
    end
    
    /*
    This action loads a font from a given font file. 

    Attribute: Example

        use Libraries.Game.Graphics.Font
        use Libraries.System.File
        use Libraries.Game.Game

        class Main is Game

            action Main
                StartGame()
            end

            action CreateGame
                Font font
                File file
                file:SetPath("CustomFont.ttf")
                font:LoadFont(file)
            end

        end
    */
    action LoadFont(File fontFile)
        if initialized
            Dispose()
            EmptyGlyphTable()
        end

        LoadFontNative(fontFile:GetAbsolutePath())
        height = GetHeight()
        initialized = true
    end

    /*
    This action will load a font by name from the system. On Windows, this will
    search for fonts in "C:\Windows\Fonts". On Mac, this will search for fonts
    in "/Library/Fonts".

    Attribute: Example

        use Libraries.Game.Graphics.Font
        use Libraries.Game.Game

        class Main is Game

            action Main
                StartGame()
            end

            action CreateGame
                Font font
                font:LoadFont("Arial")
            end

        end
    */
    action LoadFont(text fontName)

        File fontFile
        text os = GetOS()

        if os:Contains("Windows")
            fontFile:SetWorkingDirectory("C:\Windows\Fonts")
        elseif os:Contains("Mac OS X")
            fontFile:SetWorkingDirectory("/Library/Fonts")
        else
            // Find fonts for a linux system.
        end

        text fontPath = FindFontExtension(fontName)
        if fontPath = ""
            alert("No font file named " + fontName + " could be found on the system!")
        else
            fontFile:SetPath(fontPath)
            LoadFont(fontFile)
        end
    end

    private system action LoadFontNative(text fontFile)

    private action FindFontExtension(text fontName) returns text
        File fontFile
        text os = GetOS()

        if os:Contains("Windows")
            fontFile:SetWorkingDirectory("C:\Windows\Fonts")
        elseif os:Contains("Mac OS X")
            fontFile:SetWorkingDirectory("/Library/Fonts")
        else
            // Find fonts for a linux system.
        end

        boolean success = false
        fontFile:SetPath(fontName + ".ttf")
        
        if fontFile:Exists()
           return fontName + ".ttf"
        else
            fontFile:SetPath(fontName + ".ttc")
            if fontFile:Exists()
                return fontName + ".ttc"
            else
                fontFile:SetPath(fontName + ".otf")
                if fontFile:Exists()
                    return fontName + ".otf"
                else
                    fontFile:SetPath(fontName + ".dfont")
                    if fontFile:Exists()
                        return fontName + ".dfont"
                    else
                        return ""
                    end
                end
            end
        end
    end

    /*
    This action will look in the default system font folder and look for a font
    of the given name. If a font is found with the same name, the action will
    return true. Otherwise, it returns false.

    Attribute: Example

        use Libraries.Game.Graphics.Font
        use Libraries.Game.Game

        class Main is Game

            action Main
                StartGame()
            end

            action CreateGame
                Font font
                boolean hasFont = font:FontIsAvailable("Times New Roman")
            end

        end
    */
    action FontIsAvailable(text fontName) returns boolean
        if FindFontExtension(fontName) = ""
            return false
        else
            return true
        end
    end

    /*
    This action will set the size of the font. The default font size is 14.

    Attribute: Example

        use Libraries.Game.Graphics.Font
        use Libraries.Game.Game

        class Main is Game

            action Main
                StartGame()
            end

            action CreateGame
                Font font
                font:SetSize(16)
            end

        end
    */
    action SetSize (integer newSize)
        size = newSize
        textChanged = true
        if initialized
            SetSizeNative(newSize)
            height = GetHeight()
            EmptyGlyphTable()
        end
    end

    private system action SetSizeNative(integer newSize)

    /*
    This action will return the current size of the font. The default font size
    is 14.

    Attribute: Example

        use Libraries.Game.Graphics.Font
        use Libraries.Game.Game

        class Main is Game

            action Main
                StartGame()
            end

            action CreateGame
                Font font
                output "The default font size is " + font:GetSize()
            end

        end
    */
    action GetSize returns integer
        return size
    end

    /*
    This action will rotate the characters produced by this font by the given
    number of degrees.

    Attribute: Example

        use Libraries.Game.Graphics.Font
        use Libraries.Game.Game

        class Main is Game

            action Main
                StartGame()
            end

            action CreateGame
                Font font
                font:Rotate(45)
            end

        end
    */
    action Rotate(number rotation)
        SetAngle(angle + rotation)
    end

    /*
    This action will set the angle in degrees of the characters produced by this
    font.

    Attribute: Example

        use Libraries.Game.Graphics.Font
        use Libraries.Game.Game

        class Main is Game

            action Main
                StartGame()
            end

            action CreateGame
                Font font
                font:SetAngle(90)
            end

        end
    */
    action SetAngle(number newAngle)
        textChanged = true

        if newAngle <= -360
            newAngle = newAngle - 360 * cast(integer, newAngle/360)
        end
        if newAngle < 0
            newAngle = 360 + newAngle
        end
        if newAngle >= 360
            newAngle = newAngle - 360 * cast(integer, newAngle/360)
        end

        angle = newAngle

        if initialized
            SetAngleNative(angle * -1)
            EmptyGlyphTable()
        end
    end

    private system action SetAngleNative(number angle)

    // Inclusion of color to come in the future.
    /*action SetColor(Color newColor)
        color = newColor
    end*/

    // Inclusion of color to come in the future.
    /*action GetColor returns Color
        return color
    end*/

    /*
    This action will return the current angle of the font. The default angle is
    0 degrees.

    Attribute: Example

        use Libraries.Game.Graphics.Font
        use Libraries.Game.Game

        class Main is Game

            action Main
                StartGame()
            end

            action CreateGame
                Font font
                output "The font has an angle of " + font:GetAngle() + " degrees."
            end

        end
    */
    action GetAngle returns number
        return angle
    end

    /*
    This action will release the resources used by a loaded font. The Font 
    object will no longer be usable until another font is loaded.

    Attribute: Example

        use Libraries.Game.Graphics.Font
        use Libraries.Game.Game

        class Main is Game

            action Main
                StartGame()
            end

            action CreateGame
                Font font
                font:LoadFont("Arial")
                font:Dispose()
            end

        end
    */
    action Dispose
        DisposeNative()
        initialized = false
    end

    private system action DisposeNative

    /*
    This action will return a Glyph object for this font's representation of the
    given character. 

    Attribute: Example

        use Libraries.Game.Graphics.Font
        use Libraries.Game.Graphics.Glyph
        use Libraries.Game.Game

        class Main is Game

            action Main
                StartGame()
            end

            action CreateGame
                Font font
                font:LoadFont("Arial")
                Glyph a = font:GetGlyph("a")
            end

        end
    */
    action GetGlyph(text character) returns Glyph
        text char = character:GetCharacter(0)
        if glyphTable:HasKey(char)
            return glyphTable:GetValue(char):CopyGlyph()
        else
            Glyph temp = GetGlyphNative(char)
            glyphTable:Add(char, temp)
            return temp
        end
    end

    private system action GetGlyphNative(text character) returns Glyph

    /*action LoadDrawables

        if initialized
            // The size and angle are set before drawing to ensure that FreeType is
            // using the proper parameters for each.
            SetSize(size)
            SetAngle(angle)

            // Dispose of the old sprites.
            Iterator<Drawable> iterator = drawables:GetIterator()
            Drawable deleter = undefined

            repeat while iterator:HasNext()
                deleter = iterator:Next()
                deleter:Dispose()
            end

            List<Drawable> tempList

            integer index = 0
            number x = cursorX
            number y = cursorY

            Drawable drawable = undefined
            Glyph glyph = undefined

            repeat targetText:GetSize() times
                glyph = GetGlyph(targetText:GetCharacter(index))
                drawable = glyph:drawable
                drawable:SetPosition(x + glyph:lengthToGlyph, y + glyph:heightFromBaseLine - drawable:GetHeight())
                
                tempList:AddToEnd(drawable)

                index = index + 1
                x = x + glyph:horizontalAdvance
                y = y + glyph:verticalAdvance
            end

            drawables = tempList
            textChanged = false
        end

    end

    action GetDrawable(integer index) returns Drawable
        return drawables:Get(index)
    end

    action GetDrawableListSize returns integer
        return drawables:GetSize()
    end*/

    /*
    This action will check if the font has been loaded yet. If the font has
    been loaded, this will return true. Otherwise, it will return false.

    Attribute: Example

        use Libraries.Game.Graphics.Font
        use Libraries.Game.Game

        class Main is Game

            action Main
                StartGame()
            end

            action CreateGame
                Font font
                font:LoadFont("Arial")
                boolean loaded = font:IsLoaded()
            end

        end
    */
    action IsLoaded returns boolean
        return initialized
    end

    private system action GetHeight returns integer

    /*
    This action will the height of each line of text produced by this font.

    Attribute: Example

        use Libraries.Game.Graphics.Font
        use Libraries.Game.Game

        class Main is Game

            action Main
                StartGame()
            end

            action CreateGame
                Font font
                font:LoadFont("Arial")
                integer height = font:GetLineHeight()
            end

        end
    */
    action GetLineHeight returns integer
        return height
    end

    /*
    This action will return an array of all files found in the system's default
    font folder.

    Attribute: Example

        use Libraries.Game.Graphics.Font
        use Libraries.System.File
        use Libraries.Containers.Array
        use Libraries.Game.Game

        class Main is Game

            action Main
                StartGame()
            end

            action CreateGame
                Font font
                Array<File> fontFiles = font:GetAvailableFonts()
                integer index = 0
                output "The following fonts are available on the system: "
                repeat fontFiles:GetSize() times
                    File temp = fontFiles:Get(index)
                    output temp:GetPath()
                    index = index + 1
                end
            end

        end
    */
    action GetAvailableFonts returns Array<File>

        text os = GetOS()

        Array<File> fontFiles = undefined
        File fontDirectory

        output "OS = " + os

        if os:Contains("Windows")
            fontDirectory:SetWorkingDirectory("C:\Windows\Fonts")
            fontDirectory:SetPath("")
            fontFiles = fontDirectory:GetDirectoryListing()
        elseif os:Contains("Mac OS X")
            fontDirectory:SetWorkingDirectory("/Library/Fonts")
            fontDirectory:SetPath("")
            fontFiles = fontDirectory:GetDirectoryListing()
        else
            // Find fonts for a linux system.
        end

        return fontFiles
    end
    
    // Used to determine where to look for available fonts.
    private action GetOS returns text
        Properties properties
        text os = properties:GetOperatingSystemName()
        return os
    end

    private action EmptyGlyphTable
        Iterator<Glyph> glyphs = glyphTable:GetValueIterator()
        Glyph temp = undefined
        Drawable tempDrawable = undefined
        repeat while glyphs:HasNext()
            temp = glyphs:Next()
            tempDrawable = temp:drawable
            tempDrawable:Dispose()
        end
        glyphTable:Empty()
    end

end