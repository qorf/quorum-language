package Libraries.Web.Page

use Libraries.Web.Page.WebTag
use Libraries.Containers.List
use Libraries.Containers.Blueprints.Iterator
//use Libraries.Web.Page.Attribute
//use Libraries.Web.Page.Attributes
//use Libraries.Web.Page.ColumnGroup

/*
The WebTable class represents HTML's (Hypertext Markup Language) table tag which 
is a class that builds a table. This table contains any number
of rows that have been added to the table. 

Attribute: Example

use Libraries.Web.Page.all
class Main
   action main
      //make a web page
      WebPage page
      WebTable table
      //add a TableRow
      TableRow row
      table:AddRow(row)
      page:AddToBody(table)
   end
end
*/
class WebTable is WebTag
    constant text border = "border"
//    ColumnGroup columnGroup

    /*
        This action gets the border attribute of the web table and returns true
        if there is a border around the cells and false if it does not.

        Attribute: Returns True if the cells have a border and false if not. 

        Attribute: Example

        use Libraries.Web.Page.WebTable
        WebTable table
        boolean hasBorder = table:GetBorder()
    */
    action GetBorder returns boolean
            Attribute b = GetAttribute(border)
            if b = undefined
                return false
            elseif b:GetValue() = ""
                return false
            else
                return true
            end 
    end
    
    /*
        This action sets the border attribute of the web table. If set to true
        there is a border around the cells and if false the cells do not have a 
        a border.

        Attribute: Parameter value True for a border and false for no border. 

        Attribute: Example
        use Libraries.Web.Page.WebTable
        WebTable table
        table:SetBorder(true)
    */
    action SetBorder(boolean value) 
        if value
            AddAttribute(border,"1")
        else
            AddAttribute(border, "")
        end
    end

    /*
        This action adds a row to the table element. See TableRow for the construction
        of a row in a table.

        Attribute: Parameter value A TableRow to be added to the table. 

        Attribute: Example
        use Libraries.Web.Page.WebTable
        use Libraries.Web.Page.TableRow
        use Libraries.Web.Page.TableHeader
        WebTable table
        TableRow row
        TableHeader header
        header:SetDescription("name")
        row:Add(header)
        table:AddRow(row)
    */
//    action AddRow(TableRow value)
//        //Add(value)
//    end

    /*
        This action sets the column group attribute of the web table. The column
        group allows a column or group of columns to be controlled and formatted.

        Attribute: Parameter value The column group. 

        Attribute: Example
        use Libraries.Web.Page.WebTable
        use Libraries.Web.Page.ColumnGroup

        WebTable table
        ColumnGroup cg
        table:SetColumnGroup(cg)
    */
//    action SetColumnGroup(ColumnGroup value)
//        me:columnGroup = value
//    end


    /*
        This action gets the Column group attribute of the web table and returns
        it. The column group is used to format an entire column or group of
        columns.

        Attribute: Returns The column group. 

        Attribute: Example
        use Libraries.Web.Page.WebTable
        use Libraries.Web.Page.ColumnGroup
        WebTable table
        ColumnGroup cg = table:GetColumnGroup()
    */
//    action GetColumnGroup returns ColumnGroup
//        return columnGroup
//    end


    /*
        This action places an TableCaption object inside of a WebTable object.

        Attribute: Parameter content specifies what will be added to the WebTable.

        Attribute: Example
        use Libraries.Web.Page.all

        class Main
            action Main
                WebPage page
                WebTable table
                TableCaption caption

                Body body = page:GetBody()
                body:Add(table)
                caption:SetDescription("This is a table!")
                table:Add(caption)

                text value = page:Generate()
                output value
            end
        end
    */
    action Add(TableCaption content)
        parent:WebTag:tags:Add(content)
    end

    /*
        This action places an ColumnGroup object inside of a WebTable object.

        Attribute: Parameter content specifies what will be added to the WebTable.

        Attribute: Example
        use Libraries.Web.Page.all

        class Main
            action Main
                WebPage page
                WebTable table
                ColumnGroup group
                Column column

                Body body = page:GetBody()
                body:Add(table)
                group:Add(column)
                table:Add(group)
                text value = page:Generate()
                output value
            end
        end
    */

    action Add(ColumnGroup content)
        parent:WebTag:tags:Add(content)
    end

    /*
        This action places an TableHeader object inside of a WebTable object.

        Attribute: Parameter content specifies what will be added to the WebTable.

        Attribute: Example
        use Libraries.Web.Page.all

        class Main
            action Main
                WebPage page
                WebTable table
                TableHeader header

                Body body = page:GetBody()
                body:Add(table)
                header:SetDescription("My Table")
                table:Add(header)
                text value = page:Generate()
                output value
            end
        end
    */
    action Add(TableHeader content)
        parent:WebTag:tags:Add(content)
    end

    /*
        This action places an TableFooter object inside of a WebTable object.

        Attribute: Parameter content specifies what will be added to the WebTable.

        Attribute: Example
        use Libraries.Web.Page.all

        class Main
            action Main
                WebPage page
                WebTable table
                TableFooter footer

                Body body = page:GetBody()
                body:Add(table)
                footer:SetDescription("Table 1: A list of things")
                table:Add(footer)
                text value = page:Generate()
                output value
            end
        end
    */

    action Add(TableFooter content)
        parent:WebTag:tags:Add(content)
    end

    /*
        This action places an TableBody object inside of a WebTable object.
        If a TableBody exists in a WebTable, then TableRows must only be declared
        within TableBody objects, rather than in the WebTable directly.

        Attribute: Parameter content specifies what will be added to the WebTable.

        Attribute: Example
        use Libraries.Web.Page.all

        class Main
            action Main
                WebPage page
                WebTable table
                TableBody tbody
                TableRow row

                Body body = page:GetBody()
                body:Add(table)
                table:Add(tbody)
                tbody:Add(row)
                text value = page:Generate()
                output value
            end
        end
    */
    action Add(TableBody content)
        parent:WebTag:tags:Add(content)
    end

    /*
        This action places an TableRow object inside of a WebTable object.
        There can only be TableRow objects directly in a WebTable if no TableBody
        is present within the table.

        Attribute: Parameter content specifies what will be added to the WebTable.

        Attribute: Example
        use Libraries.Web.Page.all

        class Main
            action Main
                WebPage page
                WebTable table
                TableRow row

                Body body = page:GetBody()
                body:Add(table)
                table:Add(row)
                text value = page:Generate()
                output value
            end
        end
    */
    action Add(TableRow content)
        parent:WebTag:tags:Add(content)
    end

    action Generate returns text
        validateTags()
        text result = "<table " 
        Attributes attributes = parent:WebTag:GetAttributes()
        attributeText = attributes:Generate()
        result = result + attributeText + ">"
        result = result + me:GenerateNestedTags()
        result = result + "</table>"
        return result
    end

//optional caption, colgroup*, optional thead and/or tfoot, (tbody* + tr+), optional tfoot (if it wasn't used before)
    private action validateTags
        List<WebTag> tags = parent:WebTag:tags
        boolean correct = true
        boolean hasTableFooterInStart = false

        Iterator<WebTag> iterator = tags:GetIterator()
        
        //can be empty
        if not iterator:HasNext()
            return now
        end

        WebTag value = iterator:Next()
        if value is TableCaption
            if iterator:HasNext()
                value = iterator:Next()
                if value is TableCaption
                    alert("The table caption was already declared; only one caption is allowed per table.")
                end
            else 
                return now
            end
        end

        repeat while value is ColumnGroup
            if iterator:HasNext()
                value = iterator:Next()
            else 
                return now
            end
        end

        if value is TableHeader
            if iterator:HasNext()
                value = iterator:Next()
                if value is TableHeader
                    alert("The table header was already declared; only one table header is allowed per table.")
                end
            else 
                return now
            end
        end

        if value is TableFooter
            hasTableFooterInStart = true
            if iterator:HasNext()
                value = iterator:Next()
                if value is TableFooter
                    alert("The table footer was already declared; only one table footer is allowed per table.")
                end
            else 
                return now
            end
        end

        boolean hasBody = false
        repeat while value is TableBody
            hasBody = true
            if iterator:HasNext()
                value = iterator:Next()
            else 
                return now
            end
        end

        //if there are no bodies, is the row issue satisfied?
        //You can add rows by themselves or as part of a TableBody element, but not both?
        if value is TableRow
            if not hasBody
                if iterator:HasNext()
                    value = iterator:Next()
                else 
                    return now
                end
            else
                alert("Since a table body was declared for the table, all table rows must be added to a table body rather than directly to the table itself.")
            end

            repeat while value is TableRow
                if iterator:HasNext()
                    value = iterator:Next()
                else 
                    return now
                end
            end
        end

        if value is TableFooter
            if not hasTableFooterInStart
                return now
            else
                alert("The Table Footer was already defined in the table, but it can only appear once.")
            end
        end

        alert("I found a tag with the HTML: " + value:Generate() + ", but this is not allowed at this point in the table.")
    end
end
