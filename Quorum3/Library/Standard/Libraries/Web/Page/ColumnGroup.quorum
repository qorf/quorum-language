package Libraries.Web.Page
use Libraries.Web.Page.WebTag
/*
The ColumnGroup class represents HTML's (Hypertext Markup Language) colgroup tag 
which allows one or more columns to be selected for formatting.
Columns should be added to the ColumnGroup. 
You can find more information about this tag at: 
<a href="http://www.w3schools.com/tags/tag_colgroup.asp">The colgroup attribute</a>.

Attribute: Example

use Libraries.Web.Page.all
class Main
   action main
      //make a web page
      WebPage page
      
      //create a table
      Table table
      //create a column group
      ColumnGroup cg
      //create a column
      Column col
      //setup that column
      col:SetSpan(2)
      //add the column to the column group
      cg:Add(col)
      //add the column group to the table
      table:SetColumnGroup(cg)
      page:AddToBody(table)
   end
end
*/
class ColumnGroup is WebTag
    constant text span = "span"

    /*
        This action gets the span attribute of the ColumnGroup. The span is
        the number of units the column group encompasses.

        Attribute: Returns The span of the column group. 

        Attribute: Example
        use Libraries.Web.Page.Attribute
        use Libraries.Web.Page.ColumnGroup
        ColumnGroup cg
        Attribute span = cg:GetSpan()
        if span not= undefined
            text s = span:GetValue()
        end
    */
    action GetSpan returns Attribute
        return GetAttribute(span)
    end

    /*
        This action sets the span attribute of the column group. The span attribute
        is the number of units the column group encompasses.

        Attribute: Parameter value The span.

        Attribute: Example
        use Libraries.Web.Page.Attribute
        use Libraries.Web.Page.ColumnGroup
        ColumnGroup cg
        cg:SetSpan(2)
        
    */
    action SetSpan(integer value)
        me:AddAttribute(span, "" + value)
    end
        
    action Generate returns text
        text result = "<colgroup " 
        result = result + GenerateAttributes() + ">"
        result = result + me:GenerateNestedTags()
        result = result + "</colgroup>"
        return result
    end

    /*
    This action places a Column object inside of a ColumnGroup object.

    Attribute: Parameter content specifies what will be added to the page.

    Attribute: Example
    use Libraries.Web.Page.all

    class Main
        action Main
            WebPage page
            Body body = page:GetBody()

            Column column
            body:Add(column)

            text value = page:Generate()
            output value
        end
    end
    */
    action Add(Column content)
        parent:WebTag:tags:Add(content)
    end

    /*
    This action places a HiddenContent object inside of a ColumnGroup object.

    Attribute: Parameter content specifies what will be added to the page.

    Attribute: Example
    use Libraries.Web.Page.all

    class Main
        action Main
            WebPage page
            Body body = page:GetBody()

            HiddenContent hiddenContent
            body:Add(hiddenContent)

            text value = page:Generate()
            output value
        end
    end
    */
    action Add(HiddenContent content)
        parent:WebTag:tags:Add(content)
    end
end