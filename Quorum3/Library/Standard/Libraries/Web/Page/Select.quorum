package Libraries.Web.Page

use Libraries.Web.Page.WebTag
use Libraries.Web.Page.Attribute
use Libraries.Web.Page.Attributes
use Libraries.Web.Page.OptionGroup
use Libraries.Web.Page.Option

/*
The Select class represents HTML's (Hypertext Markup Language) select tag which 
is a selection list of options or items. 
You can find more information about this tag at: 
<a href="http://www.w3schools.com/tags/tag_select.asp">The select attribute</a>.

Attribute: Example
use Libraries.Web.Page.all
class Main
   action main
      WebPage page
      
      Select select
      OptionGroup og
      og:SetLabel("States")
      select:AddOptionGroup(og)

      page:AddToBody(select)
      output page:Generate()
   end
end
*/
class Select is WebTag, FlowContent
    constant text autofocus = "autofocus"
    constant text disabled = "disabled"
    constant text formId = "form"
    constant text multiple = "multiple"
    constant text name = "name"
    constant text size = "size"

    /*
        This action gets whether the Select list is automatically got focus (true)
        or not(false).

        Attribute: Returns True if auto focused and false if not auto focused. 

        Attribute: Example
        use Libraries.Web.Page.Select

        Select select
        boolean result = select:GetAutofocus()
        
    */
    action GetAutofocus returns boolean
        Attribute focus = GetAttribute(autofocus)
        return focus:GetValue() = autofocus
    end
    
    /*
        This action sets the select list to be auto focused if true and not if false.

        Attribute: Parameter value True to auto focused and false to not auto focus the select. 

        Attribute: Example
        use Libraries.Web.Page.Select

        Select select
        select:SetAutofocus(true)
        
    */
    action SetAutofocus(boolean value)
        if value
            AddAttribute(autofocus, autofocus)
        else
            RemoveAttribute(autofocus)
        end
    end

    /*
        This action gets whether the Select list is disabled (true)
        or enabled(false).

        Attribute: Returns True if diabled and false if enabled. 

        Attribute: Example
        use Libraries.Web.Page.Select

        Select select
        boolean result = select:GetDisabled()
        
    */
    action GetDisabled returns boolean
        Attribute disable = GetAttribute(me:disabled)
        return disable:GetValue() = me:disabled
    end
    
    /*
        This action sets the select list to be disabled if true and enabled if false.

        Attribute: Parameter value True to disable and false to enable the select. 

        Attribute: Example
        use Libraries.Web.Page.Select

        Select select
        select:SetDisabled(true)
        
    */
    action SetDisabled (boolean value)
        if value
            AddAttribute(disabled, disabled)
        else
            RemoveAttribute(disabled)
        end
    end

    /*
        This action gets a form that the select belongs to.

        Attribute: Returns The form a select list belongs to. 

        Attribute: Example
        use Libraries.Web.Page.Select
        use Libraries.Web.Page.Attribute

        Select select
        Attribute result = select:GetFormId()
        
    */
    action GetFormId returns Attribute
        return me:GetAttribute(formId)
    end

    /*
        This action sets the form a select list is associated with.

        Attribute: Parameter value The name of the associated form. 

        Attribute: Example
        use Libraries.Web.Page.Select

        Select select
        select:SetFormId("myForm")
        
    */
    action SetFormId(text value)
        AddAttribute(formId, value)
    end

    /*
        This action gets whether the Select list can have more than one option selected (true)
        or not(false).

        Attribute: Returns True if more than one can be selected and false if not. 

        Attribute: Example
        use Libraries.Web.Page.Select

        Select select
        boolean result = select:GetMultiple()
        
    */
    action GetMultiple returns boolean
        Attribute m = GetAttribute(multiple)
        return m:GetValue() = multiple
    end

    /*
        This action sets the select list to be able to select more than one 
        option if true and not if false.

        Attribute: Parameter value True to multiple select and false to not allow 
        multiple option to be selected. 

        Attribute: Example
        use Libraries.Web.Page.Select

        Select select
        select:SetMultiple(true)
        
    */
    action SetMultiple(boolean value)
        if value
            AddAttribute(multiple, multiple)
        else
            RemoveAttribute(multiple)
        end
    end

    /*
        This action gets the name of the select list.

        Attribute: Returns Name of the select list. 

        Attribute: Example
        use Libraries.Web.Page.Select
        use Libraries.Web.Page.Attribute

        Select select
        Attribute result = select:GetName()
        
    */
    action GetName returns Attribute
        return me:GetAttribute(name)
    end

    /*
        This action sets the name of the select list.

        Attribute: Parameter value The name of the select list. 

        Attribute: Example
        use Libraries.Web.Page.Select

        Select select
        select:SetName("mySelect")
        
    */
    action SetName(text value)
        AddAttribute(name, value)
    end

    /*
        This action gets the number of visible options in the select list.

        Attribute: Returns The number of options. 

        Attribute: Example
        use Libraries.Web.Page.Select
        use Libraries.Web.Page.Attribute

        Select select
        Attribute result = select:GetSize()
        
    */
    action GetSize returns Attribute
        return me:GetAttribute(size)
    end

    /*
        This action sets the number of visible options in the select list.

        Attribute: Parameter value The size. 

        Attribute: Example
        use Libraries.Web.Page.Select

        Select select
        select:SetName("5")
        
    */
    action SetSize(text value)
        AddAttribute(size, value)
    end

    /*
        This action adds an option to the slecet list.

        Attribute: Parameter value The option to be added to the list. 

        Attribute: Example
        use Libraries.Web.Page.Select
        use Libraries.Web.Page.Option

        Select s
        s:SetName("States")
        Option option
        option:SetLabel("Alabama")
        s:AddOption(option)
        
    */
    action AddOption(Option option)
        //Add(option)
    end

    /*
        This action removes an option from the slecet list.

        Attribute: Parameter value The option to be removed from the list. 

        Attribute: Example
        use Libraries.Web.Page.Select
        use Libraries.Web.Page.Option

        Select s
        s:SetName("States")
        Option option
        option:SetLabel("Alabama")
        s:AddOption(option)

        s:RemoveOption(option)
        
    */
    action RemoveOption(Option option)
        Remove(option)
    end

    /*
        This action adds an option group to the slecet list.

        Attribute: Parameter value The option groupt to be added to the list. 

        Attribute: Example
        use Libraries.Web.Page.Select
        use Libraries.Web.Page.OptionGroup

        Select s
        s:SetName("States")
        OptionGroup og
        og:SetLabel("Alabama")
        s:AddOptionGroup(og)
        
    */
    action AddOptionGroup(OptionGroup optionGroup)
        //Add(optionGroup)
    end

    /*
        This action removes an option group from the slecet list.

        Attribute: Parameter value The option group to be removed from the list. 

        Attribute: Example
        use Libraries.Web.Page.Select
        use Libraries.Web.Page.OptionGroup

        Select s
        s:SetName("States")
        OptionGroup og
        og:SetLabel("Alabama")
        s:AddOptionGroup(og)

        s:RemoveOptionGroup(og)
        
    */
    action RemoveOptionGroup(OptionGroup optionGroup)
        Remove(optionGroup)
    end
    

    action Generate returns text
        text result = "<select " 
        Attributes attributes = parent:WebTag:GetAttributes()
        attributeText = attributes:Generate()
        result = result + attributeText + ">"
        result = result + me:GenerateNestedTags()
        result = result + "</select>"
        return result
    end

    /*
    This action places a OptionGroup object inside of a Select object.

    Attribute: Parameter content specifies what will be added to the page.

    Attribute: Example
    use Libraries.Web.Page.all

    class Main
        action Main
            WebPage page
            Body body = page:GetBody()

            OptionGroup optionGroup
            body:Add(optionGroup)

            text value = page:Generate()
            output value
        end
    end
    */
    action Add(OptionGroup content)
        parent:WebTag:tags:Add(content)
    end

    /*
    This action places a Option object inside of a Select object.

    Attribute: Parameter content specifies what will be added to the page.

    Attribute: Example
    use Libraries.Web.Page.all

    class Main
        action Main
            WebPage page
            Body body = page:GetBody()

            Option option
            body:Add(option)

            text value = page:Generate()
            output value
        end
    end
    */
    action Add(Option content)
        parent:WebTag:tags:Add(content)
    end

    /*
    This action places a HiddenContent object inside of a Select object.

    Attribute: Parameter content specifies what will be added to the page.

    Attribute: Example
    use Libraries.Web.Page.all

    class Main
        action Main
            WebPage page
            Body body = page:GetBody()

            HiddenContent hiddenContent
            body:Add(hiddenContent)

            text value = page:Generate()
            output value
        end
    end
    */
    action Add(HiddenContent content)
        parent:WebTag:tags:Add(content)
    end

    /*
    This action places a Script object inside of a Select object.

    Attribute: Parameter content specifies what will be added to the page.

    Attribute: Example
    use Libraries.Web.Page.all

    class Main
        action Main
            WebPage page
            Body body = page:GetBody()

            Script script
            body:Add(script)

            text value = page:Generate()
            output value
        end
    end
    */
    action Add(Script content)
        parent:WebTag:tags:Add(content)
    end

end