package Libraries.Web

use Libraries.Web.WebTag
//use Libraries.Web.Attribute
//use Libraries.Web.Attributes
//use Libraries.Web.ColumnGroup

/*
The WebTable class represents HTML's (Hypertext Markup Language) table tag which 
is a class that builds a table. This table contains any number
of rows that have been added to the table. 

Attribute: Example

use Libraries.Web.all
class Main
   action main
      //make a web page
      WebPage page
      WebTable table
      //add a TableRow
      TableRow row
      table:AddRow(row)
      page:AddToBody(table)
   end
end
*/
class WebTable is WebTag
    constant text border = "border"
    ColumnGroup columnGroup

    /*
        This action gets the border attribute of the web table and returns true
        if there is a border around the cells and false if it does not.

        Attribute: Returns True if the cells have a border and false if not. 

        Attribute: Example

        use Libraries.Web.WebTable
        WebTable table
        boolean hasBorder = table:GetBorder()
    */
    action GetBorder returns boolean
            Attribute b = GetAttribute(border)
            if b = undefined
                return false
            elseif b:GetValue() = ""
                return false
            else
                return true
            end 
    end
    
    /*
        This action sets the border attribute of the web table. If set to true
        there is a border around the cells and if false the cells do not have a 
        a border.

        Attribute: Parameter value True for a border and false for no border. 

        Attribute: Example
        use Libraries.Web.WebTable
        WebTable table
        table:SetBorder(true)
    */
    action SetBorder(boolean value) 
        if value
            AddAttribute(border,"1")
        else
            AddAttribute(border, "")
        end
    end

    /*
        This action adds a row to the table element. See TableRow for the construction
        of a row in a table.

        Attribute: Parameter value A TableRow to be added to the table. 

        Attribute: Example
        use Libraries.Web.WebTable
        use Libraries.Web.TableRow
        use Libraries.Web.TableHeader
        WebTable table
        TableRow row
        TableHeader header
        header:SetDescription("name")
        row:Add(header)
        table:AddRow(row)
    */
    action AddRow(TableRow value)
        Add(value)
    end

    /*
        This action sets the column group attribute of the web table. The column
        group allows a column or group of columns to be controlled and formatted.

        Attribute: Parameter value The column group. 

        Attribute: Example
        use Libraries.Web.WebTable
        use Libraries.Web.ColumnGroup

        WebTable table
        ColumnGroup cg
        table:SetColumnGroup(cg)
    */
    action SetColumnGroup(ColumnGroup value)
        me:columnGroup = value
    end


    /*
        This action gets the Column group attribute of the web table and returns
        it. The column group is used to format an entire column or group of
        columns.

        Attribute: Returns The column group. 

        Attribute: Example
        use Libraries.Web.WebTable
        use Libraries.Web.ColumnGroup
        WebTable table
        ColumnGroup cg = table:GetColumnGroup()
    */
    action GetColumnGroup returns ColumnGroup
        return columnGroup
    end

    action Generate returns text
        text result = "<table " 
        Attributes attributes = parent:WebTag:GetAttributes()
        attributeText = attributes:Generate()
        result = result + attributeText + ">"
        result = result + columnGroup:Generate() + me:GenerateNestedTags()
        result = result + GetDescription() + "</table>"
        return  result
    end
end
