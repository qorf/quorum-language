package Libraries.Interface.Views

use Libraries.Interface.Views.ControlView
use Libraries.Game.Graphics.Drawable
use Libraries.Game.Graphics.Color
use Libraries.Game.Graphics.ColorGroup
use Libraries.Game.Graphics.Gradient
use Libraries.Game.Graphics.Texture
use Libraries.Game.Graphics.Painter2D

class SelectionHighlightView is ControlView, Drawable

    ColorGroup color = undefined
    Color unselected

    on create
        unselected:SetColor(1, 1, 1, 0)
    end

    action Initialize(ColorGroup selectionColor)
        SetSelectionColor(selectionColor)
        LoadFilledRectangle(1, 1, unselected)
    end

    action SetSelectionColor(ColorGroup selectionColor)
        color = selectionColor
    end

    action GetSelectionColor returns ColorGroup
        return color
    end

    action QueueForDrawing(Painter2D painter)
        parent:Drawable:QueueForDrawing(painter)
    end

    action UpdateSize(number width, number height)
        SetSize(width, height)
    end

    action UpdatePosition(number x, number y, number z)
        SetPosition(x, y)
    end

    action UpdateRotation(number angle)
        SetRotation(angle)
    end

    action UpdateFlipping(boolean flipX, boolean flipY)
        SetFlipX(flipX)
        SetFlipY(flipY)
    end

    action GainedSelection
        if color not= undefined
            SetColor(color)
        end
    end

    action LostSelection
        SetColor(unselected)
    end

    action Copy returns ControlView
        SelectionHighlightView content
        if color not= undefined
            content:Initialize(color)
        end
        return content
    end
    
end