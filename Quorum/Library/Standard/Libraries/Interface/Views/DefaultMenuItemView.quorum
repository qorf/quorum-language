package Libraries.Interface.Views

use Libraries.Interface.Item2D
use Libraries.Interface.Controls.Control
use Libraries.Interface.Views.ControlView
use Libraries.Game.Graphics.Label
use Libraries.Game.Graphics.Painter2D
use Libraries.Interface.Layouts.LayoutProperties
use Libraries.Interface.Layouts.ManualLayout
use Libraries.Containers.Array
use Libraries.Game.Graphics.Drawable
use Libraries.Game.Graphics.Color
use Libraries.Game.Graphics.HierarchyRenderer

class DefaultMenuItemView is Control, MenuItemView, HierarchyRenderer
    
    Label nameLabel
    Label shortcutLabel

    LayoutProperties nameLayout
    LayoutProperties shortcutLayout
    LayoutProperties arrowLayout

    Control arrow
    Control selectionHighlight

    DrawableView arrowView
    DrawableView selectionHighlightView

    number imageSpace = 10
    number labelPadding = 10
    number arrowSpace = 10

    number heightFactor = 1.4

    on create
        ManualLayout layout
        SetLayout(layout)
    end

    action Initialize(text name)
        Initialize(name, "")
    end

    action Initialize(text name, text shortcut)
        Color color
        selectionHighlightView:LoadFilledRectangle(1, 1, color:CustomColor(0.25, 0.25, 1, 1))
        selectionHighlight:SetView2D(selectionHighlightView)
        selectionHighlight:Hide()
        Add(selectionHighlight)

        LayoutProperties highlightLayout
        highlightLayout:SetPercentageWidth(1.0)
        highlightLayout:SetPercentageHeight(1.0)
        selectionHighlight:AddLayoutProperties(highlightLayout)

        nameLabel:SetText(name)
        shortcutLabel:SetText(shortcut)

        nameLayout:SetPixelX(imageSpace)
        
        shortcutLayout:SetPercentageX(1)
        shortcutLayout:SetPixelX(-arrowSpace - shortcutLabel:GetWidth())

        nameLabel:AddLayoutProperties(nameLayout)
        shortcutLabel:AddLayoutProperties(shortcutLayout)

        Add(nameLabel)
        Add(shortcutLabel)

        arrowLayout:SetPercentageX(1)
        arrow:AddLayoutProperties(arrowLayout)
        arrow:SetView2D(arrowView)
        arrow:SetName("DefaultMenuItemView Arrow")
    end

    action QueueForDrawing(Painter2D painter)
        parent:HierarchyRenderer:QueueForDrawing(painter)
    end

    action UpdateSize(number width, number height)
        number lineY = (1 - nameLabel:GetLineHeight() / height) / 2.0

        if lineY < 0
            lineY = 0
        elseif lineY > 1
            lineY = 1
        end

        nameLayout:SetPercentageY(lineY)
        shortcutLayout:SetPercentageY(lineY)

        // Setting the size also triggers the layout.
        SetSize(width, height)
    end

    action UpdatePosition(number x, number y, number z)
        SetPosition(x, y, z)
    end

    action UpdateRotation(number angle)
        SetRotation(angle)
    end

    action UpdateFlipping(boolean flipX, boolean flipY)
        SetFlipX(flipX)
        SetFlipY(flipY)
    end

    action BeginMouseOver

    end

    action EndMouseOver

    end

    action ClickedMouse

    end

    action ReleasedMouse

    end

    action GainedSelection
        selectionHighlight:Show()
        Color color
        nameLabel:SetColor(color:White())
        shortcutLabel:SetColor(color:White())
        arrowView:SetColor(color:White())
    end

    action LostSelection
        selectionHighlight:Hide()
        Color color
        nameLabel:SetColor(color:Black())
        shortcutLabel:SetColor(color:Black())
        arrowView:SetColor(color:Black())
    end

    action EnableMenuArrow(boolean enable)
        if enable
            number contentHeight = GetViewHeight()
            arrowSpace = 10 + contentHeight
            integer height = cast(integer, contentHeight / 2.25)
            if arrowView:IsLoaded()
                arrowView:Dispose()
            end

            Color color

            arrowLayout:SetPixelX(-contentHeight)
            //arrowLayout:SetPixelY(contentHeight - height)
            arrowLayout:SetPercentageY(0.4)
            arrowLayout:SetPercentageOriginY(0.5)
            arrowLayout:SetPixelWidth(height)
            arrowLayout:SetPixelHeight(height)
            arrowView:LoadFilledTriangle(0, 0, 0, 100 + height, 100 + height, (100 + height) / 2, color:Black())
            Add(arrowView)
            Resize()
        else
            Remove(arrowView)
            arrowSpace = 10
        end

        shortcutLayout:SetPixelX(-arrowSpace - shortcutLabel:GetWidth())
    end

    action SetText(text value)
        nameLabel:SetText(value)
    end

    action GetText returns text
        return nameLabel:GetText()
    end

    action SetShortcut(text shortcut)
        shortcutLabel:SetText(shortcut)
        shortcutLayout:SetPixelX(-arrowSpace - shortcutLabel:GetWidth())
    end

    action GetShortcut returns text
        return shortcutLabel:GetText()
    end

    action GetViewWidth returns number
        if shortcutLabel:GetText() = ""
            return imageSpace + nameLabel:GetWidth() + arrowSpace
        else
            return imageSpace + nameLabel:GetWidth() + labelPadding + shortcutLabel:GetWidth() + arrowSpace
        end
    end

    action GetViewHeight returns number
        return nameLabel:GetHeight() * heightFactor
    end

    action GetIconWidth returns number
        return 0
    end

    action GetNameWidth returns number
        return nameLabel:GetWidth()
    end

    action GetShortcutWidth returns number
        return shortcutLabel:GetWidth()
    end

    action GetSubmenuIconWidth returns number
        return arrowSpace
    end

    action PadWidth(number iconWidth, number nameWidth, number shortcutWidth, number submenuIconWidth)
        
    end

    action Copy returns ControlView
        DefaultMenuItemView content
        content:Initialize(GetText(), GetShortcut())
        content:UpdateSize(parent:Item2D:GetWidth(), parent:Item2D:GetHeight())
        return content
    end
end