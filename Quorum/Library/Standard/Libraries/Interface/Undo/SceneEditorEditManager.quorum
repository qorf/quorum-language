package Libraries.Interface.Undo

use Libraries.Interface.Undo.EditManager
use Libraries.Interface.Undo.Edit

class SceneEditorEditManager is EditManager

    /*
    If this is false, incoming edits won't be added to the undo list.
    Used to prevent changes that occur during an undo/redo from modifying
    the undo/redo lists.
    */
    boolean acceptEdits = true

    action IsAcceptingEdits returns boolean
        return acceptEdits
    end

    action AcceptEdits(boolean accept)
        acceptEdits = accept
    end

    action Add(Edit undo)
        if IsAcceptingEdits() = false
            return now
        end

        parent:EditManager:Add(undo)
    end

    /*
        The Undo action undoes an Edit that was added to the system. If there is 
        nothing to undo, this call is ignored.

        Attribute: Example
        use Libraries.Interface.Undo.EditManager
        use Libraries.Interface.Undo.Edit

        EditManager edits
        Edit edit
        edits:Add(edit)
        edits:Undo()
    */
    action Undo
        if CanUndo()
            boolean allow = IsAcceptingEdits()
            Edit undo = parent:EditManager:undoList:RemoveFromEnd()

            AcceptEdits(false)
            undo:Undo()
            AcceptEdits(allow)

            parent:EditManager:redoList:Add(undo)
        end
    end

    /*
        The Redo action redoes an Edit that was added to the system. If there is 
        nothing to redo, then this action is ignored.

        Attribute: Example
        use Libraries.Interface.Undo.EditManager
        use Libraries.Interface.Undo.Edit

        EditManager edits
        Edit edit
        edits:Add(edit)
        edits:Undo()
        edits:Redo()
    */
    action Redo
        if CanRedo()
            boolean allow = IsAcceptingEdits()
            Edit redo = parent:EditManager:redoList:RemoveFromEnd()

            AcceptEdits(false)
            redo:Redo()
            AcceptEdits(allow)

            parent:EditManager:undoList:Add(redo)
        end
    end

end