package Libraries.Interface.Controls

use Libraries.Interface.Layouts.Layout
use Libraries.Interface.Layouts.FlowLayout
use Libraries.Interface.Layouts.LayoutProperties
use Libraries.Interface.Item2D
use Libraries.Game.Graphics.Drawable
use Libraries.Game.Graphics.Label
use Libraries.Containers.Array
use Libraries.Game.Graphics.Color
use Libraries.Game.Graphics.ColorGroup
use Libraries.Game.Graphics.Gradient
use Libraries.Game.Graphics.Texture
use Libraries.Game.Graphics.TextureRegion
use Libraries.Game.Graphics.Font
use Libraries.Interface.Views.View2D
use Libraries.Interface.Views.LabelBoxView
use Libraries.Interface.Views.LabelBoxToggleView
use Libraries.Interface.Behaviors.Controls.TabCloseBehavior
use Libraries.Game.Graphics.PixelMap
use Libraries.Game.Graphics.Format

class Tab is ToggleButton
    TabPane container = undefined

    // The Item that is displayed in the TabPane when the Tab is activated.
    Item2D relatedItem = undefined

    // The Item that should receive focus when the Tab is activated. If left
    // undefined, the relatedItem is focused instead, if possible.
    Item2D focusTarget = undefined

    Layout layout = undefined
    LayoutProperties defaultProperties = undefined
    LayoutProperties iconLayoutProperties = undefined
    LayoutProperties labelLayoutProperties = undefined
    LayoutProperties buttonLayoutProperties = undefined

    Icon icon
    Label label
    Button closeButton

    on create
        AllowToggleOff(false)

        FlowLayout flow
        layout = flow
        flow:SetIgnoreHidden(true)
        SetLayout(flow)

        iconLayoutProperties = icon:GetDefaultLayoutProperties()
        iconLayoutProperties:SetHorizontalLayoutMode(iconLayoutProperties:MAINTAIN_ASPECT_RATIO)
        iconLayoutProperties:SetVerticalLayoutMode(iconLayoutProperties:STANDARD)
        iconLayoutProperties:SetPercentageWidth(1.0)
        iconLayoutProperties:SetPercentageHeight(1.0)
        iconLayoutProperties:SetTopPadding(4)
        iconLayoutProperties:SetBottomPadding(4)
        iconLayoutProperties:SetLeftPadding(4)
        
        defaultProperties = GetDefaultLayoutProperties()
        defaultProperties:SetHorizontalLayoutMode(defaultProperties:FIT_CONTENTS)
        defaultProperties:SetVerticalLayoutMode(defaultProperties:FIT_FONT)

        Font font = GetDefaultFont()
        if font not= undefined
            integer defaultFontSize = GetDefaultFontSize()
            defaultProperties:SetFont(font)
            defaultProperties:SetFontSize(defaultFontSize)
        end  

        labelLayoutProperties = label:GetDefaultLayoutProperties()
        buttonLayoutProperties = closeButton:GetDefaultLayoutProperties()

        SetAccessibilityCode(parent:Item:TAB)
        closeButton:SetName("Close")
        closeButton:SetFont(undefined)

        TabCloseBehavior behavior
        behavior:SetTab(me)
        closeButton:SetBehavior(behavior)

        labelLayoutProperties:SetHorizontalLayoutMode(labelLayoutProperties:FIT_CONTENTS)
        labelLayoutProperties:SetLeftPadding(7)
        labelLayoutProperties:SetRightPadding(7)

        buttonLayoutProperties:SetHorizontalLayoutMode(buttonLayoutProperties:MAINTAIN_ASPECT_RATIO)
        buttonLayoutProperties:SetVerticalLayoutMode(buttonLayoutProperties:STANDARD)
        buttonLayoutProperties:SetPercentageWidth(1.0)
        buttonLayoutProperties:SetPercentageHeight(1.0)
        buttonLayoutProperties:SetTopPadding(4)
        buttonLayoutProperties:SetBottomPadding(4)
        buttonLayoutProperties:SetRightPadding(4)

        icon:Hide()

        Add(icon)
        Add(label)
        Add(closeButton)
    end

    /*
        This action gets the containing tab pane for this tab.
    */
    action GetTabPane returns TabPane
        return container
    end

    /*
        This action sets the containing tab pane for this tab.
    */
    action SetTabPane(TabPane pane)
        me:container = pane
    end

    action IsAccessibleParent returns boolean
        return true
    end

    action GetDefaultFontName returns text
        Font font
        if font:IsFontAvailable("Arial")
            return "Arial"
        else
            return parent:Control:GetDefaultFontName()
        end
    end

    action GetDefaultFontSize returns integer
        return 14
    end

    action SetRelatedItem(Item2D item)
        relatedItem = item
    end

    action GetRelatedItem returns Item2D
        return relatedItem
    end

    action SetIcon(Icon newIcon)
        parent:Control:SetIcon(newIcon)
        if newIcon = undefined
            icon:Hide()
        else
            icon:Load(newIcon)
            number aspectRatio = icon:GetWidth() / cast(number, icon:GetHeight())
            iconLayoutProperties:SetPercentageWidth(aspectRatio)
            icon:Show()
        end
    end

    action SetName(text name)
        label:SetText(name)
        icon:SetName(name + " Icon")
        parent:Item2D:SetName(name)
    end

    action DisplayName(boolean display)
        displayLabel = display
        if display
            label:Show()
        else
            label:Hide()
        end
    end

    action DisplayCloseButton(boolean display)
        displayClose = display
        if display
            closeButton:Show()
        else
            closeButton:Hide()
        end
    end

    action IsDisplayingCloseButton returns boolean
        return closeButton:IsShowing()
    end

    action OnToggleOn
        if GetButtonGroup() is TabBar
            TabBar bar = cast(TabBar, GetButtonGroup())
            TabPane pane = bar:GetTabPane()
            if pane not= undefined
                pane:UpdateSelection(me)
            end

            bar:FitTab(me)
        end
    end

    action OnToggleOff
        if GetButtonGroup() is TabBar
            TabBar bar = cast(TabBar, GetButtonGroup())
            TabPane pane = bar:GetTabPane()
            if pane not= undefined
                pane:UpdateSelection(undefined)
            end
        end
    end

    /*
    For internal use only.
    */
    action GetLabel returns Label
        return label
    end

    /*
    This action is used to load the graphical components of the Control. This is
    handled automatically by the Game engine as needed, and most users shouldn't
    need to use this action directly.
    */
    action LoadGraphics(LayoutProperties properties)
        if properties = undefined
            return now
        end

        TextureRegion region = properties:GetIcon()
        if region = undefined
            if icon:IsShowing()
                icon:Hide()
            end
        else
            if icon:IsShowing() = false
                icon:Load(region)
                number aspectRatio = icon:GetWidth() / cast(number, icon:GetHeight())
                iconLayoutProperties:SetPercentageWidth(aspectRatio)
                icon:Show()
            end
            ColorGroup iconColor = properties:GetIconColor()
            if iconColor = undefined
                Color color
                iconColor = color:White()
            end

            icon:SetColor(iconColor)
        end

        Font font = properties:GetFont()
        if font not= undefined
            label:SetFont(font)
            label:SetSize(properties:GetFontSize())
        else
            label:Hide()
        end

        View2D view = properties:GetView2D()
        if view = undefined
            if GetView2D() = undefined
                LabelBoxToggleView content
                Color color
                ColorGroup background = properties:GetBackgroundColor()
                ColorGroup border = properties:GetBorderColor()
                number thickness = properties:GetBorderThickness()
                if background = undefined
                    Gradient backgroundGradient
                    Color lightGray = color:CustomColor(0.9, 0.9, 0.9, 1)
                    Color gray = color:LightGray()
                    backgroundGradient:Set(gray, gray, lightGray, lightGray)
                    background = backgroundGradient
                end
                if border = undefined
                    border = color:Black()
                end
                Gradient selectionGradient
                selectionGradient:Set(background:GetTopLeft(), background:GetTopRight(), color:White(), color:White())
                content:SetBorderThickness(cast(integer, thickness))
                content:Initialize(background, border, selectionGradient, border)
                view = content
            else
                view = GetView2D()
            end
        end

        SetView2D(view)

        Icon closeIcon
        
        Color color 
        color = color:White()
        Texture theX = GenerateX(100, 25, 10, color)
        closeIcon:Load(theX)
        closeButton:SetIcon(closeIcon)

        parent:Control:LoadGraphics(properties)
    end

    private action GenerateX(integer size, integer buffer, integer width, Color color) returns Texture
        PixelMap map
        Format format
        format:SetValue(format:RGBA8888)
        map:CreatePixelMap(size, size, format)
        
        Color circle
        circle:SetColor(237.0 / 255.0, 113.0 / 255.0, 107.0 / 255.0, 1.0)
        map:FillCircle(size / 2, size / 2, cast(integer, size * 0.43), circle)
        integer x = buffer
        integer y = buffer
        repeat while x < size - buffer
            map:SetPixel(x, y, color)
            map:SetPixel(x, size - y - 1, color)

            integer theWidth = 1
            repeat while theWidth < width
                if y + theWidth < size - buffer
                    map:SetPixel(x, y + theWidth, color)
                end

                if x + theWidth < size - buffer
                    map:SetPixel(x + theWidth, y, color)
                end
                theWidth = theWidth + 1
            end

            theWidth = 1
            repeat while theWidth < width
                if x + theWidth < size - buffer
                    map:SetPixel(x + theWidth, size - y - 1, color)
                end

                if size  - buffer- y - theWidth - 1 >= 0
                    map:SetPixel(x, size - y - theWidth - 1, color)
                end
                theWidth = theWidth + 1
            end

            x = x + 1
            y = y + 1
        end

        Texture texture
        texture:LoadFromPixelMap(map)
        return texture
    end

    action SetFocusTarget(Item2D target)
        focusTarget = target
    end

    action GetFocusTarget returns Item2D
        return focusTarget
    end
end
