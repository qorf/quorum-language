package Libraries.Interface.Controls.Blocks

use Libraries.Interface.Controls.Blocks.EditField
use Libraries.Interface.Controls.Blocks.BlockLabel
use Libraries.Interface.Options.BlockOptionConstants
use Libraries.Language.Compile.Context.all
use Libraries.Language.Compile.Location
use Libraries.Interface.Controls.Layouts.ScopeBlockLayout
use Libraries.Interface.Controls.Layouts.SingleLineBlockLayout
use Libraries.Language.Compile.Parsing.Token

class CommentBlock is SingleLineBlock
    BlockLabel commentLabel = undefined
    BlockLabel secondLabel = undefined
    EditBox messageBox = undefined
    EditField messageField = undefined
    boolean isMultiLineComment = false
    Token token = undefined

    on create
        BlockOptionConstants constants
        SetInterfaceOptionsKey(constants:COMMENT_KEY)

        SingleLineBlockLayout layout
        SetLayout(layout)

        SetName("Comment Block")
    end

    action UpdateName(integer selectedLine)
        // TO-DO: NYI
        // This should be the entire text read by the screen reader when focused, not just the "output" portion
        SetName("Comment")
    end

    action SetupElements
        BlockEnvironment editor = GetBlockEnvironment()
        number spacing = editor:GetDefaultGlyphWidth()
        SetLeftPadding(spacing * 0.5)
        SetRightPadding(spacing * 0.5)

        if not isMultiLineComment
            BlockLabel bl
            me:commentLabel = bl
            commentLabel:SetFont(editor:GetFont())
            commentLabel:SetFontSize(editor:GetFontSize())
            commentLabel:SetText("//")
            commentLabel:SetRightPadding(spacing)
            Add(commentLabel)

            EditField box
            me:messageField = box
            Add(messageField)

            text endOfLine = ""
            endOfLine = endOfLine:GetLineFeed()
            integer eolSize = endOfLine:GetSize()

//            integer eolSize = editor:GetCode():GetDefaultLineSeparator():GetSize()
//            text endOfLine = ""
//            endOfLine = editor:GetCode():GetDefaultLineSeparator()

            if token:value:EndsWith(endOfLine)
                messageField:Setup("Message", token:startIndex + 2 - GetStartIndex(), 1 + token:endIndex - eolSize - GetStartIndex())
            else
                messageField:Setup("Message", token:startIndex + 2 - GetStartIndex(), 1 + token:endIndex - GetStartIndex())
            end
            messageField:AddBottomLabel("Comment")
        else
            BlockLabel bl
            me:commentLabel = bl
            commentLabel:SetFont(editor:GetFont())
            commentLabel:SetFontSize(editor:GetFontSize())
            commentLabel:SetText("/*")
            commentLabel:SetRightPadding(spacing)
            Add(commentLabel)

            EditBox box
            me:messageBox = box
            Add(messageBox) 
    
            messageBox:Setup("Message", token:startIndex + 2 - GetStartIndex(), 1 + token:endIndex - 2 - GetStartIndex())
            messageBox:AddBottomLabel("Comment")
            messageBox:SetRightPadding(spacing)

            BlockLabel bl2
            me:secondLabel = bl2
            secondLabel:SetFont(editor:GetFont())
            secondLabel:SetFontSize(editor:GetFontSize())
            secondLabel:SetText("*/")
            Add(secondLabel)
        end
        

    end

    action IsMultiLineComment returns boolean
        return isMultiLineComment
    end

    action SetIsMultiLineComment(boolean multi)
        isMultiLineComment = multi
    end

    action GetToken returns Token
        return token
    end

    action SetToken(Token token)
        me:token = token
    end

    private action InitializeLocalIndices
        // Check if we have a valid context and parent block, but the start index hasn't been set yet.
        if token not= undefined and GetParentBlock() not= undefined and GetLocalStartIndex() < 0
            text endOfLine = ""
//            endOfLine = GetEditor():GetCode():GetDefaultLineSeparator()
            endOfLine = endOfLine:GetLineFeed()
            // Set our starting local values using the context and parent.
            SetLocalStartIndex(token:startIndex - GetParentBlock():GetStartIndex())
            if token:value:EndsWith(endOfLine)
                SetLocalEndIndex(token:endIndex + 1 - endOfLine:GetSize() - GetParentBlock():GetStartIndex())
            else
                SetLocalEndIndex(token:endIndex + 1 - GetParentBlock():GetStartIndex())
            end
            SetLocalStartLine(token:startLine - 1 - GetParentBlock():GetStartLine())
            if not isMultiLineComment
                if token:value:EndsWith(endOfLine)
                    SetLocalEndLine(token:endLine - 2 - GetParentBlock():GetStartLine())
                else
                    // assume the comment ended with a EOF
                    SetLocalEndLine(token:endLine - 1 - GetParentBlock():GetStartLine())
                end
            else
                SetLocalEndLine(token:endLine - 1 - GetParentBlock():GetStartLine())
            end
        end
    end
end