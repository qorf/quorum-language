package Libraries.Interface.Controls.Blocks

use Libraries.Game.Graphics.Label
use Libraries.Game.Graphics.ColorGroup
use Libraries.Interface.Options.BlockOptionConstants
use Libraries.Interface.Controls.Blocks.Block
use Libraries.Game.Graphics.Font
use Libraries.Containers.Array
use Libraries.System.File
use Libraries.Interface.Events.FocusEvent

class BlockLabel is Label, BlockItem
    
    integer relativeLineIndex = 0

    BlockOptionConstants constants

    on create
        SetPositionOnBaseLine(false)
    end

    action GetFontColor returns ColorGroup
        if HasColorPropertyOverride("fontColor")
            return parent:Control:GetFontColor()
        end

        Block block = GetBlock()
        if block not= undefined
            return block:GetColorProperty(constants:STATIC_FONT_COLOR)
        else
            return parent:Control:GetFontColor()
        end
    end

    action GetFontOutlineColor returns ColorGroup
        if HasColorPropertyOverride("fontOutlineColor")
            return parent:Control:GetFontOutlineColor()
        end

        Block block = GetBlock()
        if block not= undefined
            return block:GetColorProperty(constants:STATIC_FONT_OUTLINE_COLOR)
        else
            return parent:Control:GetFontOutlineColor()
        end
    end

    action GetFontOutlineThickness returns number
        if HasNumberPropertyOverride("fontOutlineThickness")
            return parent:Control:GetFontOutlineThickness()
        end

        Block block = GetBlock()
        if block not= undefined
            return block:GetNumberProperty(constants:STATIC_FONT_OUTLINE_THICKNESS)
        else
            return parent:Control:GetFontOutlineThickness()
        end
    end

    action HasFontOutlineThickness returns boolean
        Block block = GetBlock()
        boolean result = false

        if block not= undefined
            result = block:GetDefaultLayoutProperties():HasNumberProperty(constants:STATIC_FONT_OUTLINE_THICKNESS)
        end

        return result or parent:Control:HasFontOutlineThickness()
    end
    

    // By default, all BlockItems are a single line in length.
    action GetLineCount returns integer
        return 1
    end

    /*
    This action returns a line index, relative to the start of the block
    this item is attached to. For example, if this returns 0, this item
    is on the same line as where the block starts. If this returns 2, this
    item appears 2 lines below the starting line of the block.
    */
    action GetRelativeLineIndex returns integer
        return relativeLineIndex
    end
    
    action SetRelativeLineIndex(integer relativeLineIndex)
        me:relativeLineIndex = relativeLineIndex
    end
    
action GainedFocus(FocusEvent event)
        CodeEditor editor = GetEditor()
        if editor not= undefined
            editor:OnFocusChange(event)
        end
        
        parent:Label:GainedFocus(event)
    end
    
    action LostFocus(FocusEvent event)
        CodeEditor editor = GetEditor()
        if editor not= undefined
            editor:OnFocusLoss(event)
        end
        
        parent:Label:LostFocus(event)
    end

end