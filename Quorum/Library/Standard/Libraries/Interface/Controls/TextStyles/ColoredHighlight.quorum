package Libraries.Interface.Controls.TextStyles

use Libraries.Game.Graphics.Color
use Libraries.Game.Graphics.ColorGroup
use Libraries.Game.Graphics.Drawable
use Libraries.Game.Graphics.Texture

class ColoredHighlight is TextHighlight
    
    Texture texture = undefined
    ColorGroup color = undefined

    on create
        Color c
        color = c:Yellow()

        SetVisibleWhileSelected(false)
        SetLineHighlight(false)
    end

    /*
    This action returns a Drawable representing the visual content of a text
    highlight of a given width and height.
    */
    action GetDrawable(number width, number height) returns Drawable
        if texture = undefined
            texture = GenerateTexture()
        end

        Drawable drawable
        drawable:Load(texture)
        drawable:SetColor(color)
        drawable:SetSize(width, height)

        return drawable
    end

    /*
    Used the first time this class needs to return a Drawable to generate the
    Texture used by drawables produced by this highlight.
    */
    private action GenerateTexture returns Texture
        Texture newTexture
        Color c
        newTexture:LoadFilledRectangle(1, 1, c:White())
        return newTexture
    end

    action SetColor(ColorGroup color)
        me:color = color
    end

    action GetColor returns ColorGroup
        return color
    end
end