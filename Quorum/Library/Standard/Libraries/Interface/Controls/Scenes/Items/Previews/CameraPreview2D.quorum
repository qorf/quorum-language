package Libraries.Interface.Controls.Scenes.Items.Previews

use Libraries.Interface.Item2D
use Libraries.Game.Graphics.Color
use Libraries.Game.Graphics.Drawable
use Libraries.Interface.Events.MouseEvent
use Libraries.Interface.Controls.Scenes.SceneConstants

class CameraPreview2D is Item2D

    Drawable bottomBorder
    Drawable topBorder
    Drawable leftBorder
    Drawable rightBorder

    number thickness = 0
    Color color

    on create
        SceneConstants constants
        SetProperty(constants:EDITOR_TAG, undefined)
        thickness = constants:CAMERA_PREVIEW_2D_DEFAULT_BORDER_THICKNESS
        color:CopyColor(constants:CAMERA_PREVIEW_2D_DEFAULT_COLOR)

        bottomBorder:LoadFilledRectangle(1, 1, color)
        topBorder:LoadFilledRectangle(1, 1, color)
        leftBorder:LoadFilledRectangle(1, 1, color)
        rightBorder:LoadFilledRectangle(1, 1, color)

        Add(bottomBorder)
        Add(topBorder)
        Add(leftBorder)
        Add(rightBorder)

        SetZ(constants:CAMERA_PREVIEW_2D_DEPTH)
    end

    action SetSize(number width, number height)
        parent:Item2D:SetSize(width, height)
        UpdateBorders()
    end

    action SetWidth(number width)
        parent:Item2D:SetWidth(width)
        UpdateBorders()
    end

    action SetHeight(number height)
        parent:Item2D:SetHeight(height)
        UpdateBorders()
    end

    action SetBorderThickness(number thickness)
        me:thickness = thickness
        UpdateBorders()
    end

    action GetBorderThickness returns number
        return thickness
    end

    private action UpdateBorders
        leftBorder:SetSize(thickness, GetHeight())
        rightBorder:SetSize(thickness, GetHeight())
        bottomBorder:SetSize(GetPaddedWidth(), thickness)
        topBorder:SetSize(GetPaddedWidth(), thickness)

        leftBorder:SetPosition(-thickness, 0)
        rightBorder:SetPosition(GetWidth(), 0)
        bottomBorder:SetPosition(-thickness, -thickness)
        topBorder:SetPosition(-thickness, GetHeight())
    end

    action GetPaddedWidth returns number
        return 2 * thickness + GetWidth()
    end

    action GetPaddedHeight returns number
        return 2 * thickness + GetHeight()
    end

    action ProcessMouseEvent(MouseEvent event)
        /*
        By setting the event handled flag to false, we essentially make
        the preview refuse any incoming mouse events, so it's processed
        by other items in the layer instead.
        */
        event:SetEventHandled(false)
        return now        
    end
end