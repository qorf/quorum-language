package Libraries.Interface.Controls

use Libraries.Containers.Array
use Libraries.Containers.Support.SortableContainer
use Libraries.Game.Graphics.Drawable
use Libraries.Game.Graphics.Painter2D
use Libraries.Interface.Controls.TextStyles.TextHighlight
use Libraries.Game.Graphics.ColorGroup

class TextBoxRenderer is Control

    Array<Array<Drawable>> lines
    Array<Drawable> selectionDrawables
    Array<SortableContainer<TextHighlight, Drawable>> preSelectionHighlights
    Array<SortableContainer<TextHighlight, Drawable>> postSelectionHighlights
    Array<Gutter> gutters
    Drawable caret

    /*
    These variables indicate if the pre/post selection highlights
    need to be sorted before they're rendered.
    */
    boolean sortPreHighlights = false
    boolean sortPostHighlights = false

    Array<Drawable> drawablePool
    Array<SortableContainer<TextHighlight, Drawable>> highlightContainerPool

    // The line number of the first line of text we're displaying.
    integer startLine = 0

    // The array index of the first line. 
    integer startArray = 0

    on create
        SetCustomDrawing(true)
        Add(caret)
    end

    action GetDrawable returns Drawable
        if drawablePool:IsEmpty() = false
            Drawable drawable = drawablePool:RemoveFromEnd()
            return drawable
        else
            Drawable drawable
            drawable:SetAcceptsMouseInput(false)
            return drawable
        end
    end

    action RecycleDrawable(Drawable drawable)
        if drawable = undefined
            return now
        end

        drawablePool:Add(drawable)
    end

    action GetHighlightContainer returns SortableContainer<TextHighlight, Drawable>
        if highlightContainerPool:IsEmpty() = false
            return highlightContainerPool:RemoveFromEnd()
        else
            SortableContainer<TextHighlight, Drawable> container
            return container
        end
    end

    action RecycleHighlightContainer(SortableContainer<TextHighlight, Drawable> container)
        if container = undefined
            return now
        end

        // We don't recycle the Drawables from the highlights because
        // we didn't create them, and so we don't know if they're being reused
        // by whatever the original sources of the highlight drawables are.

        container:Set(undefined, undefined)
        highlightContainerPool:Add(container)
    end

    action Draw(Painter2D painter)
        DrawView(painter)
        DrawContents(painter)
    end

    action DrawView(Painter2D painter)
        if GetView2D() not= undefined
            GetView2D():Draw(painter)
        end
    end

    action DrawContents(Painter2D painter)
        if sortPreHighlights
            preSelectionHighlights:Sort()
            sortPreHighlights = false
        end

        number priority = 0

        if preSelectionHighlights:IsEmpty() = false
            priority = preSelectionHighlights:Get(0):GetSorter():GetRenderPriority()
        end

        integer counter = 0
        repeat while counter < preSelectionHighlights:GetSize()
            SortableContainer<TextHighlight, Drawable> container = preSelectionHighlights:Get(counter)

            // If we find an object is in the wrong position according to its render priority,
            // we'll sort the objects on the next Draw call.
            if container:GetSorter():GetRenderPriority() < priority
                sortPreHighlights = true
            end

            container:GetValue():Draw(painter)
            counter = counter + 1
        end

        counter = 0
        repeat while counter < selectionDrawables:GetSize()
            painter:Draw(selectionDrawables:Get(counter))
            counter = counter + 1
        end

        if sortPostHighlights
            postSelectionHighlights:Sort()
            sortPostHighlights = false
        end

        if postSelectionHighlights:IsEmpty() = false
            priority = postSelectionHighlights:Get(0):GetSorter():GetRenderPriority()
        end

        counter = 0
        repeat while counter < postSelectionHighlights:GetSize()
            SortableContainer<TextHighlight, Drawable> container = postSelectionHighlights:Get(counter)

            // If we find an object is in the wrong position according to its render priority,
            // we'll sort the objects on the next Draw call.
            if container:GetSorter():GetRenderPriority() < priority
                sortPostHighlights = true
            end

            container:GetValue():Draw(painter)
            counter = counter + 1
        end

        counter = 0
        repeat while counter < lines:GetSize()
            integer characterCounter = 0
            Array<Drawable> line = lines:Get(counter)
            repeat while characterCounter < line:GetSize()
                painter:Draw(line:Get(characterCounter))
                characterCounter = characterCounter + 1
            end
            counter = counter + 1
        end

        counter = 0
        repeat while counter < gutters:GetSize()
            gutters:Get(counter):Draw(painter)
            counter = counter + 1
        end

        caret:Draw(painter)
    end

    action AddCharacter(integer line, integer index, Drawable character)
        Array<Drawable> array = GetLine(line)
        if array = undefined
            return now
        end
        Add(character)
        array:Add(index, character)
    end

    action RemoveCharacter(integer line, integer index)
        Array<Drawable> array = GetLine(line)
        if array = undefined
            // If the array is undefined, the line couldn't be fetched, most
            // likely because the line is off the screen.
            return now
        end
        if index >= array:GetSize()
            return now
        end
        Drawable character = array:RemoveAt(index)
        Remove(character)
        RecycleDrawable(character)
    end

    action AddSelectionDrawable(Drawable selection)
        Add(selection)
        selectionDrawables:Add(selection)
    end

    action RemoveSelectionDrawable(Drawable selection)
        selectionDrawables:Remove(selection)
        Remove(selection)
        RecycleDrawable(selection)
    end

    action AddPreSelectionHighlight(TextHighlight highlight, Drawable drawable)
        Add(drawable)
        SortableContainer<TextHighlight, Drawable> container = GetHighlightContainer()
        container:Set(highlight, drawable)
        preSelectionHighlights:Add(container)
        sortPreHighlights = true
    end

    action RemovePreSelectionHighlight(TextHighlight highlight)
        integer counter = 0
        repeat while counter < preSelectionHighlights:GetSize()
            SortableContainer<TextHighlight, Drawable> container = preSelectionHighlights:Get(counter)
            if container:GetSorter() = highlight
                counter = preSelectionHighlights:GetSize()

                preSelectionHighlights:Remove(container)
                Remove(container:GetValue())
            else
                counter = counter + 1
            end
        end
    end

    action AddPostSelectionHighlight(TextHighlight highlight, Drawable drawable)
        Add(drawable)
        SortableContainer<TextHighlight, Drawable> container = GetHighlightContainer()
        container:Set(highlight, drawable)
        postSelectionHighlights:Add(container)
        sortPostHighlights = true
    end

    action RemovePostSelectionHighlight(TextHighlight highlight)
        integer counter = 0
        repeat while counter < postSelectionHighlights:GetSize()
            SortableContainer<TextHighlight, Drawable> container = postSelectionHighlights:Get(counter)
            if container:GetSorter() = highlight
                counter = postSelectionHighlights:GetSize()

                postSelectionHighlights:Remove(container)
                Remove(container:GetValue())
            else
                counter = counter + 1
            end
        end
    end

    action AddGutter(Gutter gutter)
        Add(gutter)
        gutters:Add(gutter)
    end

    action RemoveGutter(Gutter gutter) returns boolean
        Remove(gutter)
        return gutters:Remove(gutter)
    end

    action SetCaret(Drawable newCaret)
        caret = newCaret
    end

    action GetCaret returns Drawable
        return caret
    end

    /*
    This action empties the most common drawables that would need to be cleared
    from the TextBox: the text, selection drawables, and highlighting.

    This DOES NOT remove the Gutters or Caret!
    */
    action Empty
        EmptyLines()
        EmptySelectionDrawables()
        EmptyPreSelectionHighlights()
        EmptyPostSelectionHighlights()
    end

    action EmptyLine(integer index)
        Array<Drawable> array = GetLine(index)
        
        if array not= undefined
            RecycleLine(array)
        end
    end

    action EmptyLines
        integer counter = 0

        repeat while counter < lines:GetSize()
            RecycleLine(lines:Get(counter))
            counter = counter + 1
        end

        // Reset the cyclical array start pointer to 0.
        startArray = 0
    end

    /*
    Recycles all Drawables in the array, and empties the array.
    */
    private action RecycleLine(Array<Drawable> line)
        repeat until line:IsEmpty()
            Drawable drawable = line:RemoveFromFront()
            Remove(drawable)
            RecycleDrawable(drawable)
        end
    end

    action EmptySelectionDrawables
        repeat until selectionDrawables:IsEmpty()
            RemoveSelectionDrawable(selectionDrawables:Get(0))
        end
    end

    action EmptyPreSelectionHighlights
        repeat until preSelectionHighlights:IsEmpty()
            SortableContainer<TextHighlight, Drawable> container = preSelectionHighlights:RemoveFromEnd()
            Remove(container:GetValue())
        end
        sortPreHighlights = false
    end

    action EmptyPostSelectionHighlights
        repeat until postSelectionHighlights:IsEmpty()
            SortableContainer<TextHighlight, Drawable> container = postSelectionHighlights:RemoveFromEnd()
            Remove(container:GetValue())
        end
        sortPostHighlights = false
    end

    action SetLineCount(integer lineCount)
        if lineCount < 0
            return now
        elseif lineCount = 0
            integer counter = 0
            repeat while counter < lines:GetSize()
                RecycleLine(lines:Get(counter))
                counter = counter + 1
            end

            lines:Empty()

            return now
        end

        repeat while lineCount > lines:GetSize()
            Array<Drawable> line
            lines:Add(startArray, line)
            startArray = startArray + 1
        end

        repeat while lineCount < lines:GetSize()
            integer index = startArray - 1
            if index < 0
                index = lines:GetSize() - 1
            end

            Array<Drawable> line = lines:RemoveAt(index)
            RecycleLine(line)
        end
    end

    action GetLineCount returns integer
        return lines:GetSize()
    end

    action SetStartLine(integer line)
        if line = startLine
            return now
        end

        integer difference = line - startLine

        // If the difference is larger than the number of lines we display, then
        // the new section of text we're about to view shares no lines in common
        // with the last view of the text, so we can get rid of the old drawables.
        if (difference > lines:GetSize()) or (-difference > lines:GetSize())
            Empty()
        elseif difference > 0
            integer counter = 0
            repeat while counter < difference
                RecycleLine(lines:Get( (startArray + counter) mod lines:GetSize() ))
                counter = counter + 1
            end

            startArray = (startArray + difference) mod lines:GetSize()
        else
            integer counter = 0
            repeat while counter > difference and startArray - counter < lines:GetSize()
                integer target = startArray - counter
                if target < 0
                    target = lines:GetSize() - target
                end

                RecycleLine(lines:Get(target))
                counter = counter - 1
            end

            startArray = startArray - difference
            if startArray < 0
                startArray = lines:GetSize() - startArray
            end
        end

        startLine = line
    end

    action GetStartLine returns integer
        return startLine
    end

    action GetLine(integer index) returns Array<Drawable>
        integer offset = index - startLine
        if offset < 0 or offset > lines:GetSize() or lines:IsEmpty()
            return undefined
        end

        return lines:Get((startArray + offset) mod lines:GetSize())
    end

    action GetGutters returns Array<Gutter>
        return gutters
    end

    // The TextBoxRenderer will only resize if we explicitly request it, or an
    // underlying component has to.
    action NeedsLayout returns boolean
        return false
    end

    action GetBackgroundColor returns ColorGroup
        if HasColorPropertyOverride("backgroundColor")
            return parent:Control:GetBackgroundColor()
        end

        TextBox box = GetTextBox()
        if box not= undefined
            return box:GetBackgroundColor()
        else
            return parent:Control:GetBackgroundColor()
        end
    end

    action GetBorderColor returns ColorGroup
        if HasColorPropertyOverride("borderColor")
            return parent:Control:GetBorderColor()
        end

        TextBox box = GetTextBox()
        if box not= undefined
            return box:GetBorderColor()
        else
            return parent:Control:GetBorderColor()
        end
    end

    action GetBorderThickness returns number
        if HasNumberPropertyOverride("borderThickness")
            return parent:Control:GetBorderThickness()
        end

        TextBox box = GetTextBox()
        if box not= undefined
            return box:GetBorderThickness()
        else
            return parent:Control:GetBorderThickness()
        end
    end

    action GetTextBox returns TextBox
        Item2D candidate = GetParent()
        repeat until candidate is TextBox or candidate = undefined
            candidate = candidate:GetParent()
        end

        // Returns either the TextBox this is attached to, or undefined
        return cast(TextBox, candidate)
    end

    action GetTopLeftRounding returns number
        if HasNumberPropertyOverride("topLeftRounding")
            return parent:Control:GetTopLeftRounding()
        end

        TextBox box = GetTextBox()
        if box not= undefined
            return box:GetTopLeftRounding()
        else
            return parent:Control:GetTopLeftRounding()
        end
    end

    action HasTopLeftRounding returns boolean
        number value = GetTopLeftRounding()
        return value:IsNotANumber() = false
    end

    action GetTopRightRounding returns number
        if HasNumberPropertyOverride("topRightRounding")
            return parent:Control:GetTopRightRounding()
        end

        TextBox box = GetTextBox()
        if box not= undefined
            return box:GetTopRightRounding()
        else
            return parent:Control:GetTopRightRounding()
        end
    end

    action HasTopRightRounding returns boolean
        number value = GetTopRightRounding()
        return value:IsNotANumber() = false
    end

    action GetBottomLeftRounding returns number
        if HasNumberPropertyOverride("bottomLeftRounding")
            return parent:Control:GetBottomLeftRounding()
        end

        TextBox box = GetTextBox()
        if box not= undefined
            return box:GetBottomLeftRounding()
        else
            return parent:Control:GetBottomLeftRounding()
        end
    end

    action HasBottomLeftRounding returns boolean
        number value = GetBottomLeftRounding()
        return value:IsNotANumber() = false
    end

    action GetBottomRightRounding returns number
        if HasNumberPropertyOverride("bottomRightRounding")
            return parent:Control:GetBottomRightRounding()
        end

        TextBox box = GetTextBox()
        if box not= undefined
            return box:GetBottomRightRounding()
        else
            return parent:Control:GetBottomRightRounding()
        end
    end

    action HasBottomRightRounding returns boolean
        number value = GetBottomRightRounding()
        return value:IsNotANumber() = false
    end
end