package Libraries.Interface.Controls.Charts

use Libraries.Interface.Controls.ControlLabel
use Libraries.Game.Graphics.ColorGroup
use Libraries.Game.Graphics.Font
use Libraries.Game.Graphics.Fonts.FontManager
use Libraries.Game.Graphics.Fonts.FontFamily
use Libraries.System.File

/*
    This class represents labels on chart items. 

    Attribute: Author William Allee
*/
class ChartLabel is ControlLabel, ChartItem
    boolean isBold = false
    on create
        SetInputGroup("Chart")
        SetFocusable(true)
        SetAccessibilityCode(parent:Item:LABEL)
    end

    // Try to get the chart this is attached to via the parent hierarchy.
    action GetParentChart returns Chart
        Item2D parentItem = GetParent()
        repeat until parentItem is Chart or parentItem = undefined
            parentItem = parentItem:GetParent()
        end
        return cast(Chart, parentItem)
    end
    /*
        Gets the font color of the chart label.

        Attribute: Returns ColorGroup or color of the font on a label.
    */
    action GetFontColor returns ColorGroup
if HasColorPropertyOverride("fontColor")
            return parent:Control:GetFontColor()
        end
        
        Chart chart = GetParentChart()
        if chart not= undefined
            return chart:GetFontColor()
        end

        return parent:Control:GetFontColor()
    end
    /*
        Gets the selected text, font color of the chart label.

        Attribute: Returns ColorGroup or color of the font on a selected label.
    */
    action GetSelectionFontColor returns ColorGroup
        Chart chart = GetParentChart()
        if chart not= undefined
            return chart:GetSelectionFontColor()
        end

        return parent:Control:GetFontColor()
    end
    /*
        Gets the selection color of the chart label.

        Attribute: Returns ColorGroup or color of the font on a selected label.
    */
    action GetSelectionColor returns ColorGroup
        Chart chart = GetParentChart()
        if chart not= undefined
            return chart:GetSelectionColor()
        end

        return parent:Control:GetFontColor()
    end
    /*
        This action is called when the chart label gains focus.
    */
    action GainedSelection
        parent:ChartItem:GainedSelection()
        parent:ControlLabel:GainedSelection()
    end
    /*
        This action is called when the chart label loses focus.
    */
    action LostSelection
        parent:ChartItem:LostSelection()
        parent:ControlLabel:LostSelection()
    end
    
    action IsBold returns boolean
        return isBold
    end

    action SetBold(boolean isBold)
        if true
me:isBold = isBold
        Font font = GetFont()
        if font not= undefined
integer size = font:GetSize()
            text name = font:GetName()
            FontManager manager
            FontFamily family = manager:GetFontFamily(name)
            if family not= undefined
                text boldName = family:GetBold()
                if boldName not= undefined
                    Font bold
                        bold:SetSize(100)
                    bold:LoadFont(boldName)
                    SetFont(bold)
                end
            end
        end
        end
    end
end