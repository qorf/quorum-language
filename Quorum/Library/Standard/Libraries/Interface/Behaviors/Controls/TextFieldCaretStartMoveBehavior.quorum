package Libraries.Interface.Behaviors.Controls

use Libraries.Interface.Item
use Libraries.Interface.Controls.TextField
use Libraries.Interface.Events.BehaviorEvent
use Libraries.Interface.Behaviors.Behavior
use Libraries.Interface.Behaviors.Controls.TextFieldCaretMoveBehavior

class TextFieldCaretStartMoveBehavior is Behavior
    
    public constant integer LEFT = 1
    public constant integer RIGHT = 2
 
    integer direction = 0
    boolean selecting = false

    action SetDirection(integer direction)
        me:direction = direction
    end

    action GetDirection returns integer
        return direction
    end

    action SetSelecting(boolean select)
        selecting = select
    end

    action GetSelecting returns boolean
        return selecting
    end

    action Run(BehaviorEvent event)
        Item item = event:GetItem()
        if not (item is TextField)
            return now
        end

        TextField box = cast(TextField, item)

        TextFieldCaretMoveBehavior behavior
        behavior:SetSelecting(selecting)
        behavior:SetTimer(box:GetCaretMoveDelay())

        /*
        Begin advancing the caret in the given direction. It will continue to
        advance in that direction until the key is released or a new movement
        command overrides it.
        */
        box:ShowCaret()
        if direction = LEFT
            behavior:SetDirection(LEFT)
            box:AddBehavior(behavior)
        elseif direction = RIGHT
            behavior:SetDirection(RIGHT)
            box:AddBehavior(behavior)
        end
    end
end