package Libraries.Interface.Behaviors.Controls

use Libraries.Interface.Item
use Libraries.Interface.Controls.TextBox
use Libraries.Interface.Events.BehaviorEvent
use Libraries.Interface.Behaviors.Behavior
use Libraries.Interface.Behaviors.Controls.TextBoxDeleteBehavior

class TextBoxStartDeleteBehavior is Behavior
    
    public constant integer BACKWARD = 1
    public constant integer FORWARD = 2
 
    integer direction = 0

    action SetDirection(integer direction)
        me:direction = direction
    end

    action GetDirection returns integer
        return direction
    end

    action Run(BehaviorEvent event)
        Item item = event:GetItem()
        if not (item is TextBox)
            return now
        end

        TextBox box = cast(TextBox, item)

        TextBoxDeleteBehavior behavior
        behavior:SetTimer(box:GetCaretMoveDelay())

        /*
        Begin deleting in the given direction. Deletion will continue until the
        key is released or another command overrides it.
        */
        if direction = BACKWARD
            behavior:SetDirection(BACKWARD)
            box:AddBehavior(behavior)
        elseif direction = FORWARD
            behavior:SetDirection(FORWARD)
            box:AddBehavior(behavior)
        end
    end
end