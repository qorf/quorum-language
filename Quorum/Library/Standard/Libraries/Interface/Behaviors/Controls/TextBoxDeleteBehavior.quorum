package Libraries.Interface.Behaviors.Controls

use Libraries.Interface.Item
use Libraries.Interface.Controls.TextBox
use Libraries.Interface.Events.BehaviorEvent
use Libraries.Interface.Behaviors.TimedBehavior

class TextBoxDeleteBehavior is TimedBehavior
    
    public constant integer BACKWARD = 1
    public constant integer FORWARD = 2
 
    TextBox textBox = undefined

    integer direction = 0

    action SetDirection(integer direction)
        me:direction = direction
    end

    action GetDirection returns integer
        return direction
    end

    action Run(BehaviorEvent event)
        Item item = event:GetItem()
        if not (item is TextBox)
            return now
        end

        textBox = cast(TextBox, item)
        
        if direction = BACKWARD
            textBox:DeleteBackward()
        elseif direction = FORWARD
            textBox:DeleteForward()
        end
    end

    action Dispose
        if textBox = undefined
            return now
        end
        
        if not textBox:IsFocused()
            return now
        end

        SetTimer(textBox:GetCaretMoveTime())
        textBox:AddBehavior(me)
    end
end