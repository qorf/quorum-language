package Libraries.Interface.Behaviors.Scenes.Selection

use Libraries.Interface.Events.BehaviorEvent
use Libraries.Compute.Vector3
use Libraries.Game.Graphics.OrthographicCamera
use Libraries.Interface.Controls.Scenes.Items.EditorItem2D
use Libraries.Interface.Controls.Scenes.Items.EditorItem3D
use Libraries.Interface.Controls.Scenes.Items.CameraItem2D
use Libraries.Interface.Behaviors.Behavior

class SelectionScaleBehavior is Behavior

    Vector3 scale

    action SetScale(number x, number y, number z)
        scale:Set(x, y, z)
    end

    action SetScale(Vector3 value)
        scale:Set(value)
    end

    action GetScale returns Vector3
        return scale
    end

    action Run(BehaviorEvent event)
        if event:GetItem() is CameraItem2D
            CameraItem2D cameraItem = cast(CameraItem2D, event:GetItem())
            number zoom = cameraItem:GetZoom()

            if (scale:GetX() > 1 and scale:GetY() > 1 and scale:GetZ() > 1)
                cameraItem:SetZoom(zoom + 0.2)
            elseif (scale:GetX() < 1 and scale:GetY() < 1 and scale:GetZ() < 1)
                number value = zoom - 0.2
                if value < 0.2
                    value = 0.2
                end

                cameraItem:SetZoom(value)
            end
            

        elseif event:GetItem() is EditorItem3D
            EditorItem3D item = cast(EditorItem3D, event:GetItem())
            item:Scale(scale)

            // Temporary output while developing.
            if (scale:GetX() > 1 and scale:GetY() > 1 and scale:GetZ() > 1)
                item:NotifyAccessibility("Scaled up")
            elseif (scale:GetX() < 1 and scale:GetY() < 1 and scale:GetZ() < 1)
                item:NotifyAccessibility("Scaled down")
            elseif scale:GetX() > 1
                item:NotifyAccessibility("Scaled X up")
            elseif scale:GetX() < 1
                item:NotifyAccessibility("Scaled X down")
            elseif scale:GetY() > 1
                item:NotifyAccessibility("Scaled Y up")
            elseif scale:GetY() < 1
                item:NotifyAccessibility("Scaled Y down")
            elseif scale:GetZ() > 1
                item:NotifyAccessibility("Scaled Z up")
            elseif scale:GetZ() < 1
                item:NotifyAccessibility("Scaled Z down")
            end
        elseif event:GetItem() is EditorItem2D
            EditorItem2D item = cast(EditorItem2D, event:GetItem())
            item:Scale(scale:GetX(), scale:GetY())

            // Temporary output while developing.
            if (scale:GetX() > 1 and scale:GetY() > 1)
                item:NotifyAccessibility("Scaled up")
            elseif (scale:GetX() < 1 and scale:GetY() < 1)
                item:NotifyAccessibility("Scaled down")
            elseif scale:GetX() > 1
                item:NotifyAccessibility("Scaled X up")
            elseif scale:GetX() < 1
                item:NotifyAccessibility("Scaled X down")
            elseif scale:GetY() > 1
                item:NotifyAccessibility("Scaled Y up")
            elseif scale:GetY() < 1
                item:NotifyAccessibility("Scaled Y down")
            end
        end
    end
end