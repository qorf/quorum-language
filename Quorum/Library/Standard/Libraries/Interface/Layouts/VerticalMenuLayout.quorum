package Libraries.Interface.Layouts

use Libraries.Interface.Item2D
use Libraries.Containers.Array
use Libraries.Interface.Layouts.Layout
use Libraries.Interface.Controls.MenuPanel
use Libraries.Interface.Controls.MenuItem
use Libraries.Interface.Controls.Control

class VerticalMenuLayout is Layout
    
    action Layout(Control container, Array<Item2D> items, number containerWidth, number containerHeight)

        LayoutProperties containerProperties = undefined

        number leftBorder = 0
        number rightBorder = 0
        number topBorder = 0
        number bottomBorder = 0

        if container not= undefined
            containerProperties = container:GetDefaultLayoutProperties()
            if containerProperties not= undefined
                number thickness = containerProperties:GetBorderThickness()

                if containerProperties:HasLeftBorder()
                    leftBorder = thickness
                end
                if containerProperties:HasRightBorder()
                    rightBorder = thickness
                end
                if containerProperties:HasTopBorder()
                    topBorder = thickness
                end
                if containerProperties:HasBottomBorder()
                    bottomBorder = thickness
                end
            end
        end

        number y = bottomBorder
        number longestWidth = 0
        integer i = 0
        MenuPanel panel = undefined
        if container is MenuPanel
            panel = cast(MenuPanel, container)

            repeat while i < items:GetSize()
                Item2D item = items:Get(i)
                if item is MenuItem
                    MenuItem menuItem = cast(MenuItem, item)
                    LayoutProperties properties = menuItem:GetLayoutProperties(containerWidth, containerHeight)

                    if properties:NeedsRendering()
                        menuItem:LoadGraphics(properties)
                    end

                    if menuItem:GetIconWidth() > panel:GetIconRegionWidth()
                        panel:SetIconRegionWidth(menuItem:GetIconWidth())
                    end
                    if menuItem:GetNameWidth() > panel:GetLabelRegionWidth()
                        panel:SetLabelRegionWidth(menuItem:GetNameWidth())
                    end
                    if menuItem:GetShortcutWidth() > panel:GetShortcutRegionWidth()
                        panel:SetShortcutRegionWidth(menuItem:GetShortcutWidth())
                    end
                    if menuItem:GetSubmenuIconWidth() > panel:GetArrowRegionWidth()
                        panel:SetArrowRegionWidth(menuItem:GetSubmenuIconWidth())
                    end
                end
                i = i + 1
            end
        end

        i = items:GetSize()
        repeat while i > 0
            i = i - 1
            Item2D kid = items:Get(i)
            kid:Resize()

            if longestWidth < kid:GetWidth()
                longestWidth = kid:GetWidth()
            end

            kid:SetPosition(leftBorder, y)

            y = y + kid:GetHeight()
        end

        number oldHeight = container:GetHeight()

        container:SetSize(longestWidth + leftBorder + rightBorder, y + topBorder)
        container:SetY(-container:GetHeight())

        repeat while i < items:GetSize()
            Item2D kid = items:Get(i)
            kid:SetWidth(longestWidth)
            i = i + 1
        end
    end

end