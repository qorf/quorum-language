package Libraries.Interface.Controls.Layouts

use Libraries.Interface.Layouts.Layout
use Libraries.Interface.Controls.Control
use Libraries.Containers.Array
use Libraries.Interface.Item2D
use Libraries.Interface.Controls.Blocks.Block
use Libraries.Interface.Controls.Blocks.CodeEditor
use Libraries.Interface.Controls.Blocks.BlockItem
use Libraries.Interface.Controls.Blocks.MultipleLineBlock
use Libraries.Interface.Controls.Blocks.MultipleLineBlockPart
use Libraries.Interface.Controls.Blocks.BlockEnvironment
use Libraries.Interface.Controls.Blocks.MultipleLineBlockConnector

class MultipleLineBlockLayout is Layout

    action Layout(Control container, Array<Item2D> items, number newContainerWidth, number newContainerHeight)
        MultipleLineBlock block = cast(MultipleLineBlock, container)
        //CodeEditor editor = block:GetEditor()
        BlockEnvironment editor = block:GetBlockEnvironment()
        if editor = undefined
            return now
        end

        integer lineRange = block:GetTotalNavigationSize()
        integer blockIndex = block:GetNavigationIndex()
        number connectionTop = 0
        number longestWidth = 0

        Array<MultipleLineBlockPart> blockParts = block:GetBlockParts()
        Array<MultipleLineBlockConnector> connectors = block:GetBlockConnectors()

        integer i = 0
        repeat while i < blockParts:GetSize()
            MultipleLineBlockPart part = blockParts:Get(i)

            if part:GetDefaultLayoutProperties():NeedsRendering()
                part:LoadGraphics(part:GetDefaultLayoutProperties())
            end

            // For now, assume the element is capable of figuring out its own size during a Resize.
            part:Resize()

            number currentHeight = editor:GetLineY(part:GetNavigationEndIndex()) - editor:GetLineY(blockIndex + lineRange - 1)
            part:SetPosition(0, currentHeight)

            if i > 0
                Control connector = connectors:Get(i - 1)
                number y = part:GetY() + part:GetHeight() - 1 * part:GetBorderThickness() - 1
                connector:SetSize(editor:GetScopeIndentationWidth(), connectionTop - y)
                connector:SetPosition(0, y)
            end

            connectionTop = currentHeight + 1 * part:GetBorderThickness() + 1
            
            if part:GetWidth() > longestWidth
                longestWidth = part:GetWidth()
            end

            i = i + 1
        end

        number totalBlockHeight = (editor:GetLineY(blockIndex) + editor:GetLineHeight(blockIndex)) - editor:GetLineY(blockIndex + lineRange - 1)
        block:SetSize(longestWidth - (block:GetScopeDepth() * 4.5 + 1) * editor:GetDefaultGlyphWidth(), totalBlockHeight)
    end
    
end