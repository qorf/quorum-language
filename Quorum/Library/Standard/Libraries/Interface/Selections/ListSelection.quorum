package Libraries.Interface.Selections

use Libraries.Interface.Item
use Libraries.Interface.Controls.List
use Libraries.Interface.Controls.ListItem

class ListSelection is Selection
    
    integer selection = -1

    action Initialize(List list)
        Initialize(list, "")
    end

    action Set(integer index)
        selection = index
        if IsEmpty()
            SetDisplayName("")
        else
            ListItem item = GetList():GetListItem(index)
            text entryText = item:GetText()
            if entryText = ""
                SetDisplayName(item:GetName())
            else
                SetDisplayName(entryText)
            end
        end
    end

    action Set(ListItem item)
        Set(GetList():GetIndexOf(item))
    end

    action GetIndex returns integer
        return selection
    end

    action GetListItem returns ListItem
        List list = GetList()

        if selection < 0 or selection >= list:GetSize()
            return undefined
        end

        return list:GetListItem(selection)
    end

    action GetList returns List
        Item item = GetItem()
        if item is List
            return cast(List, item)
        end
        return undefined
    end

    action IsEmpty returns boolean
        return selection < 0
    end

    action Empty
        selection = -1
    end

end