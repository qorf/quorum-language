package Libraries.Language.Compile

use Libraries.Language.Compile.Context.all
use Libraries.Containers.Stack
use Libraries.Language.Compile.Symbol.all
use Libraries.System.File
use Libraries.Containers.List
use Libraries.Containers.Iterator
use Libraries.Containers.Array

class Parser is QuorumSourceListener
    QualifiedName packageValue = undefined
    Source source
    SymbolTable symbolTable = undefined
    Action currentAction = undefined
    Class currentClass = undefined
    Block currentBlock = undefined
    Block recentlyPoppedBlock = undefined
    integer loopCounter = 0
    Stack<Block> blocks
    Array<QualifiedName> unresolvedUseStatements
    List<Class> classes
    boolean isFirstGenericInFile = false
    Stack<Location> locationStack
    Stack<Documentation> documents
    constant text ILLEGAL_ID = "_"

    /*
        This action returns an iterator of all of the classes parsed
        by this parser.
    */
    action GetClassesIterator returns Iterator<Class>
        return classes:GetIterator()
    end

    action EnterStart(StartContext context) 
        source:SetFile(me:GetFile())
        symbolTable = me:GetSymbolTable()
    end

    action ExitPackageRule(PackageContext context) 
        packageValue = context:name
        if packageValue not= undefined
            packageValue:SetLocation(context:GetLocation())
        end
    end

    action EnterDocumentation(DocumentationContext context)
        text doc = context:document
        if doc not= undefined
            Documentation documentation
            documentation:SetLocation(context:GetLocation())
            documentation:ParseDocumentation(doc)
            documents:Push(documentation)
        end
    end

    action ExitDocumentation(DocumentationContext context) 
    end

    /*
        This action checks if a particular name is legal. By default, names
        that end with an underscore (_) are not legal, as they are used
        internally by the compiler.
    */
    private action CheckName(text name, Location location)
        if name:EndsWith(ILLEGAL_ID)
            CompilerError error
            CompilerErrorType type
            type:SetCurrentType(type:OTHER)
            error:SetCompilerErrorType(type)
            text DQ = ""
            DQ = DQ:GetDoubleQuote()
            error:SetErrorMessage("The name " + DQ + name + DQ + " is not allowed, as I do not understand things that end with an underscore (" + ILLEGAL_ID + ").")
            error:SetLocation(location)
            CompilerErrorManager manager = GetCompilerErrorManager()
            manager:Add(error)
        end
    end

    action EnterFullClassDeclaration(FullClassDeclarationContext context)
        text name = context:className
        CheckName(name, context:GetLocation())
        AddClass(name)
        isFirstGenericInFile = true

        
        if currentClass not= undefined
            boolean isStatic = context:staticClass
            currentClass:SetStatic(isStatic)
        end

        if not documents:IsEmpty()
            Documentation doc = documents:Pop()
            if currentClass not= undefined
                currentClass:SetDocumentation(doc)
            end
        end
    end

    action ExitFullClassDeclaration(FullClassDeclarationContext context) 
        if currentClass = undefined
            return now
        end
        currentClass:SetNameLocation(context:classNameLocation)
        i = 0
        repeat while i < unresolvedUseStatements:GetSize()
            currentClass:AddUnresolvedUseStatement(unresolvedUseStatements:Get(i))
            i = i + 1
        end

        QualifiedName generics = context:name
        
        i = 0
        repeat generics:GetNumberGenerics() times
            currentClass:AddGeneric(generics:GetGeneric(i))
            i = i + 1
        end
        currentClass:SetLocation(context:GetLocation())
        ExitClass()
    end

    action EnterNoActionsNoClass(NoActionsNoClassContext context) 
        File file = GetFile()
        text name = file:GetFileName()
        text extension = file:GetFileExtension()
        integer length = extension:GetSize()
        name = name:GetSubtext(0, name:GetSize() - length - 1)
        CheckName(name, context:GetLocation())
        AddClass(name)
        isFirstGenericInFile = true

        Action act
        currentAction = act
        act:SetParentClass(currentClass)

        Type type
        type:SetToVoid()
        currentAction:SetReturnType(type)
        currentAction:SetName("Main")
        currentAction:SetLocation(context:GetLocation())

        //add it to the class
        CompilerError error = currentClass:Add(currentAction)
        if error not= undefined
            CompilerErrorManager manager = me:GetCompilerErrorManager()
            manager:Add(error)
        end

        BlockContext b
        Location loc = b:GetLocation()
        loc:SetLocation(context:GetLocation())
        EnterBlock(b)
    end

    action ExitNoActionsNoClass(NoActionsNoClassContext context) 
        BlockContext b
        Location loc = b:GetLocation()
        loc:SetLocation(context:GetLocation())
        ExitBlock(b)
        text source = GetSource()
        number size = source:GetSize() + 1

        currentAction:SetIndexEnd(cast(integer, size))
        Block actionBlock = currentAction:GetBlock()
        actionBlock:SetIndexEnd(cast(integer, size))

        ExitClass()
        currentClass:SetLocation(context:GetLocation())
        currentClass:SetIndexEnd(cast(integer, size))
        loc:SetIndexEnd(cast(integer, size))
    end

    action ExitClass
        i = 0
        repeat while i < unresolvedUseStatements:GetSize()
            currentClass:AddUnresolvedUseStatement(unresolvedUseStatements:Get(i))
            i = i + 1
        end

        if currentClass = undefined
            return now
        end

        currentClass:SetFile(me:GetFile())

        if currentClass:GetStaticKey() not= "Libraries.Language.Object"
            //if this class is not Libraries.Language.Object, add this class
            //as a parent
            QualifiedName object
            object:Add("Libraries")
            object:Add("Language")
            object:Add("Object")

            //by default, all classes inherit from Object
            currentClass:AddUnresolvedParent(object)
        end

        Array<QualifiedName> newArray
        unresolvedUseStatements = newArray
        classes:Add(currentClass)
    end

    action EnterActionsNoClass(ActionsNoClassContext context) 
        File file = GetFile()
        text name = file:GetFileName()
        text extension = file:GetFileExtension()
        integer length = extension:GetSize()
        name = name:GetSubtext(0, name:GetSize() - length - 1)
        CheckName(name, context:GetLocation())
        AddClass(name)
        isFirstGenericInFile = true
    end

    action ExitActionsNoClass(ActionsNoClassContext context) 
        ExitClass()
    end

    action ExitUse(UseContext context) 
        QualifiedName name = context:name
        name:SetLocation(context:GetLocation())
        unresolvedUseStatements:Add(name)       
    end

    action EnterGenericStatement(GenericContext context) end
    action ExitGenericStatement(GenericContext context) end

    action EnterGenericDeclaration(GenericDeclarationContext context) end
    action ExitGenericDeclaration(GenericDeclarationContext context) 
    end

    action ExitInheritStatement(InheritStatementContext context) 
        QualifiedName p = context:name
        Array<Type> gen = context:generics
        if currentClass = undefined
            return now
        end
        currentClass:AddUnresolvedParent(context:name)
        currentClass:SetParentGenerics(p:GetStaticKey(), gen)
    end

    action AddClass(text name)
        text key = name
        Class clazz
        if packageValue not= undefined
            key = packageValue:GetStaticKey() + "." + name
            clazz:SetPackage(packageValue)
        else 
            QualifiedName default
            clazz:SetPackage(default)
        end
        clazz:SetName(name)
        clazz:SetFile(GetFile())
        CompilerError error = symbolTable:Add(clazz)
        if error not= undefined
            CompilerErrorManager manager = GetCompilerErrorManager()
            manager:Add(error)
        else 
            currentClass = clazz
        end
    end

    action EnterSystemAction(ActionContext context) 
        Action act

        if context:isPrivate
            act:SetPrivate()
        end
        currentAction = act
        currentAction:SetParentClass(currentClass)
        currentAction:SetIsSystem(true)
        currentAction:SetLocation(context:GetLocation())
        if not documents:IsEmpty()
            Documentation doc = documents:Pop()
            act:SetDocumentation(doc)
        end
    end

    action EnterBlock(BlockContext context) 
        Block block
        if currentBlock not= undefined
            currentBlock:Add(block)
            if not locationStack:IsEmpty()
                Location location = locationStack:Pop()
                location = location:CreateLocationCopy()
                block:SetLocation(location)
            end
        elseif currentAction not= undefined
            currentAction:SetBlock(block)
            Location location = currentAction:CreateLocationCopy()
            block:SetLocation(location)
        end
        currentBlock = block      
        blocks:Push(block)
    end
    
    action ExitBlock(BlockContext context)
        if currentBlock not= undefined
            recentlyPoppedBlock = currentBlock
            currentBlock = currentBlock:GetParentBlock() //might be null, which is fine
        end
    end

    action ExitDetectStatement(DetectStatementContext context)
        text name = context:name
        Variable variable
        variable:SetParentClass(currentClass)
        variable:SetName(name)
        variable:SetIsParameter(false)
        variable:SetVisibleToDebugger(true)

        Type type
        type:SetToObject("Libraries.Language.Errors.Error")
        variable:SetType(type)

        recentlyPoppedBlock:SetExceptionVariable(variable)
    end

    action ExitAlwaysStatement(AlwaysStatementContext context) 
    end

    action ExitSystemAction(ActionContext context) 

    end

    action EnterBlueprintAction(ActionContext context) 
        Action act

        if context:isPrivate
            act:SetPrivate()
        end
        currentAction = act
        currentAction:SetParentClass(currentClass)
        currentAction:SetIsBluePrint(true)
        currentAction:SetLocation(context:GetLocation())
        if not documents:IsEmpty()
            Documentation doc = documents:Pop()
            act:SetDocumentation(doc)
        end
    end

    action ExitBlueprintAction(ActionContext context)
    end

    action EnterCheckStatement(CheckContext context) 
        locationStack:Push(context:GetLocation())
    end

    action EnterDetectStatement(DetectStatementContext context) 
        locationStack:Push(context:GetLocation())
    end

    action EnterAlwaysStatement(AlwaysStatementContext context) 
        locationStack:Push(context:GetLocation())
    end

    action EnterIfStatement(IfContext context) 
        locationStack:Push(context:GetLocation())
    end

    action EnterElseIfStatement(IfContext context) 
        locationStack:Push(context:GetLocation())
    end

    action EnterElseStatement(IfContext context) 
        locationStack:Push(context:GetLocation())
    end

    action EnterLoopStatement(LoopContext context)
        locationStack:Push(context:GetLocation())
        if context:isTimes
            constant text LOOP_NAME = "Loopy"
            Variable variable
            variable:SetParentClass(currentClass)
            variable:SetName(loopCounter + LOOP_NAME)
            variable:SetIsParameter(false)
            variable:SetVisibleToDebugger(false)
            Type type
            type:SetToInteger()
            type:SetIntegerConstant(0)
            variable:SetType(type)
            currentBlock:Add(variable)

            constant text LOOP_NAME_RESULT = "LoopyResult"
            Variable variable2
            variable2:SetParentClass(currentClass)
            variable2:SetName(loopCounter + LOOP_NAME_RESULT)
            variable2:SetIsParameter(false)
            variable2:SetVisibleToDebugger(false)
            Type type2
            type2:SetToInteger()
            type2:SetIntegerConstant(0)
            variable2:SetType(type2)
            currentBlock:Add(variable2)
        end

        loopCounter = loopCounter + 1
    end

    action ExitActionHeader(ActionContext context)
        List<Variable> headerParams = context:parameters
        Iterator<Variable> it = headerParams:GetIterator()
        repeat while it:HasNext()
            Variable param = it:Next()

            //this type is required. If it is undefined, then it means 
            //that something else about this parse is wrong.
            //We also need to make sure they aren't using the same name.
            //Special thanks to Gabriel for finding this issue.
            if param:GetType() not= undefined
                CompilerError error = currentAction:AddParameter(param)
                if error not= undefined
                    CompilerErrorManager manager = me:GetCompilerErrorManager()
                    manager:Add(error)
                end
            end
        end
        
        currentAction:SetReturnType(context:returnType)
        currentAction:SetName(context:actionName)
        currentAction:SetHeaderLocation(context:GetLocation())
        currentAction:SetNameLocation(context:actionNameLocation)
        CheckName(context:actionName, context:GetLocation())

        //add it to the class
        if currentClass = undefined
            return now
        end
        CompilerError error = currentClass:Add(currentAction)
        if error not= undefined
            CompilerErrorManager manager = me:GetCompilerErrorManager()
            manager:Add(error)
        end
    end

    action EnterConstructor(ConstructorContext context) 
        Action act
        currentAction = act
        act:SetParentClass(currentClass)
        currentAction:SetName("on create")
        currentAction:SetLocation(context:GetLocation())
        Type type
        type:SetToVoid()
        act:SetReturnType(type)
        
        if currentClass = undefined
            return now
        end

        if currentClass:GetConstructor() not= undefined
            CompilerError error
            error:SetLocation(context:GetLocation())
            CompilerErrorType cet
            cet:SetCurrentType(cet:METHOD_DUPLICATE)
            error:SetCompilerErrorType(cet)
            error:SetErrorMessage("I noticed that there is a second constructor defined, but I am not sure " +
                "which one is supposed to setup the object.")
            CompilerErrorManager manager = me:GetCompilerErrorManager()
            manager:Add(error)
        else
            currentClass:SetConstructor(act)
        end
        if not documents:IsEmpty()
            Documentation doc = documents:Pop()
            act:SetDocumentation(doc)
        end
    end

    action ExitConstructor(ConstructorContext context) 
        currentAction = undefined
    end

    action EnterAction(ActionContext context) 
        Action act
        if context:isPrivate
            act:SetPrivate()
        end

        currentAction = act
        act:SetParentClass(currentClass)
        currentAction:SetLocation(context:GetLocation())
        if not documents:IsEmpty()
            Documentation doc = documents:Pop()
            act:SetDocumentation(doc)
        end
    end

    action ExitAction(ActionContext context) 
    end

    action ExitReturnStatement(ReturnContext context)
    end

    action ExitStatement(StatementContext context) 
    end

    action ExitNormalAssignment(NormalAssignmentContext context)
        Variable variable
        variable:SetParentClass(currentClass)
        variable:SetLocation(context:variableLocation)
        if context:isPublic
            variable:SetPublic()
        end

        if context:isPrivate
            variable:SetPrivate()
        end

        if context:isConstant
            variable:SetConstant(true)
        end

        variable:SetHasExplicitModifier(context:hasModifier)
        variable:SetName(context:name)
        CheckName(context:name, context:GetLocation())
        variable:SetIsParameter(false)
        variable:SetType(context:leftHandSide) //guaranteed to exist for this rule
        //the right hand side is not guaranteed to exist for this
        //particular rule. If it is there, we don't care about it in the 
        //parser anyway, because we can't type check in the first pass
        CompilerError error = undefined
        if currentBlock = undefined //we're in a field initiatlization block
            if currentClass = undefined
                //this is an error and has already been caught.
                return now
            end
            if currentClass = undefined
                return now
            end
            error = currentClass:Add(variable)
            if not documents:IsEmpty()
                Documentation doc = documents:Pop()
                variable:SetDocumentation(doc)
            end
        else //we're in the block of an action
            error = currentBlock:Add(variable)
        end

        if error not= undefined
            CompilerErrorManager manager = GetCompilerErrorManager()
            manager:Add(error)
        end
    end

    action EnterParentAssignment(ParentAssignmentContext context) end
    action ExitParentAssignment(ParentAssignmentContext context) end

    action EnterObjectAssignment(ObjectAssignmentContext context) end
    action ExitObjectAssignment(ObjectAssignmentContext context) end

    action EnterNoTypeAssignment(NoTypeAssignmentContext context) end
    action ExitNoTypeAssignment(NoTypeAssignmentContext context) 
        Variable variable
        variable:SetParentClass(currentClass)
        variable:SetLocation(context:variableLocation)
        variable:SetName(context:name)
        CheckName(context:name, context:GetLocation())
        variable:SetIsParameter(false)

        Variable isDefinedAlready = undefined
        if currentBlock = undefined //we're in a field initiatlization block
            if currentClass = undefined
                return now
            end
            isDefinedAlready = currentClass:GetVariable(context:name)
        else //we're in the block of an action
            if currentBlock:GetParentAction() = undefined
                a = 5
            end
            isDefinedAlready = currentBlock:GetVariable(context:name)
        end

        CompilerError error = undefined
        if isDefinedAlready = undefined
            if currentBlock = undefined //we're in a field initiatlization block
                if currentClass = undefined
                    return now
                end
                error = currentClass:Add(variable)
            else //we're in the block of an action
                error = currentBlock:Add(variable)
                if not documents:IsEmpty()
                    Documentation doc = documents:Pop()
                    variable:SetDocumentation(doc)
                end
            end
        else 
            isDefinedAlready:AddUsage(context:GetLocation())
        end

        if error not= undefined
            CompilerErrorManager manager = GetCompilerErrorManager()
            manager:Add(error)
        end
    end
end