package Libraries.Language.Compile.Translate

use Libraries.Language.Compile.Symbol.Type

class ObjectToPrimitiveNumberCast is ExplicitCastOpcode
    JavaBytecodeOpcodes opcodes

    action Write
        JavaBytecodeMethodWriter methodWriter = GetMethodWriter()
        VisitLineNumber()
        QuorumOpcode opcodeToCast = GetOpcodeToCast()
        if opcodeToCast not= undefined
            opcodeToCast:Write()
        end
        methodWriter:VisitMethodInvoke(opcodes:GetInvokeStatic(), "plugins/quorum/Libraries/Language/Types/Number", "ConvertObjectToNumber",
                "(Lquorum/Libraries/Language/Object_;)D", false)
    end

    action WritePython returns text
        text result = ""
        QuorumOpcode opcodeToCast = GetOpcodeToCast()
        if opcodeToCast not= undefined
            result = result + WrapGetValuePython(opcodeToCast)
        else
            result = ""
        end
        return result
    end
    
    action WriteJavaScript returns text
        text result = ""
        QuorumOpcode opcodeToCast = GetOpcodeToCast()
        if opcodeToCast not= undefined
            result = result + WrapGetValue(opcodeToCast)
        else 
            result = ""
        end
        return result
    end

    action GetTemplateCopy returns QuorumOpcode
        ObjectToPrimitiveNumberCast op
        op:SetType(me:GetType())
        return op
    end
end