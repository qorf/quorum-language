package Libraries.Language.Compile.Translate

use Libraries.Language.Compile.Symbol.Type
use Libraries.Language.Compile.Translate.JavaBytecodeOpcodes
use Libraries.Language.Compile.Symbol.Operation
use Libraries.Language.Compile.Interpreter.Runtime
use Libraries.Language.Compile.Interpreter.Result

class ComparisonOpcode is BinaryOpcode 
    JavaBytecodeOpcodes opcodes
    public boolean isTextCompare = false
    public boolean isTextEquality = false
    public boolean isRightUndefined = false
    public boolean isLeftUndefined = false
    public integer textCompareValue = -2

    action Write
        VisitLineNumber()
        JavaBytecodeLabel jumpNotEqual
        JavaBytecodeLabel jumpPastLabel
        
        if not (isTextEquality and isLeftUndefined)
            QuorumOpcode left = me:GetLeftOpcode()
            left:Write()
            WriteLeft()
        end

        //Do not write the right hand side if it is undefined and this is a text compare
        if not (isTextEquality and isRightUndefined)
            QuorumOpcode right = me:GetRightOpcode()
            right:Write()
            WriteRight()
        end

        boolean isInIf = me:GetIsIfExpression()

        JavaBytecodeMethodWriter methodWriter = GetMethodWriter()

        if isTextEquality
            //If either side is null, skip this and the final block will be called
            //which includes an appropriate not null opcode.
            //
            if not isRightUndefined and not isLeftUndefined
                methodWriter:VisitMethodInvoke(opcodes:GetInvokeVirtual(), "java/lang/String", "compareTo", "(Ljava/lang/String;)I", false)
                //methodWriter:VisitMethodInvoke(opcodes:GetInvokeVirtual(), "java/lang/String", "equals", "(Ljava/lang/Object;)Z", false)
            end
        elseif isTextCompare
            methodWriter:VisitMethodInvoke(opcodes:GetInvokeVirtual(), "java/lang/String", "compareTo", "(Ljava/lang/String;)I", false)
        end

        
        
        if not isInIf
            methodWriter:VisitJump(GetJavaOpcode(), jumpNotEqual)
            methodWriter:VisitConstant(1)
            methodWriter:VisitJump(opcodes:GetGoto(), jumpPastLabel)
            methodWriter:VisitLabel(jumpNotEqual)
            methodWriter:VisitConstant(0)
            methodWriter:VisitLabel(jumpPastLabel)
        end
    end

    action WriteLeft
        integer op = GetJavaLeftOpcode()
        if op not= -1
            JavaBytecodeMethodWriter methodWriter = GetMethodWriter()
            methodWriter:VisitInstruction(op)
            //if there is a second one, do that as well
            integer op2 = GetJavaLeftOpcode2()
            if op2 not= -1
                methodWriter:VisitInstruction(op2)
            end
        end
    end

    action WriteRight
        integer op = GetJavaRightOpcode()
        if op not= -1
            JavaBytecodeMethodWriter methodWriter = GetMethodWriter()
            methodWriter:VisitInstruction(op)
            //if there is a second one, do that as well
            integer op2 = GetJavaRightOpcode2()
            if op2 not= -1
                methodWriter:VisitInstruction(op2)
            end
        end
    end
    
    action GetTemplateCopy returns QuorumOpcode
        ComparisonOpcode op
        op:SetJavaOpcode(GetJavaOpcode())
        op:SetJavaLeftOpcode(GetJavaLeftOpcode())
        op:SetJavaRightOpcode(GetJavaRightOpcode())
        op:SetJavaLeftOpcode2(GetJavaLeftOpcode2())
        op:SetJavaRightOpcode2(GetJavaRightOpcode2())
        op:isTextCompare = isTextCompare
        op:textCompareValue = textCompareValue
        op:isTextEquality = isTextEquality
        op:isLeftUndefined = isLeftUndefined
        op:isRightUndefined = isRightUndefined

        Operation newOp
        Operation oldOp = me:GetOperation()
        newOp:SetOperation(oldOp:GetOperation())
        op:SetOperation(newOp)
        return op
    end
end