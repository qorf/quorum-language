package Libraries.Language.Compile.Translate

use Libraries.Language.Compile.Symbol.Type

class ExplicitCastOpcode is QuorumOpcode
    QuorumOpcode opcodeToCast = undefined
    JavaBytecodeOpcodes opcodes

    action SetBytecodeWriteOffsetToAllChildren(integer offset)
        SetBytecodeWriteOffset(offset)
        if opcodeToCast not= undefined
            opcodeToCast:SetBytecodeWriteOffset(offset)
        end
    end

    action SetMethodWriterToAllChildren(JavaBytecodeMethodWriter writer)
        SetMethodWriter(writer)
        if opcodeToCast not= undefined
            opcodeToCast:SetMethodWriterToAllChildren(writer)
        end
    end

    action Write
        JavaBytecodeMethodWriter methodWriter = GetMethodWriter()
        if opcodeToCast not= undefined
            opcodeToCast:Write()
        end
        Type type = GetType()
        methodWriter:VisitType(opcodes:GetCheckCast(), type:ConvertStaticKeyToBytecodeInterfacePath())
    end

    action GetOpcodeToCast returns QuorumOpcode
        return opcodeToCast
    end

    action SetOpcodeToCast(QuorumOpcode op)
        opcodeToCast = op
    end
    
    action WriteJavaScript returns text
        text DQ = ""
        DQ = DQ:GetDoubleQuote()
        if opcodeToCast not= undefined
            Type type = GetType()
            return "global_CheckCast(" + opcodeToCast:WriteJavaScript() + ", "
                + DQ + type:GetStaticKey() + DQ + ")"
        else 
            return ""
        end
    end

    action WrapGetValue(QuorumOpcode op) returns text
        text DQ = ""
        DQ = DQ:GetDoubleQuote()
        return "global_GetValue_(" + op:WriteJavaScript() + ", " + DQ + GetType():GetStaticKey() + DQ + ")"
    end

    action GetTemplateCopy returns QuorumOpcode
        ExplicitCastOpcode op
        op:SetType(me:GetType())
        return op
    end
end