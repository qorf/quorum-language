package Libraries.Language.Compile.Translate

use Libraries.Language.Compile.Symbol.Type
use Libraries.Language.Compile.Symbol.Variable
use Libraries.Language.Compile.Symbol.Class

class AssignmentOpcode is QuorumOpcode
    integer opcode = -1
    integer opcode2 = -1
    QuorumOpcode rightHandSide = undefined
    QuorumOpcode implicitCast = undefined
    Variable variable = undefined
    JavaBytecodeOpcodes opcodes
    constant integer ME = 0
    Class clazz = undefined
    boolean isObjectField = false
    Variable field = undefined
    Class fieldHolder = undefined
    Class parentWithField = undefined

    boolean isInFieldBlock = false
    boolean isNewAssignment = false

    action IsInFieldBlock returns boolean
        return isInFieldBlock
    end

    action SetIsInFieldBlock(boolean block)
        isInFieldBlock = block
    end

    
    action IsNewAssignment returns boolean
        return isNewAssignment
    end

    action SetIsNewAssignment(boolean block)
        isNewAssignment = block
    end

    action GetImplicitCast returns QuorumOpcode
        return implicitCast
    end

    action SetImplicitCast(QuorumOpcode opcode)
        implicitCast = opcode
    end

    action SetParentWithField(Class myParent)
        parentWithField = myParent
    end

    action GetParentWithField returns Class
        return parentWithField
    end

    action GetFieldHolder returns Class
        return fieldHolder
    end

    action SetFieldHolder(Class fieldHolder)
        me:fieldHolder = fieldHolder
    end

    action IsObjectField returns boolean
        return isObjectField
    end

    action SetIsObjectField(boolean field)
        isObjectField = field
    end

    action GetField returns Variable
        return field
    end

    action SetField(Variable field)
        me:field = field
    end

    action GetClass returns Class
        return clazz
    end

    action SetClass(Class clazz)
        me:clazz = clazz
    end

    action GetVariable returns Variable
        return variable
    end

    action SetVariable(Variable var)
        variable = var
    end

    action GetRightHandSide returns QuorumOpcode
        return rightHandSide
    end

    action SetRightHandSide(QuorumOpcode opcode)
        rightHandSide = opcode
    end

    action GetJavaOpcode returns integer
        return opcode
    end

    action SetJavaOpcode(integer op)
        opcode = op
    end

    action GetJavaOpcode2 returns integer
        return opcode2
    end

    action SetJavaOpcode2(integer op)
        opcode2 = op
    end

    action SetBytecodeWriteOffsetToAllChildren(integer offset)
        SetBytecodeWriteOffset(offset)
        if rightHandSide not= undefined
            rightHandSide:SetBytecodeWriteOffsetToAllChildren(offset)
        end
    end

    action SetMethodWriterToAllChildren(JavaBytecodeMethodWriter writer)
        SetMethodWriter(writer)
        if rightHandSide not= undefined
            rightHandSide:SetMethodWriterToAllChildren(writer)
        end
    end

    private action WriteImplicitIfExists
        if implicitCast not= undefined
            implicitCast:Write()
        end
    end

    action Write 
        JavaBytecodeMethodWriter methodWriter = GetMethodWriter()
        VisitLineNumber()

        if parentWithField not= undefined
            //push on me
            methodWriter:VisitVariable(opcodes:GetObjectLoad(), ME)

            //check to see if this is a call to push on a field from a parent variable
            if field not= undefined
                text owner = clazz:ConvertStaticKeyToBytecodePath()
                text name = field:GetName()

                Type type = field:GetType()
                text description = type:ConvertTypeToBytecodeSignatureInterface() //:ConvertStaticKeyToBytecodePathTypeName()
                methodWriter:VisitField(opcodes:GetField(), owner, name, description)
            else
                //push on the parent
                text owner = clazz:ConvertStaticKeyToBytecodePath()
                text name = parentWithField:ConvertStaticKeyToParentFieldName()
                text description = parentWithField:ConvertStaticKeyToBytecodePathTypeName()
                methodWriter:VisitField(opcodes:GetField(), owner, name, description)
            end
            //write the right hand side
            rightHandSide:Write()
            
            //push any opcodes listed for assignment
            if opcode2 not= -1
                methodWriter:VisitInstruction(opcode2)
            end
            
            WriteImplicitIfExists()

            //do final assignment. This is guaranteed to be a field
            Type type = variable:GetType()
            path = parentWithField:ConvertStaticKeyToBytecodeInterfacePath()
            setterName = parentWithField:ConvertToActionNameFromField(variable, false)
            methodName = type:ConvertToSignatureFromFieldInterface(false)
            methodWriter:VisitMethodInvoke(opcodes:GetInvokeInterface(), 
                path, setterName, methodName, true)
            return now
        end

        if not isObjectField
            if variable not= undefined
                if variable:IsField()
                    methodWriter:VisitVariable(opcodes:GetObjectLoad(), ME)
                    rightHandSide:Write()
                    Type type = variable:GetType()
                    if opcode2 not= -1
                        methodWriter:VisitInstruction(opcode2)
                    end
                    WriteImplicitIfExists()
                    methodWriter:VisitField(opcodes:GetPutField(), 
                        clazz:ConvertStaticKeyToBytecodePath(), variable:GetName(), 
                        type:ConvertTypeToBytecodeSignatureInterface())
                else
                    rightHandSide:Write()
                    if opcode2 not= -1
                        methodWriter:VisitInstruction(opcode2)
                    end
                    WriteImplicitIfExists()
                    methodWriter:VisitVariable(opcode, variable:GetBytecodeLocation() + CalculateBytecodeOffset(variable))
                end
            end

            return now
        end


        Type type = field:GetType()
        if variable:IsField()
            methodWriter:VisitVariable(opcodes:GetObjectLoad(), ME)
            Class isIn = variable:GetParentClass()
            path = isIn:ConvertStaticKeyToBytecodeInterfacePath()
            getterName = isIn:ConvertToActionNameFromField(variable, true)

            Type t = variable:GetType()
            methodName = t:ConvertToSignatureFromFieldInterface(true)
            methodWriter:VisitMethodInvoke(opcodes:GetInvokeInterface(), 
                path, getterName, methodName, true)
        else
            methodWriter:VisitVariable(opcodes:GetObjectLoad(), variable:GetBytecodeLocation() + CalculateBytecodeOffset(variable))
        end

        rightHandSide:Write()
        path = fieldHolder:ConvertStaticKeyToBytecodeInterfacePath()
        setterName = fieldHolder:ConvertToActionNameFromField(field, false)
        methodName = type:ConvertToSignatureFromFieldInterface(false)
        if opcode2 not= -1
            methodWriter:VisitInstruction(opcode2)
        end

        WriteImplicitIfExists()
        methodWriter:VisitMethodInvoke(opcodes:GetInvokeInterface(), 
            path, setterName, methodName, true)
    end

    action WritePython returns text 
        text result = ""

        TranslateConstants constants
        text tabs = constants:GetTabText()

        if isObjectField
            if variable:IsField()
                Class p = variable:GetParentClass()
                result = result + "self." + p:ConvertToActionNameFromField(variable, true) + "()"
            else
                result = result + variable:GetJavaScriptName()
            end

            if field:IsField()
                Class p = field:GetParentClass()
                result = result + "." + p:ConvertToActionNameFromField(field, false) + "("
                result = result + rightHandSide:WritePython() + ");"
            else
                result = result + "." + field:GetJavaScriptName() + " = " + rightHandSide:WritePython() + ";"
            end

            EOL = ""
            EOL = EOL:GetCarriageReturn() + EOL:GetLineFeed()
            result = result + EOL
            return result
        end
        if variable:IsField()
            result = result + "self." 
            if parentWithField not= undefined
                if field not= undefined
                    Class p = field:GetParentClass()
                    result = result + p:ConvertToActionNameFromField(field, true) + "()" + "."
                end
                result = result + parentWithField:ConvertStaticKeyToParentFieldName() + "."
            end
            result = result + tabs + variable:GetJavaScriptName()
        else
            result = result + tabs + variable:GetJavaScriptName() 
        end

        //if the variable is a number and the RHS is an integer, python won't make this implicit, so cast
        Type lType = variable:GetType()
        Type rType = rightHandSide:GetType()
        if lType:IsNumber() and rType:IsInteger()
            result = result + " = float(" + rightHandSide:WritePython() + ")"
        else
            result = result + " = " + rightHandSide:WritePython()
        end
        EOL = ""
        EOL = EOL:GetCarriageReturn() + EOL:GetLineFeed()
        result = result + EOL
        return result
    end

    action WriteJavaScript returns text 
        text result = ""

        if isObjectField
            if variable:IsField()
                Class p = variable:GetParentClass()
                result = result + "this." + p:ConvertToActionNameFromField(variable, true) + "()"
            else
                result = result + variable:GetJavaScriptName()
            end

            if field:IsField()
                Class p = field:GetParentClass()
                result = result + "." + p:ConvertToActionNameFromField(field, false) + "("
                result = result + rightHandSide:WriteJavaScript() + ");"
            else
                result = result + "." + field:GetJavaScriptName() + " = " + rightHandSide:WriteJavaScript() + ";"
            end

            EOL = ""
            EOL = EOL:GetCarriageReturn() + EOL:GetLineFeed()
            result = result + EOL
            return result
        end
        if variable:IsField()
            result = result + "this." 
            if parentWithField not= undefined
                if field not= undefined
                    Class p = field:GetParentClass()
                    result = result + p:ConvertToActionNameFromField(field, true) + "()" + "."
                end
                result = result + parentWithField:ConvertStaticKeyToParentFieldName() + "."
            end
            result = result + variable:GetJavaScriptName()
        else
            if isNewAssignment
                result = result + "var " 
            end
            result = result + variable:GetJavaScriptName() 
        end
        result = result + " = " + rightHandSide:WriteJavaScript() + ";"
        EOL = ""
        EOL = EOL:GetCarriageReturn() + EOL:GetLineFeed()
        result = result + EOL
        return result
    end

    action GetTemplateCopy returns QuorumOpcode
        AssignmentOpcode op
        Type t = GetType()
        me:SetType(t:Copy())
        op:SetJavaOpcode(me:GetJavaOpcode())
        op:SetJavaOpcode2(me:GetJavaOpcode2())
        op:opcode = opcode
        return op
    end
end