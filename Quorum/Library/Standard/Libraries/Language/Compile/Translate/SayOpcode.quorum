package Libraries.Language.Compile.Translate

use Libraries.Language.Compile.Symbol.Type
use Libraries.Containers.List
use Libraries.Containers.Iterator
use Libraries.Language.Compile.Symbol.Action
use Libraries.Language.Compile.Translate.JavaBytecodeOpcodes

class SayOpcode is QuorumOpcode
    QuorumOpcode opcode = undefined
    JavaBytecodeMethodWriter methodWriter = undefined
    JavaBytecodeOpcodes opcodes
    QuorumBytecodeConverter converter

    action SetBytecodeWriteOffsetToAllChildren(integer offset)
        SetBytecodeWriteOffset(offset)
        if opcode not= undefined
            opcode:SetBytecodeWriteOffsetToAllChildren(offset)
        end
    end

    action SetMethodWriterToAllChildren(JavaBytecodeMethodWriter writer)
        SetMethodWriter(writer)
        if opcode not= undefined
            opcode:SetMethodWriterToAllChildren(writer)
        end
    end

    action GetOpcode returns QuorumOpcode
        return opcode
    end

    action SetOpcode(QuorumOpcode op)
        opcode = op
    end

    action GetMethodWriter returns JavaBytecodeMethodWriter
        return methodWriter
    end

    action SetMethodWriter(JavaBytecodeMethodWriter writer)
        methodWriter = writer
    end

    action Write
        VisitLineNumber()
        Type type = opcode:GetType()
        opcode:Write()
        methodWriter:VisitMethodInvoke(opcodes:GetInvokeStatic(), "plugins/quorum/Libraries/Sound/Speech", "StaticSay",
                "(" + converter:ConvertTypeToBytecodeSignature(type) + ")V", false)
    end

    /* This is incorrect and needs to be changed to speech. Need to check on this. */
    action WritePython returns text
        EOL = ""
        quote = EOL:GetDoubleQuote()
        EOL = EOL:GetCarriageReturn() + EOL:GetLineFeed()
        TranslateConstants constants
        text tabs = constants:GetTabText()

        opcodeResult = opcode:WritePython()
        text result = ""
        Type type = opcode:GetType()
        if type:IsBoolean()
            result = tabs + "print(__builtins__.str(" + opcodeResult + ").lower())" + EOL
        else
            result = tabs + "print(" + opcodeResult + ")" + EOL
        end
        return result
    end

    action WriteJavaScript returns text
        text rhs = opcode:WriteJavaScript()
        text utterance = "$utterance = new SpeechSynthesisUtterance(" + rhs + ");"
        JSOutput = utterance + "speechSynthesis.speak($utterance);"
        return JSOutput
    end

    action GetTemplateCopy returns QuorumOpcode
        SayOpcode op
        return op
    end
end