package Libraries.Compute.MatrixTransform

use Libraries.Compute.MatrixTransform.MatrixTransform
use Libraries.Compute.Matrix
use Libraries.Containers.Array

    /*
        This is a MatrixTransform class to perform a matrix multiply operation on two matrices. 

        Attribute Returns A new matrix after the operation is performed.

        Attribute: Example

        use Libraries.Compute.Matrix
        use Libraries.Compute.MatrixTransform.Multiply

        class Main 
            action Main
                Matrix matrix
                matrix:Fill(2, 3, 10.0)
                Matrix matrix2
                matrix2:Fill(3, 2, 2.0)
                Multiply multiply
                multiply:SetValue(matrix2)
                Matrix result = multiply:Transform(matrix)
                output result:ToText()
            end
        end
    */
class Multiply is MatrixTransform
    private Matrix values
    /*
        This method sets the a matrix of values for the transform function to apply. 

        Attribute Parameter The value matrix to apply in the transform.

        Attribute: Example

        use Libraries.Compute.Matrix
        use Libraries.Compute.MatrixTransform.Multiply

        class Main 
            action Main
                Matrix matrix2
                matrix2:Fill(3, 2, 2.0)
                Multiply multiply
                multiply:SetValue(matrix2)
                output multiply:GetValueMatrix():ToText()
            end
        end
    */
    action SetValue(Matrix values)
        me:values = values
    end

    /*
        This method gets the matrix of values that the transform function will apply. 

        Attribute Parameter The value to apply in the transform.

        Attribute: Example

        use Libraries.Compute.Matrix
        use Libraries.Compute.MatrixTransform.Multiply

        class Main 
            action Main
                Matrix matrix2
                matrix2:Fill(3, 2, 2.0)
                Multiply multiply
                multiply:SetValue(matrix2)
                output multiply:GetValueMatrix():ToText()
            end
        end
    */
    action GetValueMatrix returns Matrix
        return values
    end

    /*
        This is a MatrixTransform class to perform a matrix multiply operation on two matrices. 

        Attribute Returns A new matrix after the operation is performed.

        Attribute: Example

        use Libraries.Compute.Matrix
        use Libraries.Compute.MatrixTransform.Multiply

        class Main 
            action Main
                Matrix matrix
                matrix:Fill(2, 3, 10.0)
                Matrix matrix2
                matrix2:Fill(3, 2, 2.0)
                Multiply multiply
                multiply:SetValue(matrix2)
                Matrix result = multiply:Transform(matrix)
                output result:ToText()
            end
        end
    */
    action Transform(Matrix matrix) returns Matrix
        Matrix newMatrix
        newMatrix:SetSize(matrix:GetRows(), values:GetColumns())
        row = 0
        repeat matrix:GetRows() times
            col = 0
            repeat values:GetColumns() times
                number sum = 0.0
                i = 0
                repeat matrix:GetColumns() times
                    sum = sum + matrix:Get(row,i) * values:Get(i,col)
                    i = i + 1
                end
                newMatrix:Set(row, col, sum)
                col = col + 1
            end
            row = row + 1
        end
        return newMatrix
    end
end