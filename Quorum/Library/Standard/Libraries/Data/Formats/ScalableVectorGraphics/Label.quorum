package Libraries.Data.Formats.ScalableVectorGraphics

/*
    The Label class is an implementation of text in the SVG standard. Like other
    ScalableVectorGraphicsObjects the Label class inherits global attributes but it also has
    its own attributes.

    Attribute: Author Gabriel Contreras

    Attribute: Example

    Libraries.Data.Formats.ScalableVectorGraphics.all

    // make text
    Label label

    label:SetPosition(50, 50)
    label:SetText("words words words")
    label:SetTextAnchor("middle")
    label:SetFontSize("12pt")

*/
class Label is ScalableVectorGraphicsObject
    constant text objectName = "text"
    private text labelText = ""
    private text x = ""
    private text y = ""
    private text dx = ""
    private text dy = ""
    private integer rotate = 0
    private text textLength = ""
    private text fontSize = ""
    private text fontFamily = ""
    private text textAnchor = ""
    private text transform = ""

    on create
        SetIsContainer(true)
    end

    /*
        This action writes out all of the necessary tags and attributes that make up this
        label. This action will be called automatically by the parent ScalableVectorGraphics,
        as such this action does not need to be used directly by the user.

        Attribute: Returns the formatted string of all tags and attributes for this shape

    */
    action ToText returns text
        result = ""
        newLine = result:GetLineFeed()
        dq = result:GetDoubleQuote()
        result = "<" + objectName
        if x not= ""
            result = result + " x=" +dq+ x +dq
        end
        if y not= ""
            result = result + " y=" +dq+ y +dq
        end
        if dx not= ""
            result = result + " dx=" +dq+ dx +dq
        end
        if dy not= ""
            result = result + " dy=" +dq+ dy +dq
        end
        if rotate not= 0
            result = result + " rotate=" +dq+ rotate +dq
        end
        if textLength not= ""
            result = result + " textLength=" +dq+ textLength +dq
        end
        if fontSize not= ""
            result = result + " font-size=" +dq+ fontSize +dq
        end
        if fontFamily not= ""
            result = result + " font-family=" +dq+ fontFamily +dq
        end
        if textAnchor not= ""
            result = result + " text-anchor=" +dq+ textAnchor +dq
        end
        //this should be a global attribute
        if transform not= ""
            result = result + " transform=" +dq+ transform +dq
        end
        result = result + GlobalAttributesToText()
        result =  result+ ">" + ConvertReservedCharacters(labelText) + GetCloseTag(objectName, IsContainer())

        if IsOneLine() = false
            result = result + newLine
        end

        return result
    end

    /*
        This action sets the text that this element will render.  

        Attribute: Parameter labelText the text that this label will show

        Attribute: Example

        Libraries.Data.Formats.ScalableVectorGraphics.all

        Label myLabel
        myLabel:SetText("Words Words and more words")
    */
    action SetText(text labelText)
        me:labelText = labelText
    end

    /*
        This action returns the text that this object would show.  

        Attribute: Returns the text that this label shows

        Attribute: Example

        Libraries.Data.Formats.ScalableVectorGraphics.all

        Label myLabel
        myLabel:SetText("Words Words and more words")

        output myLabel:GetText()
    */
    action GetText returns text
        return labelText
    end

    /*
        This action sets the position of the label. Note that text in SVG is positioned based
        where the text anchor is. 

        Attribute: Parameter x The x position of the label. 
        Attribute: Parameter y The y position of the label.

        Attribute: Example

        Libraries.Data.Formats.ScalableVectorGraphics.all

        Label myLabel
        myLabel:SetPosition(60,60)
    */
    action SetPosition(number x, number y)
        me:x = "" + x
        me:y = "" + y
    end

    /*
        This action sets the x position of the label. Note that text in SVG is positioned based
        where the text anchor is. 

        Attribute: Parameter x The x position of the label.

        Attribute: Example

        Libraries.Data.Formats.ScalableVectorGraphics.all

        Label myLabel
        myLabel:SetPositionX(60)
    */
    action SetPositionX(number x)
        me:x = "" + x
    end

    /*
        This action sets the y position of the label. Note that text in SVG is positioned based
        where the text anchor is. 

        Attribute: Parameter y The y position of the label.

        Attribute: Example

        Libraries.Data.Formats.ScalableVectorGraphics.all

        Label myLabel
        myLabel:SetPositionY(60)
    */
    action SetPositionY(number y)
        me:y = "" + y
    end

    /*
        This action sets the position of the label. Note that text in SVG is positioned based
        where the text anchor is. 

        Attribute: Parameter x The x position of the label. 
        Attribute: Parameter y The y position of the label.
        Attribute: Parameter unit The unit of the length given.

        Attribute: Example

        Libraries.Data.Formats.ScalableVectorGraphics.all

        Label myLabel
        myLabel:SetPosition(50,50, "em")
    */
    action SetPosition(number x, number y, text unit)
        me:x = "" + x + unit
        me:y = "" + y + unit
    end

    /*
        This action sets the x position of the label. Note that text in SVG is positioned based
        where the text anchor is. 

        Attribute: Parameter x The x position of the label.
        Attribute: Parameter unit The unit of the length given.

        Attribute: Example

        Libraries.Data.Formats.ScalableVectorGraphics.all

        Label myLabel
        myLabel:SetPositionX(12,"em")
    */
    action SetPositionX(number x, text unit)
        me:x = "" + x + unit
    end

    /*
        This action sets the y position of the label. Note that text in SVG is positioned based
        where the text anchor is. 

        Attribute: Parameter y The y position of the rectangle.
        Attribute: Parameter unit The unit of the length given.

        Attribute: Example

        Libraries.Data.Formats.ScalableVectorGraphics.all

        Label myLabel
        myLabel:SetPositionY(12,"em")
    */
    action SetPositionY(number y, text unit)
        me:y = "" + y + unit
    end

    /*
        This action sets how much to shift the text on the x axis from its position. 

        Attribute: Parameter dx The amount of shift on the x axis.

        Attribute: Example

        Libraries.Data.Formats.ScalableVectorGraphics.all

        Label myLabel
        myLabel:SetHorizontalShift(12)
    */
    action SetHorizontalShift(number dx)
        me:dx = "" + dx
    end

    /*
        This action sets how much to shift the text on the x axis from its position. 

        Attribute: Parameter dx The amount of shift on the x axis.
        Attribute: Parameter unit The unit of the length given.

        Attribute: Example

        Libraries.Data.Formats.ScalableVectorGraphics.all

        Label myLabel
        myLabel:SetHorizontalShift(50, "%")
    */
    action SetHorizontalShift(number dx, text unit)
        me:dx = "" + dx + unit
    end

    /*
        This action returns the shift the text on the x axis from its position with the unit appended. 

        Attribute: Returns The amount of shift on the x axis.

        Attribute: Example

        Libraries.Data.Formats.ScalableVectorGraphics.all

        Label myLabel
        myLabel:GetHorizontalShift(12)

        output myLabel:GetHorizontalShift()
    */
    action GetHorizontalShift returns text
        return dx
    end

    /*
        This action sets how much to shift the text on the y axis from its position. 

        Attribute: Parameter dy The amount of shift on the y axis.

        Attribute: Example

        Libraries.Data.Formats.ScalableVectorGraphics.all

        Label myLabel
        myLabel:SetVerticalShift(12)
    */
    action SetVerticalShift(number dy)
        me:dy = "" + dy
    end

    /*
        This action sets how much to shift the text on the y axis from its position. 

        Attribute: Parameter dy The amount of shift on the y axis.
        Attribute: Parameter unit The unit of the length given.

        Attribute: Example

        Libraries.Data.Formats.ScalableVectorGraphics.all

        Label myLabel
        myLabel:SetVerticalShift(50, "%")
    */
    action SetVerticalShift(number dy, text unit)
        me:dy = "" + dy + unit
    end

    /*
        This action returns the shift the text on the y axis from its position with the unit appended. 

        Attribute: Returns The amount of shift on the y axis.

        Attribute: Example

        Libraries.Data.Formats.ScalableVectorGraphics.all

        Label myLabel
        myLabel:GetVerticalShift(12)

        output myLabel:GetVerticalShift()
    */
    action GetVerticalShift returns text
        return dy
    end

    /*
        This action sets how much to rotate each individual glyph of the text. 

        Attribute: Parameter degrees The amount of rotation in degrees.

        Attribute: Example

        Libraries.Data.Formats.ScalableVectorGraphics.all

        Label myLabel
        myLabel:SetRotation(270)
    */
    action SetRotation(integer degrees)
        me:rotate = rotate
    end

    /*
        This action returns how much each individual glyph of the text is being rotated. 

        Attribute: Returns The amount of rotation in degrees.

        Attribute: Example

        Libraries.Data.Formats.ScalableVectorGraphics.all

        Label myLabel
        myLabel:SetRotation(270)

        output myLabel:GetRotation()
    */
    action GetRotation returns integer
        return rotate
    end

    /*
        This action sets the width that the text of the label will be scaled to fit. 

        Attribute: Parameter textLength the width that the text will be fit to.

        Attribute: Example

        Libraries.Data.Formats.ScalableVectorGraphics.all

        Label myLabel
        myLabel:SetTextLength(50)
    */
    action SetTextLength(number textLength)
        me:textLength = "" + textLength
    end

    /*
        This action sets the width that the text of the label will be scaled to fit. 

        Attribute: Parameter textLength the width that the text will be fit to.
        Attribute: Parameter unit The unit of the length given.

        Attribute: Example

        Libraries.Data.Formats.ScalableVectorGraphics.all

        Label myLabel
        myLabel:SetTextLength(10,"%")
    */
    action SetTextLength(number textLength, text unit)
        me:textLength = "" + textLength + unit
    end

    /*
        This action returns the width that the text of the label will be scaled to fit with the unit appended. 

        Attribute: Returns The width that the text will be fit to

        Attribute: Example

        Libraries.Data.Formats.ScalableVectorGraphics.all

        Label myLabel
        myLabel:SetTextLength(50)

        output myLabel:GetTextLength()
    */
    action GetTextLength returns text
        return textLength
    end

    /*
        This action sets the current font size with the unit appended. 

        Attribute: Paramemter The value for the current font size

        Attribute: Example

        Libraries.Data.Formats.ScalableVectorGraphics.all

        Label myLabel
        myLabel:SetFontSize("12pt")

        output myLabel:GetFontSize()
    */
    action SetFontSize(text fontSize)
        me:fontSize = fontSize
    end

    /*
        This action returns the current font size with the unit appended. 

        Attribute: Returns The current font size

        Attribute: Example

        Libraries.Data.Formats.ScalableVectorGraphics.all

        Label myLabel
        myLabel:SetFontSize("12pt")

        output myLabel:GetFontSize()
    */
    action GetFontSize returns text
        return fontSize
    end

    
    /*
        This action sets the current font-family to the list of fonts give (can list multiple with comma-delimited list). 

        Attribute: Paramemter The value for the current font family

        Attribute: Example

        Libraries.Data.Formats.ScalableVectorGraphics.all

        Label myLabel
        myLabel:SetFontFamily("Arial")
        myLabel:SetFontFamily("Arial, Halvetica, sans-serif")

        output myLabel:GetFontSize()
    */
    action SetFontFamily(text fontFamily)
        me:fontFamily = fontFamily
    end

    
    /*
        This action returns the current font-family to the list of fonts give (can list multiple with comma-delimited list). 

        Attribute: Returns the value for the current font family

        Attribute: Example

        Libraries.Data.Formats.ScalableVectorGraphics.all

        Label myLabel
        myLabel:SetFontFamily("Arial")
        myLabel:SetFontFamily("Arial, Halvetica, sans-serif")

        output myLabel:GetFontSize()
    */
    action GetFontFamily returns text
        return fontFamily
    end

    /*
        This action sets where the text anchor is. The text anchor affects how a text chunk is aligned to
        the point that is the label's position. Possible values are start, middle, and end. By default a label
        will use start.

        Attribute: Parameter Where to set the text anchor.

        Attribute: Example

        Libraries.Data.Formats.ScalableVectorGraphics.all

        Label myLabel
        myLabel:SetTextAnchor("middle")
    */
    action SetTextAnchor(text textAnchor)
        me:textAnchor = textAnchor
    end

    /*
        This action returns where the text anchor is set. 

        Attribute: Returns The current text anchor.

        Attribute: Example

        Libraries.Data.Formats.ScalableVectorGraphics.all

        Label myLabel
        myLabel:GetTextAnchor("middle")

        output myLabel:GetTextAnchor()
    */
    action GetTextAnchor returns text
        return textAnchor
    end

    /*
        This action sets a transform on the current element.

        Attribute: Parameter Where to set the text anchor.

        Attribute: Example

        Libraries.Data.Formats.ScalableVectorGraphics.all

        Label myLabel
        myLabel:SetTransform("rotate(270,100,100)")
    */
    action SetTransform(text transform)
        me:transform = transform
    end

    /*
        This action returns the current transform applied on the element 

        Attribute: Returns The current applied transform.

        Attribute: Example

        Libraries.Data.Formats.ScalableVectorGraphics.all

        Label myLabel
        myLabel:SetTransfrom("rotate(270,100,100)")

        output myLabel:GetTransform()
    */
    action GetTransform returns text
        return transform
    end

end