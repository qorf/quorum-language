package Libraries.Game.Collision.Shapes

use Libraries.Compute.Vector3
use Libraries.Game.BoundingBox
use Libraries.Game.Collision.PhysicsPosition3D

/*
This class describes the sphere shape in 3D.
*/
class Sphere is CollisionShape3D
    on create
        parent:CollisionShape3D:SetType(parent:CollisionShape3D:SPHERE)
        parent:CollisionShape3D:localScaling:Set(1, 1, 1)
    end

    /*
    This action sets the radius of the Sphere.
    */
    action Set(number radius)
        parent:CollisionShape3D:implicitShapeDimensions:SetX(radius)
        parent:CollisionShape3D:collisionMargin = radius
    end

    /*
    This action sets the radius of the Sphere.
    */
    action SetRadius(number radius)
        Set(radius)
    end

    /*
    This action finds the vertex that is furthest away in a direction specified
    by "Vector3 vector" that doesn't have an offset.
    */
    action LocalGetSupportingVertexWithoutMargin(Vector3 vector) returns Vector3
        Vector3 out
        out:Set(0, 0, 0)
        return out
    end

    /*
    This action finds the bounding box that would bound the Sphere in its
    current position.
    */
    action ComputeBoundingBox(BoundingBox boundingBox, PhysicsPosition3D transform)
        Vector3 center = transform:GetOrigin()
        Vector3 extent
        extent:Set(GetMargin(), GetMargin(), GetMargin())
        Vector3 boundingBoxMinimum
        Vector3 boundingBoxMaximum
        boundingBoxMinimum:Set(center)
        boundingBoxMinimum:Subtract(extent)
        boundingBoxMaximum:Set(center)
        boundingBoxMaximum:Add(extent)
        boundingBox:Set(boundingBoxMinimum, boundingBoxMaximum)
    end

    /*
    This action approximates the inertia of the sphere.
    */
    action CalculateLocalInertia(number mass, Vector3 inertia)
        number elem = 0.4 * mass * GetMargin() * GetMargin()
        inertia:Set(elem, elem, elem)
    end

    /*
    This action returns the radius of the Sphere.
    */
    action GetRadius returns number
        return parent:CollisionShape3D:implicitShapeDimensions:GetX() * parent:CollisionShape3D:localScaling:GetX()
    end
    
    /*
    This action sets the thickness of the "shell" that will surround this 
    Sphere to prevent objects going through each other graphically.
    */
    action SetMargin(number margin)
        parent:CollisionShape3D:SetMargin(margin)
    end

    /*
    This action returns the thickness of the "shell" that will surround this 
    Sphere to prevent objects going through each other graphically.
    */
    action GetMargin returns number
        return GetRadius()
    end

    /*
    This action sets another sphere to have the properties of the current
    sphere.
    */
    action Copy returns CollisionShape3D
        Sphere sphere
        sphere:Set(me:GetRadius())
        sphere:parent:CollisionShape3D:localScaling:Set(parent:CollisionShape3D:localScaling)
        return sphere
    end

    /*
    This action calculates the preferred number of directions to check if another object
    has penetrated this sphere.
    */
    action GetNumberPreferredPenetrationDirections returns integer
        return 0
    end

    /*
    This action calculates one of the directions to check if another object has
    penetrated this sphere. Since sphere is a special case, this action doesn't
    do anything.
    */
    action GetPreferredPenetrationDirection(integer index, Vector3 penetrationVector)
        
    end

    /*
    This action returns how many children shapes are attached to this sphere.
    */
    action GetChildCount returns integer
        return 1
    end
end