package Libraries.Game

use Libraries.Containers.HashTable
use Libraries.Game.InputSet
use Libraries.Interface.Behaviors.Behavior
use Libraries.Interface.Events.BehaviorEvent
use Libraries.Containers.Iterator

class InputTable 

    /*
    The table that maps input to Behavior objects.
    */
    HashTable<InputSet, Behavior> table

    text identifier = ""

    action GetIdentifier returns text
        return identifier
    end

    action SetIdentifier(text id)
        identifier = id
    end

    action Add(InputSet set, Behavior behavior)
        table:Add(set, behavior)
    end

    action RemoveKey(InputSet set) returns Behavior
        return table:RemoveKey(set)
    end

    action HasKey(InputSet set) returns boolean
        return table:HasKey(set)
    end

    action GetValue(InputSet set) returns Behavior
        if table:HasKey(set)
            return table:GetValue(set)
        end

        return undefined
    end

    action Copy returns InputTable
        InputTable t
        
        Iterator<InputSet> iterator = table:GetKeyIterator()
        repeat while iterator:HasNext()
            InputSet set = iterator:Next()
            InputSet copy = set:Copy()
            Behavior behavior = table:GetValue(set)
            t:Add(copy, behavior)
        end
        return t
    end

    action GetInputSets returns Iterator<InputSet>
        return table:GetKeyIterator()
    end

    action Empty
        table:Empty()
    end

    action GetSize returns integer
        return table:GetSize()
    end

    action ProcessInput(InputSet set, BehaviorEvent event) returns boolean
        if table:HasKey(set)
            Behavior behavior = table:GetValue(set)
            event:SetInputSet(set)
            behavior:Run(event)
            return true
        end
        return false
    end

end