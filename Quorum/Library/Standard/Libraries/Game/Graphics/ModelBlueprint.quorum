package Libraries.Game.Graphics

use Libraries.Containers.Number32BitArray
use Libraries.Game.Graphics.Texture
use Libraries.Game.Graphics.ColorAttribute
use Libraries.Game.Graphics.NumberAttribute
use Libraries.Game.Graphics.BlendingAttribute
use Libraries.Game.Graphics.TextureAttribute
use Libraries.Game.Graphics.TextureDescriptor
use Libraries.Game.Graphics.ModelData.all
use Libraries.Compute.Matrix4
use Libraries.Compute.Quaternion
use Libraries.Compute.Vector3
use Libraries.Game.BoundingBox
use Libraries.Game.Disposable
use Libraries.Containers.Array
use Libraries.Containers.HashTable
use Libraries.Containers.Iterator

class ModelBlueprint is Disposable
    public Array<Material> materials
    public Array<Node> nodes
    public Array<Animation> animations
    public Array<Mesh> meshes
    public Array<MeshPart> meshParts
    public Array<Disposable> disposables
    // JAVA:
    //private ObjectMap<NodePart, ArrayMap<String, Matrix4>> nodePartBones = new ObjectMap<NodePart, ArrayMap<String, Matrix4>>();

    // Used to store a copy of the hash identifier for this ModelBlueprint in the Model plugin.
    public text id = "Default"

    action Load(ModelData data)
        LoadMeshes(data:meshes)
        LoadMaterials(data:materials)
        LoadNodes(data:nodes)
        LoadAnimations(data:animations)
        CalculateTransforms()
    end

    private action LoadMeshes(Array<ModelMesh> meshes)
        integer counter = 0
        repeat meshes:GetSize() times
            ConvertMesh(meshes:Get(counter))
            counter = counter + 1
        end
    end

    private action ConvertMesh(ModelMesh modelMesh)
        integer indicesCount = 0
        integer index = 0
        
        repeat modelMesh:parts:GetSize() times
            ModelMeshPart part = modelMesh:parts:Get(index)
            indicesCount = indicesCount + part:indices:GetSize()
            index = index + 1
        end

        VertexAttributes attributes
        attributes:SetAttributes(modelMesh:attributes)
        integer verticesCount = modelMesh:vertices:GetSize() / (attributes:vertexSize / 4)

        Mesh mesh
        mesh:Load(true, verticesCount, indicesCount, attributes)
        meshes:Add(mesh)
        disposables:Add(mesh)

        // Previously, we used "FillVertexBuffer" instead of "SetVertices", but changes to the system made the former unnecessary.    
        mesh:GetVertexData():SetVertices(modelMesh:vertices)
        mesh:GetIndexData():Clear()

        integer offset = 0
        index = 0
        repeat modelMesh:parts:GetSize() times
            ModelMeshPart part = modelMesh:parts:Get(index)
            MeshPart meshPart
            meshPart:id = part:id
            meshPart:primitiveType = part:primitiveType
            meshPart:indexOffset = offset
            meshPart:verticesCount = part:indices:GetSize()
            meshPart:mesh = mesh
            mesh:GetIndexData():Put(part:indices)
            offset = offset + meshPart:verticesCount
            meshParts:Add(meshPart)
        end

        mesh:GetIndexData():SetPosition(0)
    end

    private action LoadMaterials(Array<ModelMaterial> modelMaterials)
        integer counter = 0
        repeat modelMaterials:GetSize() times
            materials:Add(ConvertMaterial(modelMaterials:Get(counter)))
            counter = counter + 1
        end
    end

    private action ConvertMaterial(ModelMaterial mtl) returns Material

        Material result
        result:SetID(mtl:id)

        ColorAttribute creator

        if mtl:ambient not= undefined
            result:Add(creator:CreateAmbient(mtl:ambient))
        end
        if mtl:diffuse not= undefined
            result:Add(creator:CreateDiffuse(mtl:diffuse))
        end
        if mtl:specular not= undefined
            result:Add(creator:CreateSpecular(mtl:specular))
        end
        if mtl:emissive not= undefined
            result:Add(creator:CreateEmissive(mtl:emissive))
        end
        if mtl:reflection not= undefined
            result:Add(creator:CreateReflection(mtl:reflection))
        end
        if mtl:shininess > 0
            NumberAttribute temp
            temp:SetAttribute(temp:GetShininessValue(), mtl:shininess)
            result:Add(temp)
        end
        if mtl:opacity not= 1.0
            BlendingAttribute temp
            temp:opacity = mtl:opacity
            result:Add(temp)
        end
        
        // JAVA:
        // ObjectMap<String, Texture> textures = new ObjectMap<String, Texture>();
        HashTable<text, Texture> textures

        // Comment from libGDX:
        // FIXME uvScaling/uvTranslation totally ignored
        
        if mtl:textures not= undefined
            integer counter = 0
            ModelTextureData tex = undefined
            repeat mtl:textures:GetSize() times
                tex = mtl:textures:Get(counter)
                
                Texture texture = undefined
                if textures:HasKey(tex:fileName)
                    texture = textures:GetValue(tex:fileName)
                else
                    Texture newTexture
                    texture = newTexture
                    texture:LoadModelTexture(tex:fileName)
                    textures:Add(tex:fileName, texture)
                    disposables:Add(texture)
                end

                TextureDescriptor descriptor
                descriptor:texture = texture
                descriptor:minFilter:ConstructTextureFilter(texture:GetMinimizeFilter():GetValue())
                descriptor:magFilter:ConstructTextureFilter(texture:GetMagnifyFilter():GetValue())
                descriptor:uWrap:ConstructTextureWrap(texture:GetHorizontalWrap():GetValue())
                descriptor:vWrap:ConstructTextureWrap(texture:GetVerticalWrap():GetValue())
                
                number offsetU = 0
                number offsetV = 0
                number scaleU = 1
                number scaleV = 1
                if tex:uvTranslation not= undefined
                    offsetU = tex:uvTranslation:GetX()
                    offsetV = tex:uvTranslation:GetY()
                end

                if tex:uvScaling not= undefined
                    scaleU = tex:uvScaling:GetX()
                    scaleV = tex:uvScaling:GetY()
                end

                integer usageValue = 0
                TextureAttribute temp

                if tex:usage = tex:USAGE_DIFFUSE
                    usageValue = temp:GetDiffuseValue()
                elseif tex:usage = tex:USAGE_SPECULAR
                    usageValue = temp:GetSpecularValue()
                elseif tex:usage = tex:USAGE_BUMP
                    usageValue = temp:GetBumpValue()
                elseif tex:usage = tex:USAGE_NORMAL
                    usageValue = temp:GetNormalValue()
                elseif tex:usage = tex:USAGE_AMBIENT
                    usageValue = temp:GetAmbientValue()
                elseif tex:usage = tex:USAGE_EMISSIVE
                    usageValue = temp:GetEmissiveValue()
                elseif tex:usage = tex:USAGE_REFLECTION
                    usageValue = temp:GetReflectionValue()
                end

                temp:SetAttribute(usageValue, descriptor, offsetU, offsetV, scaleU, scaleV)
                result:Add(temp)

                counter = counter + 1
            end
        end

        return result
    end

    private action LoadNodes(Array<ModelNode> modelNodes)
        HashTable<NodePart, HashTable<text, Matrix4>> nodePartBones

        integer counter = 0
        repeat modelNodes:GetSize() times
            nodes:Add(LoadNode(modelNodes:Get(counter), nodePartBones))
            counter = counter + 1
        end
        
        NodePart key = undefined
        HashTable<text, Matrix4> value = undefined
        Iterator<NodePart> keyIterator = nodePartBones:GetKeyIterator()
        repeat while keyIterator:HasNext()
            key = keyIterator:Next()
            value = nodePartBones:GetValue(key)
            key:invBoneBindTransforms:Empty()
            
            text key2 = ""
            Matrix4 value2 = undefined
            Iterator<text> keyIterator2 = value:GetKeyIterator()
            repeat while keyIterator2:HasNext()
                key2 = keyIterator2:Next()
                value2 = value:GetValue(key2)

                Matrix4 temp
                temp:Set(value2)
                temp:Inverse()

                key:invBoneBindTransforms:Add(GetNode(key2), temp)
            end
        end
    end

    private action LoadNode(ModelNode modelNode, HashTable<NodePart, HashTable<text, Matrix4>> nodePartBones) returns Node
        Node node
        node:id = modelNode:id

        if modelNode:translation not= undefined
            node:translation:Set(modelNode:translation)
        end
        if modelNode:rotation not= undefined
            node:rotation:Set(modelNode:rotation)
        end
        if modelNode:scale not= undefined
            node:scale:Set(modelNode:scale)
        end

        if modelNode:parts not= undefined
            integer counter = 0
            ModelNodePart modelNodePart = undefined
            repeat modelNode:parts:GetSize() times
                modelNodePart = modelNode:parts:Get(counter)
                MeshPart meshPart = undefined
                Material meshMaterial = undefined

                if modelNodePart:meshPartID not= ""
                    integer meshCounter = 0
                    boolean searching = true
                    MeshPart part = undefined
                    repeat while searching and meshCounter < meshParts:GetSize()
                        part = meshParts:Get(meshCounter)
                        if modelNodePart:meshPartID = part:id
                            meshPart = part
                            searching = false
                        end
                        meshCounter = meshCounter + 1
                    end
                end

                if modelNodePart:materialID not= ""
                    integer materialCounter = 0
                    boolean searching = true
                    Material material = undefined
                    repeat while searching and materialCounter < materials:GetSize()
                        material = materials:Get(materialCounter)
                        if modelNodePart:materialID = material:GetID()
                            meshMaterial = material
                            searching = false
                        end
                        materialCounter = materialCounter + 1
                    end
                end

                if meshPart = undefined or meshMaterial = undefined
                    alert("Invalid node, node ID: " + node:id)
                end

                NodePart nodePart
                nodePart:meshPart = meshPart
                nodePart:material = meshMaterial
                node:parts:Add(nodePart)
                if modelNodePart:bones not= undefined
                    nodePartBones:Add(nodePart, modelNodePart:bones)
                end

                counter = counter + 1
            end
        end

        if modelNode:children not= undefined
            integer counter = 0
            ModelNode child = undefined
            repeat modelNode:children:GetSize() times
                child = modelNode:children:Get(counter)
                node:Add(LoadNode(child, nodePartBones))
                counter = counter + 1
            end
        end

        return node
    end

    private action GetNode(text nodeID) returns Node
        integer counter = 0
        Node node = undefined
        repeat nodes:GetSize() times
            node = nodes:Get(counter)
            if node:id = nodeID
                return node
            end
            counter = counter + 1
        end
        counter = 0
        Node result = undefined
        repeat nodes:GetSize() times
            node = nodes:Get(counter)
            result = node:GetChild(nodeID)
            if result not= undefined
                return result
            end
            counter = counter + 1
        end

        return result
    end

    private action LoadAnimations(Array<ModelAnimation> modelAnimations)
        integer counter = 0
        ModelAnimation anim = undefined
        repeat modelAnimations:GetSize() times
            anim = modelAnimations:Get(counter)
            
            Animation animation
            animation:id = anim:id

            integer counter2 = 0
            ModelNodeAnimation nanim = undefined
            repeat anim:nodeAnimations:GetSize() times
                nanim = anim:nodeAnimations:Get(counter2)
                Node node = GetNode(nanim:nodeID)

                if node not= undefined
                    NodeAnimation nodeAnim
                    nodeAnim:node = node

                    if nanim:translation not= undefined
                        Array<NodeKeyframe<Vector3>> temp
                        nodeAnim:translation = temp
                        temp:SetMaxSize(nanim:translation:GetSize())
                        
                        integer counter3 = 0
                        ModelNodeKeyframe<Vector3> kf = undefined
                        repeat nanim:translation:GetSize() times
                            kf = nanim:translation:Get(counter3)
                            if kf:keyTime > animation:duration
                                animation:duration = kf:keyTime
                            end

                            NodeKeyframe<Vector3> tempKF
                            Vector3 tempV
                            if kf:value = undefined
                                tempV:Set(node:translation)
                            else
                                tempV:Set(cast(Vector3, kf:value))
                            end
                            tempKF:Initialize(kf:keyTime, tempV)
                            nodeAnim:translation:Add(tempKF)

                            counter3 = counter3 + 1
                        end
                    end

                    if nanim:rotation not= undefined
                        Array<NodeKeyframe<Quaternion>> temp
                        nodeAnim:rotation = temp
                        temp:SetMaxSize(nanim:rotation:GetSize())

                        integer counter3 = 0
                        ModelNodeKeyframe<Quaternion> kf = undefined
                        repeat nanim:rotation:GetSize() times
                            kf = nanim:rotation:Get(counter3)
                            if kf:keyTime > animation:duration
                                animation:duration = kf:keyTime
                            end

                            NodeKeyframe<Quaternion> tempKF
                            Quaternion tempQ
                            if kf:value = undefined
                                tempQ:Set(node:rotation)
                            else
                                tempQ:Set(cast(Quaternion, kf:value))
                            end
                            tempKF:Initialize(kf:keyTime, tempQ)
                            nodeAnim:rotation:Add(tempKF)

                            counter3 = counter3 + 1
                        end
                    end

                    if nanim:scaling not= undefined
                        Array<NodeKeyframe<Vector3>> temp
                        nodeAnim:scaling = temp
                        temp:SetMaxSize(nanim:scaling:GetSize())

                        integer counter3 = 0
                        ModelNodeKeyframe<Vector3> kf = undefined
                        repeat nanim:scaling:GetSize() times
                            kf = nanim:scaling:Get(counter3)
                            if kf:keyTime > animation:duration
                                animation:duration = kf:keyTime
                            end

                            NodeKeyframe<Vector3> tempKF
                            Vector3 tempV
                            if kf:value = undefined
                                tempV:Set(node:scale)
                            else
                                tempV:Set(cast(Vector3, kf:value))
                            end
                            tempKF:Initialize(kf:keyTime, tempV)
                            nodeAnim:scaling:Add(tempKF)
                            
                            counter3 = counter3 + 1
                        end
                    end

                    if (nodeAnim:translation not= undefined and nodeAnim:translation:GetSize() > 0)
                    or (nodeAnim:rotation not= undefined and nodeAnim:rotation:GetSize() > 0)
                    or (nodeAnim:scaling not= undefined and nodeAnim:scaling:GetSize() > 0)
                        animation:nodeAnimations:Add(nodeAnim)
                    end
                end
                counter2 = counter2 + 1
            end

            if animation:nodeAnimations:GetSize() > 0
                animations:Add(animation)
            end
            counter = counter + 1
        end
    end

    /*
    This action will calculate the local and world transforms of all Nodes in
    this model, and then calculate the updated bone matrices.
    */
    action CalculateTransforms
        integer index = 0
        integer size = nodes:GetSize()
        repeat size times
            nodes:Get(index):CalculateAllTransforms()
            index = index + 1
        end
        index = 0
        repeat size times
            nodes:Get(index):CalculateAllBoneTransforms()
            index = index + 1
        end
    end

    action GetAnimation(text id) returns Animation
        integer counter = 0
        repeat animations:GetSize() times
            if animations:Get(counter):id:EqualsIgnoringCase(id)
                return animations:Get(counter)
            end
            counter = counter + 1
        end
        return undefined
    end

    action Dispose
        //...
    end

    /*
    CalculateBoundingBox will create a new BoundingBox object that contains the
    bounds of the Model.
    */
    action CalculateBoundingBox returns BoundingBox
        BoundingBox box
        return CalculateBoundingBox(box)
    end

    /*
    CalculateBoundingBox will compute the bounds of this Model, and then store
    them inside the provided BoundingBox object. If any data was previously
    stored in this BoundingBox, it will be overwritten.
    */
    action CalculateBoundingBox(BoundingBox box) returns BoundingBox
        box:Invalidate()
        return ExtendBoundingBox(box)
    end

    /*
    ExtendBoundingBox will expand the bounds of the given BoundingBox to fully
    encapsulate the bounds of this Model.
    */
    action ExtendBoundingBox(BoundingBox box) returns BoundingBox
        integer counter = 0

        repeat nodes:GetSize() times
            nodes:Get(counter):ExtendBoundingBox(box)
        end

        return box
    end

    /** 

	/** Adds a {@link Disposable} to be managed and disposed by this Model. Can be used to keep track of manually loaded textures
	 * for {@link ModelInstance}.
	 * @param disposable the Disposable *-
	public void manageDisposable (Disposable disposable) {
		if (!disposables.contains(disposable, true)) disposables.add(disposable);
	}

	/** @return the {@link Disposable} objects that will be disposed when the {@link #dispose()} method is called. *-
	public Iterable<Disposable> getManagedDisposables () {
		return disposables;
	}

	@Override
	public void dispose () {
		for (Disposable disposable : disposables) {
			disposable.dispose();
		}
	}

	

	/** Calculate the bounding box of this model instance. This is a potential slow operation, it is advised to cache the result.
	 * @param out the {@link BoundingBox} that will be set with the bounds.
	 * @return the out parameter for chaining *-
	public BoundingBox calculateBoundingBox (final BoundingBox out) {
		out.inf();
		return extendBoundingBox(out);
	}

	/** Extends the bounding box with the bounds of this model instance. This is a potential slow operation, it is advised to cache
	 * the result.
	 * @param out the {@link BoundingBox} that will be extended with the bounds.
	 * @return the out parameter for chaining *-
	public BoundingBox extendBoundingBox (final BoundingBox out) {
		final int n = nodes.size;
		for (int i = 0; i < n; i++)
			nodes.get(i).extendBoundingBox(out);
		return out;
	}

	/** @param id The ID of the material to fetch.
	 * @return The {@link Material} with the specified id, or null if not available. *-
	public Material getMaterial (final String id) {
		return getMaterial(id, true);
	}

	/** @param id The ID of the material to fetch.
	 * @param ignoreCase whether to use case sensitivity when comparing the material id.
	 * @return The {@link Material} with the specified id, or null if not available. *-
	public Material getMaterial (final String id, boolean ignoreCase) {
		final int n = materials.size;
		Material material;
		if (ignoreCase) {
			for (int i = 0; i < n; i++)
				if ((material = materials.get(i)).id.equalsIgnoreCase(id)) return material;
		} else {
			for (int i = 0; i < n; i++)
				if ((material = materials.get(i)).id.equals(id)) return material;
		}
		return null;
	}

	/** @param id The ID of the node to fetch.
	 * @return The {@link Node} with the specified id, or null if not found. *-
	public Node getNode (final String id) {
		return getNode(id, true);
	}

	/** @param id The ID of the node to fetch.
	 * @param recursive false to fetch a root node only, true to search the entire node tree for the specified node.
	 * @return The {@link Node} with the specified id, or null if not found. *-
	public Node getNode (final String id, boolean recursive) {
		return getNode(id, recursive, false);
	}

	/** @param id The ID of the node to fetch.
	 * @param recursive false to fetch a root node only, true to search the entire node tree for the specified node.
	 * @param ignoreCase whether to use case sensitivity when comparing the node id.
	 * @return The {@link Node} with the specified id, or null if not found. *-
	public Node getNode (final String id, boolean recursive, boolean ignoreCase) {
		return Node.getNode(nodes, id, recursive, ignoreCase);
	}*/
end