package Libraries.Game.Graphics

use Libraries.Compute.Vector
use Libraries.Containers.Array


/*
    The ColorNamer class takes in a red, green, blue (RGB) value and converts it to the 
    name of a given color. Because colors can an incredibly diverse set of names, we selected 
    colors from standard web consortium names, which can be found at the following address:

    https://www.w3.org/wiki/CSS/Properties/color/keywords

    When a color is requested that does not have an exact match in this list, the least 
    Euclidian distance is used to calculate the color that is closest. This provides a 
    mechanism to use relatively standard naming conventions without necessitating a very 
    large set of what are likely esoteric names. Note that we did not try to optimize this 
    algorithm, making it O(n) per call, where n is the number of colors in the possible set. 

    Attribute: Author Andreas Stefik
    Attribute: Example

    use Libraries.Game.Graphics.ColorNamer

    ColorNamer namer
    output namer:GetName(117,243,56) //outputs "YellowGreen"
*/
shared class ColorNamer
    Array<Vector> colors
    Array<text> colorNames

    on create
        GenerateColors()
    end

    private action GenerateColors
        //https://www.w3.org/wiki/CSS/Properties/color/keywords
        GenerateColor(0,0,0,"Black")
        GenerateColor(192,192,192,"Silver")
        GenerateColor(128,128,128,"Gray")
        GenerateColor(255,255,255,"White")
        GenerateColor(128,0,0,"Maroon")
        GenerateColor(255,0,0,"Red")
        GenerateColor(128,0,128,"Purple")
        GenerateColor(255,0,255,"Fuchsia")
        GenerateColor(0,128,0,"Green")
        GenerateColor(0,255,0,"Lime")
        GenerateColor(128,128,0,"Olive")
        GenerateColor(255,255,0,"Yellow")
        GenerateColor(0,0,128,"Navy")
        GenerateColor(0,0,255,"Blue")
        GenerateColor(0,128,128,"Teal")
        GenerateColor(0,255,255,"Aqua")

        //Extended Colors
        GenerateColor(240,248,255,"AliceBlue")
        GenerateColor(250,235,215,"AntiqueWhite")
        GenerateColor(127,255,212,"Aquamarine")
        GenerateColor(240,255,255,"Azure")
        GenerateColor(245,245,220,"Beige")
        GenerateColor(255,228,196,"Bisque")
        GenerateColor(255,235,205,"BlanchedAlmond")
        GenerateColor(138,43,226,"BlueViolet")
        GenerateColor(165,42,42,"Brown")
        GenerateColor(222,184,135,"Burlywood")
        GenerateColor(95,158,160,"CadetBlue")
        GenerateColor(127,255,0,"Chartreuse")
        GenerateColor(210,105,30,"Chocolate")
        GenerateColor(255,127,80,"Coral")
        GenerateColor(100,149,237,"CornflowerBlue")
        GenerateColor(255,248,220,"Cornsilk")
        GenerateColor(220,20,60 ,"Crimson")
        GenerateColor(0,255,255,"Cyan")
        GenerateColor(0,0,139,"DarkBlue")
        GenerateColor(0,139,139,"DarkCyan")
        GenerateColor(184,134,11,"DarkGoldenrod")
        GenerateColor(169,169,169,"DarkGray")
        GenerateColor(0,100,0,"DarkGreen")
        GenerateColor(189,183,107,"DarkKhaki")
        GenerateColor(139,0,139,"DarkMagenta")
        GenerateColor(85,107,47,"DarkOliveGreen")
        GenerateColor(255,140,0,"DarkOrange")
        GenerateColor(153,50,204,"DarkOrchid")
        GenerateColor(139,0,0,"DarkRed")
        GenerateColor(233,150,122,"DarkSalmon")
        GenerateColor(143,188,143,"DarkSeaGreen")
        GenerateColor(72,61,139,"DarkSlateBlue ")
        GenerateColor(47,79,79,"DarkSlateGray")
        GenerateColor(0,206,209,"DarkTurquoise")
        GenerateColor(148,0,211,"DarkViolet")
        GenerateColor(255,20,147,"DeepPink")
        GenerateColor(0,191,255,"DeepSkyBlue")
        GenerateColor(105,105,105,"DimGray")
        GenerateColor(30,144,255,"DodgerBlue")
        GenerateColor(178,34,34,"FireBrick")
        GenerateColor(255,250,240,"FloralWhite")
        GenerateColor(34,139,34,"ForestGreen")
        GenerateColor(220,220,220,"Gainsboro")
        GenerateColor(248,248,255,"GhostWhite")
        GenerateColor(255,215,0,"Gold")
        GenerateColor(218,165,32,"Goldenrod")
        GenerateColor(173,255,47,"GreenYellow")
        GenerateColor(240,255,240,"Honeydew")
        GenerateColor(255,105,180,"HotPink")
        GenerateColor(205,92,92,"RedBrown")
        GenerateColor(75,0,130,"Indigo")
        GenerateColor(255,255,240,"Ivory")
        GenerateColor(240,230,140,"Khaki ")
        GenerateColor(230,230,250,"Lavender")
        GenerateColor(255,240,245,"LavenderBlush")
        GenerateColor(124,252,0,"LawnGreen")
        GenerateColor(255,250,205,"LemonChiffon")
        GenerateColor(173,216,230,"LightBlue")
        GenerateColor(240,128,128,"LightCoral")
        GenerateColor(224,255,255,"LightCyan")
        GenerateColor(250,250,210,"LightGoldenrodYellow")
        GenerateColor(211,211,211,"LightGray")
        GenerateColor(144,238,144,"LightGreen")
        GenerateColor(255,182,193,"LightPink")
        GenerateColor(255,160,122,"LightSalmon")
        GenerateColor(32,178,170,"LightSeaGreen")
        GenerateColor(135,206,250,"LightSkyBlue")
        GenerateColor(119,136,153,"LightSlateGray")
        GenerateColor(176,196,222,"LightSteelBlue")
        GenerateColor(255,255,224,"LightYellow")
        GenerateColor(50,205,50,"LimeGreen")
        GenerateColor(250,240,230,"Linen")
        GenerateColor(255,0,255,"Magenta")
        GenerateColor(102,205,170,"MediumAquaMarine")
        GenerateColor(0,0,205,"MediumBlue")
        GenerateColor(186,85,211,"MediumOrchid")
        GenerateColor(147,112,219,"MediumPurple")
        GenerateColor(60,179,113,"MediumSeaGreen")
        GenerateColor(123,104,238,"MediumSlateBlue")
        GenerateColor(0,250,154,"MediumSpringGreen")
        GenerateColor(72,209,204,"MediumTurquoise ")
        GenerateColor(199,21,133,"MediumVioletRed")
        GenerateColor(25,25,112,"MidnightBlue")
        GenerateColor(245,255,250,"MintCream")
        GenerateColor(255,228,225,"MistyRose")
        GenerateColor(255,228,181,"Moccasin")
        GenerateColor(255,222,173,"NavajoWhite")
        GenerateColor(253,245,230,"OldLace")
        GenerateColor(107,142,35,"OliveDrab")
        GenerateColor(255,165,0,"Orange")
        GenerateColor(255,69,0,"OrangeRed")
        GenerateColor(218,112,214,"Orchid")
        GenerateColor(238,232,170,"PaleGoldenrod")
        GenerateColor(152,251,152,"PaleGreen")
        GenerateColor(175,238,238,"PaleTurquoise")
        GenerateColor(219,112,147,"PaleVioletRed")
        GenerateColor(255,239,213,"PapayaWhip")
        GenerateColor(255,218,185,"PeachPuff")
        GenerateColor(205,133,63,"Peru")
        GenerateColor(255,192,203,"Pink")
        GenerateColor(221,160,221,"Plum")
        GenerateColor(176,224,230,"PowderBlue")
        GenerateColor(188,143,143,"RosyBrown")
        GenerateColor(65,105,225,"RoyalBlue")
        GenerateColor(139,69,19,"SaddleBrown")
        GenerateColor(250,128,114,"Salmon")
        GenerateColor(244,164,96,"SandyBrown")
        GenerateColor(46,139,87,"Seagreen")
        GenerateColor(255,245,238,"Seashell")
        GenerateColor(160,82,45,"Sienna")
        GenerateColor(135,206,235,"SkyBlue")
        GenerateColor(106,90,205,"SlateBlue")
        GenerateColor(112,128,144,"SlateGray")
        GenerateColor(255,250,250,"Snow")
        GenerateColor(0,255,127,"SpringGreen")
        GenerateColor(70,130,180,"SteelBlue")
        GenerateColor(210,180,140,"Tan")
        GenerateColor(216,191,216,"Thistle")
        GenerateColor(255,99,71,"Tomato")
        GenerateColor(64,224,208,"Turquoise")
        GenerateColor(238,130,238,"Violet")
        GenerateColor(245,222,179,"Wheat")
        GenerateColor(245,245,245,"WhiteSmoke")
        GenerateColor(255,255,0,"Yellow")
        GenerateColor(154,205,50,"YellowGreen")
    end

    private action GenerateColor(number red, number green, number blue, text name)
        colors:Add(GenerateVector(red,green,blue))
        colorNames:Add(name)
    end

    private action GenerateVector(number red, number green, number blue) returns Vector
        Vector vector
        vector:SetSize(3)
        Set(red, green, blue, vector)
        return vector
    end

    /*
        From a red, green, blue value, outputs the closest name in the set of common w3C colors. In
        this case, the colors go from 0 to 1. The system automatically multiplies this percentage
        by 255 for the calculation.

        Attribute: Example

        use Libraries.Game.Graphics.ColorNamer
    
        ColorNamer namer
        output namer:GetName(117,243,56) //outputs "YellowGreen"
    */
    action GetNameAsPercentage(number red, number green, number blue) returns text
        return GetName(red * 255.0, green * 255.0, blue * 255.0)
    end
    
/*
        From a red, green, blue value, outputs the closest name in the set of common w3C colors. In
        this case, the colors go from 0 to 1. The system automatically multiplies this percentage
        by 255 for the calculation.

        Attribute: Example

        use Libraries.Game.Graphics.ColorNamer
    
        ColorNamer namer
        Color color
        color = color:CustomColor(117,243,56,1)
        output namer:GetName(color) //outputs "YellowGreen"
    */
    action GetName(Color color) returns text
        return GetNameAsPercentage(color:GetRed(), color:GetGreen(), color:GetBlue())
    end

    /*
        From a red, green, blue value, outputs the closest name in the set of common w3C colors. In
        this case, the colors go from 0 to 255.

        Attribute: Example

        use Libraries.Game.Graphics.ColorNamer
    
        ColorNamer namer
        output namer:GetName(117,243,56) //outputs "YellowGreen"
    */
    action GetName(number red, number green, number blue) returns text
        Vector color
        color:SetSize(3)
        Set(red, green, blue, color)
        
        number distance = 0
        distance = distance:GetMaximumValue()
        integer index = -1
        i = 0
        repeat while i < colors:GetSize()
            Vector next = colors:Get(i)
            number current = next:Distance(color)
            if current < distance
                distance = current
                index = i
            end
            i = i + 1
        end

        if i not= -1
            return colorNames:Get(index)
        end

        return undefined
    end

    private action Set(number red, number green, number blue, Vector vector) 
        vector:Set(0, red)
        vector:Set(1, green)
        vector:Set(2, blue)
    end
end