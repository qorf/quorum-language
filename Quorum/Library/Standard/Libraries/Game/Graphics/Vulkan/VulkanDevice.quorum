package Libraries.Game.Graphics.Vulkan
use Libraries.Containers.Array

class VulkanDevice

    VulkanPhysicalDevice physicalDevice = undefined
    Array<VulkanQueueFamily> queueFamilies
    boolean initialized = false

    action CreateGraphicsDevice(VulkanPhysicalDevice physicalDevice) returns boolean
        VulkanInstance instance = physicalDevice:GetInstance()
        VulkanUtilities utilities

        Array<text> extensionNames
        utilities:GetRequiredPlatformDeviceExtensions(extensionNames, instance:HasValidationLayers())
        
        VulkanConstants constants
        extensionNames:Add(constants:SWAPCHAIN_EXTENSION)
        output "CREATING WITH EXTENSIONS:"
        integer i = 0
        repeat while i < extensionNames:GetSize()
            output i + ": " + extensionNames:Get(i)
            i = i + 1
        end

        return Create(physicalDevice, extensionNames)
    end

    action Create(VulkanPhysicalDevice physicalDevice, Array<text> extensionNames) returns boolean
        me:physicalDevice = physicalDevice

        boolean success = CreateNative(physicalDevice, extensionNames, queueFamilies)

        if success
            initialized = true
        end

        return success
    end

    private system action CreateNative(VulkanPhysicalDevice physicalDevice, Array<text> extensionNames, Array<VulkanQueueFamily> queueFamilies) returns boolean

    //system action GetQueue(integer supportedFeaturesMask)

    action GetPhysicalDevice returns VulkanPhysicalDevice
        return physicalDevice
    end

    action GetQueueFamilyCount returns integer
        return queueFamilies:GetSize()
    end

    action GetQueueFamily(integer index) returns VulkanQueueFamily
        return queueFamilies:Get(index)
    end

end