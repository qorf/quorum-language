package Libraries.Game.Graphics.Vulkan
use Libraries.Compute.BitwiseOperations

class VulkanBuffer

    VulkanDevice device = undefined
    integer size = 0
    boolean supportsMapping = false

    boolean initialized = false

    /*
    This action will create a Vulkan buffer and allocate the requested amount of memory. 

    Attribute: Parameter device The VulkanDevice that the buffer will be allocated with.
    Attribute: Parameter size The number of bytes that this buffer should store.
    Attribute: Parameter usageFlags A bitmask of flags indicating how this buffer will be used. Flags from the VulkanConstants can be combined using bitwise "or" operations.
    Attribute: Parameter memoryFlags A bitmask of flags indicating what kind of memory is required for this buffer. Flags from the VulkanConstants can be combined using bitwise "or" operations.
    */
    action Create(VulkanDevice device, integer size, integer usageFlags, integer memoryFlags) returns boolean
        return Create(device, size, usageFlags, memoryFlags, false)
    end

    /*
    This action will create a Vulkan buffer and allocate the requested amount of memory. 

    Attribute: Parameter device The VulkanDevice that the buffer will be allocated with.
    Attribute: Parameter size The number of bytes that this buffer should store.
    Attribute: Parameter usageFlags A bitmask of flags indicating how this buffer will be used. Flags from the VulkanConstants can be combined using bitwise "or" operations.
    Attribute: Parameter memoryFlags A bitmask of flags indicating what kind of memory is required for this buffer. Flags from the VulkanConstants can be combined using bitwise "or" operations.
    Attribute: Parameter allowConcurrentAccess True to allow multiple queue families to access the buffer at once, or false to only allow one at a time. If this flag isn't provided, the default value is false.
    */
    action Create(VulkanDevice device, integer size, integer usageFlags, integer memoryFlags, boolean allowConcurrentAccess) returns boolean
        VulkanConstants constants
        integer sharingMode = constants:SHARING_MODE_EXCLUSIVE
        if allowConcurrentAccess
            sharingMode = constants:SHARING_MODE_CONCURRENT
        end

        boolean success = CreateNative(device, size, usageFlags, memoryFlags, sharingMode)
        if success
            me:device = device
            me:size = size
            initialized = true

            BitwiseOperations bits
            if (bits:And(memoryFlags, constants:MEMORY_PROPERTY_HOST_VISIBLE_BIT) not= 0)
                isMemoryAccessible = true
            end
        end

        return success
    end

    private system action CreateNative(VulkanDevice device, integer size, integer usageFlags, integer memoryFlags, integer sharingMode) returns boolean

    action Dispose
        alert("NYI")
    end

    /*
    This action returns true if this buffer supports "mapped" memory that is directly accessible from the program, or false otherwise.
    Non-mapped (or "local") memory is stored directly on the GPU, which is more performant but not directly accessible from the program.
    Buffers don't support mapping by default, but can use mapped memory if they are created with the "MEMORY_PROPERTY_HOST_VISIBLE_BIT" 
    as part of the memory flags when they are created.
    */
    action SupportsMapping returns boolean
        return supportsMapping
    end

    action GetDevice returns VulkanDevice
        return device
    end

    action GetSize returns integer
        return size
    end

    /*
    This action returns true if the VulkanBuffer is currently mapping to a section of memory, or false otherwise. If the buffer doesn't support
    mapping (i.e. it wasn't created with the "MEMORY_PROPERTY_HOST_VISIBLE_BIT" property) then this will always return false.
    */
    system action IsMapping returns boolean

    /*
    This action allocates a chunk of memory that is accessible to the program, and maps the memory to the GPU's resources. The allocated memory
    must be freed later using the StopMapping action when it is no longer needed, or it will automatically be freed when calling the Dispose action.
    If this buffer doesn't support mapping, using this action will cause an error.
    */
    system action BeginMapping returns boolean

    /*
    This action frees the memory that has been previously allocated by the BeginMapping action. If the buffer doesn't currently have mapped memory,
    or if it doesn't support mapping, this action will do nothing. 
    */
    system action StopMapping

    action MapToNumber64BitMemory returns VulkanNumber64BitMappedMemory
        if not initialized
            alert("I couldn't map to memory because this VulkanBuffer hasn't been initialized! Use Create() first.")
        end
        if not SupportsMapping()
            alert("I couldn't map to memory because this VulkanBuffer doesn't support mapping! Create it using the 'MEMORY_PROPERTY_HOST_VISIBLE_BIT' property.")
        end

        VulkanNumber64BitMappedMemory result
        result:Create(me)
        return result
    end

end