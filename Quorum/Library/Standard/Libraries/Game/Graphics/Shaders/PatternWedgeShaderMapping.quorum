package Libraries.Game.Graphics.Shaders

use Libraries.Game.Graphics.Shaders.DefaultShaderMapping2D
use Libraries.Game.GameStateManager
use Libraries.Game.Graphics.Drawable
use Libraries.Game.Graphics.Shaders.ShaderProgram
use Libraries.Game.Graphics.GraphicsManager
use Libraries.Game.Graphics.Camera
use Libraries.Game.Graphics.OpenGLConstants
use Libraries.Interface.Item2D
use Libraries.Game.Graphics.VertexAttribute
use Libraries.Game.Graphics.Mesh
use Libraries.Game.Graphics.DefaultVertexAttributes2D
use Libraries.Containers.Array
use Libraries.Containers.Integer32BitArray
use Libraries.Game.Graphics.VertexAttributes
use Libraries.Game.Graphics.DrawableShape
use Libraries.Interface.Controls.Charts.Graphics.Wedge
use Libraries.Compute.Math
use Libraries.Game.Graphics.Color
use Libraries.Interface.Controls.Charts.ChartOptions
use Libraries.Game.NumberUtilities

class PatternWedgeShaderMapping is DefaultShaderMapping2D

    ShaderProgram program = undefined
    GameStateManager manager
    NumberUtilities utility

    constant integer BATCH_SIZE = 200

    VertexAttribute innerRadiusAttribute = undefined
    VertexAttribute outerRadiusAttribute = undefined
    VertexAttribute minAngleAttribute = undefined
    VertexAttribute maxAngleAttribute = undefined
    VertexAttribute originPointAttribute = undefined
    VertexAttribute aspectRatioAttribute = undefined
    VertexAttribute patternDistanceAttribute = undefined
    VertexAttribute patternRotationAttribute = undefined
    VertexAttribute patternThicknessAttribute = undefined
    VertexAttribute patternColorAttribute = undefined

    /*
    This action is called when the mapping is created. It must be used to set
    the initial values of the Mesh using the Mesh's Load action.
    */
    private action InitializeMesh(Mesh mesh)
        DefaultVertexAttributes2D attributes
        Array<VertexAttribute> extraAttributes
        VertexAttribute dummy
        innerRadiusAttribute = dummy:CustomNumberAttribute("a_innerRadius")
        outerRadiusAttribute = dummy:CustomNumberAttribute("a_outerRadius")
        minAngleAttribute = dummy:CustomNumberAttribute("a_minAngle")
        maxAngleAttribute = dummy:CustomNumberAttribute("a_maxAngle")
        originPointAttribute = dummy:CustomVectorAttribute("a_originPoint", 2)
        aspectRatioAttribute = dummy:CustomNumberAttribute("a_aspectRatio")
        patternDistanceAttribute = dummy:CustomNumberAttribute("a_patternDistance")
        patternRotationAttribute = dummy:CustomNumberAttribute("a_patternRotation")
        patternThicknessAttribute = dummy:CustomNumberAttribute("a_patternThickness")
        patternColorAttribute = dummy:CustomColorAttribute("a_patternColor")
        extraAttributes:Add(innerRadiusAttribute)
        extraAttributes:Add(outerRadiusAttribute)
        extraAttributes:Add(minAngleAttribute)
        extraAttributes:Add(maxAngleAttribute)
        extraAttributes:Add(originPointAttribute)
        extraAttributes:Add(aspectRatioAttribute)
        extraAttributes:Add(patternDistanceAttribute)
        extraAttributes:Add(patternRotationAttribute)
        extraAttributes:Add(patternThicknessAttribute)
        extraAttributes:Add(patternColorAttribute)
        attributes:SetAttributes(extraAttributes)
        
        /*
        We need a mesh with enough 32-bit float values for the vertices, and the correct number of indices.
        The "vertexSize" value is calculated as bytes, not floats, so we divide by 4 to get the correct number.
        We multiply by 4 because we'll be rendering quads (4 points per Drawable).

        For the indices, we need 2 triangles (3 points each) to render a quad, so we multiply by 6 to store the values in the array.
        */
        mesh:Load(false, (attributes:parent:VertexAttributes:vertexSize / 4) * 4 * BATCH_SIZE, 6 * BATCH_SIZE, attributes)

        integer length = 6 * BATCH_SIZE
        Integer32BitArray indices
        indices:SetSize(length)

        integer indexCounter = 0
        integer vertexCounter = 0

        // Setup the indices to make pairs of triangles to render quads of vertices.
        repeat while indexCounter < length
            indices:Set(indexCounter, vertexCounter)
            indices:Set(indexCounter + 1, vertexCounter + 1)
            indices:Set(indexCounter + 2, vertexCounter + 2)
            indices:Set(indexCounter + 3, vertexCounter + 2)
            indices:Set(indexCounter + 4, vertexCounter + 3)
            indices:Set(indexCounter + 5, vertexCounter)

            indexCounter = indexCounter + 6
            vertexCounter = vertexCounter + 4
        end

        mesh:SetIndices(indices)
    end

    action SetVertexData(ShaderProgram program, Drawable drawable, DrawableShape shape)
        if drawable is Wedge
            Wedge wedge = cast(Wedge, drawable)
            shape:SetDataForAllVertices(outerRadiusAttribute:offset / 4, wedge:GetOuterRadius())
            shape:SetDataForAllVertices(innerRadiusAttribute:offset / 4, wedge:GetInnerRadius())
            shape:SetDataForAllVertices(minAngleAttribute:offset / 4, wedge:GetStartPercentage())
            shape:SetDataForAllVertices(maxAngleAttribute:offset / 4, wedge:GetEndPercentage())
            
            number originX = 0
            number originY = 0

            if wedge:GetParent() = undefined
                originX = wedge:GetGlobalX() + wedge:GetWidth() / 2
                originY = wedge:GetGlobalY() + wedge:GetHeight() / 2
            else
                Item2D item = wedge:GetParent()
                originX = item:GetGlobalX() + item:GetWidth() / 2
                originY = item:GetGlobalY() + item:GetHeight() / 2
            end

            shape:SetDataForAllVertices(originPointAttribute:offset / 4, originX)
            shape:SetDataForAllVertices(originPointAttribute:offset / 4 + 1, originY)

            ChartOptions chartOptions
            number interfaceScale = wedge:GetInterfaceScale()
            number rotation = 0
            integer patternType = wedge:GetPattern()

            if patternType = chartOptions:VERTICAL_LINES or patternType = chartOptions:PLAIN_CROSSHATCH
                rotation = 0
            elseif patternType = chartOptions:HORIZONTAL_LINES
                rotation = 90
            elseif patternType = chartOptions:DIAGONAL_DOWN_LINES or patternType = chartOptions:DIAGONAL_CROSSHATCH or patternType = chartOptions:PINPOINT_DASHES
                rotation = 45
            elseif patternType = chartOptions:DIAGONAL_UP_LINES
                rotation = 135
            end

            Math math
            // Currently we use index 0 for the pattern indices. These values aren't conveniently exposed from the drawables and currently are irrelevant.
            number patternDistance = math:Round(chartOptions:GetPatternDistanceFromPalette(0) * interfaceScale)
            number patternThickness = math:Round(chartOptions:GetPatternThicknessFromPalette(0) * interfaceScale)
            Color patternColor = chartOptions:GetPatternColorFromPalette(0)

            number width = wedge:GetWidth() * wedge:GetScaleX()
            number height = wedge:GetWidth() * wedge:GetScaleY()
            number aspectRatio = width / height

            shape:SetDataForAllVertices(aspectRatioAttribute:offset / 4, aspectRatio)
    
            shape:SetDataForAllVertices(patternDistanceAttribute:offset / 4, patternDistance / width)
            shape:SetDataForAllVertices(patternRotationAttribute:offset / 4, math:DegreesToRadians(rotation))
            shape:SetDataForAllVertices(patternThicknessAttribute:offset / 4, patternThickness / width)
            shape:SetDataForAllVertices(patternColorAttribute:offset / 4, utility:PackNormalizedVector4(patternColor:GetRed(), patternColor:GetGreen(), patternColor:GetBlue(), patternColor:GetAlpha()))
        end
    end

    action End(ShaderProgram program)
        me:program = undefined
    end

    /*
    This action is called when it is time for the ShaderMapping to render its current batch with the given ShaderProgram.
    */
    action RenderBatch(ShaderProgram program, Camera camera)
        parent:BatchingShaderMapping2D:RenderBatch(program, camera)
    end

end