package Libraries.Game.Graphics.Shaders.Vulkan

use Libraries.Game.Graphics.Shaders.ShaderManager
use Libraries.Game.Graphics.Shaders.Shader
use Libraries.Game.Graphics.Shaders.SPIRVShaderCompiler
use Libraries.Containers.ByteArray
use Libraries.Game.GameStateManager
use Libraries.Game.Graphics.GraphicsManager
use Libraries.Game.Graphics.Vulkan.VulkanGraphics
use Libraries.Game.Graphics.Vulkan.VulkanDevice
use Libraries.Game.Graphics.Vulkan.VulkanPipeline
use Libraries.Containers.HashTable
use Libraries.Game.Graphics.Vulkan.VulkanRenderPass
use Libraries.Game.Graphics.Vulkan.VulkanPipelineCache
use Libraries.Game.Graphics.Vulkan.VulkanConstants

class VulkanShaderManager is ShaderManager

    SPIRVShaderCompiler compiler = undefined
    VulkanPipelineCache pipelineCache = undefined

    // A value given to each shader as an ID. Currently doesn't correspond to anything,
    // but it can be stored to a hash with something that more accurately reflects the
    // Shader if needed.
    // The "actual" handle to the shader is stored as 64-bit integer, which currently requires a plugin layer.
    integer currentShaderID = 1

    action GetCompiler returns SPIRVShaderCompiler
        if compiler = undefined
            SPIRVShaderCompiler compiler
            me:compiler = compiler
        end

        return compiler
    end

    action DisposeShader(Shader shader)
        alert("NYI")
    end

    action CompileShader(Shader shader) returns integer
        if shader:IsCompiled()
            return shader:GetID()
        end

        text name = shader:GetName()
        if name = ""
            alert("I couldn't compile this shader because its name hasn't been set. Use the SetName action before compiling this shader.")
        end

        SPIRVShaderCompiler compiler = GetCompiler()
        ByteArray bytes = compiler:Compile(name, shader:GetCode(), shader:GetType())

        GameStateManager manager
        GraphicsManager graphics = manager:GetGameGraphics()
        if graphics is VulkanGraphics
            VulkanGraphics vulkan = cast(VulkanGraphics, graphics)
            boolean success = CompileShaderNative(shader, vulkan:GetDevice(), bytes)
            if not success
                alert("An error occurred while compiling the shader '" + name + "'")
            end
        else
            alert("I can't compile the shader '" + name + "' because VulkanGraphics wasn't available!")
        end

        Register(name, shader)
        integer id = currentShaderID
        currentShaderID = currentShaderID + 1

        return id
    end

    private system action CompileShaderNative(Shader shader, VulkanDevice device, ByteArray shaderCode) returns boolean
    
    /*
    This action takes a Quorum shader type and converts it to the equivalent Vulkan shader stage value from the VulkanConstants class.
    */
    action GetVulkanType(integer shaderType) returns integer
        VulkanConstants constants
        if shaderType = parent:ShaderManager:FRAGMENT_SHADER
            return constants:SHADER_STAGE_FRAGMENT_BIT
        elseif shaderType = parent:ShaderManager:VERTEX_SHADER
            return constants:SHADER_STAGE_VERTEX_BIT
        elseif shaderType = parent:ShaderManager:GEOMETRY_SHADER
            return constants:SHADER_STAGE_GEOMETRY_BIT
        elseif shaderType = parent:ShaderManager:TESSELLATION_CONTROL_SHADER
            return constants:SHADER_STAGE_TESSELLATION_CONTROL_BIT
        elseif shaderType = parent:ShaderManager:TESSELLATION_EVALUATION_SHADER
            return constants:SHADER_STAGE_TESSELLATION_EVALUATION_BIT
        elseif shaderType = parent:ShaderManager:COMPUTE_SHADER
            return constants:SHADER_STAGE_COMPUTE_BIT
        end

        return -1
    end

    action ReloadShader(Shader shader) returns integer
        alert("NYI")
    end

    /*
        This action takes platform into account and returns a default version of OpenGL's number to be placed at the top of a shader. For shaders
        not using OpenGL, this action does not provide useful information. The version number is calculated by Quorum, not the graphics hardware.
    */
    action GetVersionHeader returns text
        return "#version 450"
    end

    /*
    This action returns the default VulkanPipeline used for rendering 2D graphics.
    */
    action GetDefaultPipeline2D returns VulkanPipeline
        // TO-DO: This acts in a similar, but incompatible, way as GetDefaultShaderProgram2D.
        // This should be refactored so the ShaderManager can handle OpeGL ShaderPrograms and VulkanPipelines in a consistent way.

        
        return undefined
    end

    action GetPipelineCache returns VulkanPipelineCache
        if pipelineCache = undefined
            GameStateManager gameState
            VulkanGraphics graphics = cast(VulkanGraphics, gameState:GetGameGraphics())
            
            VulkanPipelineCache cache
            boolean success = cache:Create(graphics:GetDevice())
            if success
                pipelineCache = cache
            end
        end

        return pipelineCache
    end

end