package Libraries.Game.Graphics.Shaders

use Libraries.Game.Graphics.Shaders.ShaderMapping
use Libraries.Game.Graphics.Shaders.ShaderProgram
use Libraries.Game.GameStateManager
use Libraries.Game.Graphics.Camera
use Libraries.Game.Graphics.GraphicsManager
use Libraries.Game.Graphics.OpenGLConstants
use Libraries.Game.Graphics.Drawable
use Libraries.Interface.Layouts.LayoutProperties
use Libraries.Interface.Controls.Control
use Libraries.Interface.Views.View2D
use Libraries.Game.Graphics.Color
use Libraries.Game.Graphics.ColorGroup
use Libraries.Interface.Item2D
use Libraries.Interface.Controls.Checkbox
use Libraries.Interface.Views.ToggleView
use Libraries.Interface.Controls.Tab
use Libraries.Game.Graphics.DrawableShape
use Libraries.Game.Graphics.Mesh
use Libraries.Game.Graphics.DefaultVertexAttributes2D
use Libraries.Containers.Integer32BitArray
use Libraries.Containers.Number32BitArray
use Libraries.Game.Graphics.VertexAttribute
use Libraries.Containers.Array

class ControlShaderMapping is BatchingShaderMapping2D

    ShaderProgram program = undefined
    GameStateManager manager

    integer arrayIndex = 0

    constant integer ARRAY_SIZE = 200
    Number32BitArray roundingArray
    Number32BitArray borderThicknessArray
    Number32BitArray aspectRatioArray
    Number32BitArray antiAliasArray

    VertexAttribute borderColorAttribute = undefined
    VertexAttribute arrayIndexAttribute = undefined

    on create
        // The rounding and border thickness arrays store vector4 objects, so they need 4 numbers for each component.
        roundingArray:SetSize(ARRAY_SIZE * 4)
        borderThicknessArray:SetSize(ARRAY_SIZE * 4)

        aspectRatioArray:SetSize(ARRAY_SIZE)
        antiAliasArray:SetSize(ARRAY_SIZE)
    end

    /*
    This action is called when the mapping is created. It must be used to set
    the initial values of the Mesh using the Mesh's Load action.
    */
    private action InitializeMesh(Mesh mesh)
        DefaultVertexAttributes2D attributes
        Array<VertexAttribute> extraAttributes
        VertexAttribute dummy
        borderColorAttribute = dummy:CustomColorAttribute("a_borderColor")
        arrayIndexAttribute = dummy:CustomIntegerAttribute("a_arrayIndex")
        attributes:SetAttributes(extraAttributes)
        
        mesh:Load(false, 8 * 4 * ARRAY_SIZE, 6 * ARRAY_SIZE, attributes)

        integer length = 1200
        Integer32BitArray indices
        indices:SetSize(length)

        integer indexCounter = 0
        integer vertexCounter = 0

        // Setup the indices to make pairs of triangles to render quads of vertices.
        repeat while indexCounter < length
            indices:Set(indexCounter, vertexCounter)
            indices:Set(indexCounter + 1, vertexCounter + 1)
            indices:Set(indexCounter + 2, vertexCounter + 2)
            indices:Set(indexCounter + 3, vertexCounter + 2)
            indices:Set(indexCounter + 4, vertexCounter + 3)
            indices:Set(indexCounter + 5, vertexCounter)

            indexCounter = indexCounter + 6
            vertexCounter = vertexCounter + 4
        end

        mesh:SetIndices(indices)
    end

    action SetVertexData(ShaderProgram program, Drawable drawable, DrawableShape shape)
        number leftBorder = 0
        number topBorder = 0
        number rightBorder = 0
        number bottomBorder = 0

        number bottomLeftRounding = 0
        number bottomRightRounding = 0
        number topRightRounding = 0
        number topLeftRounding = 0

        number borderThickness = 0
        ColorGroup borderColor = undefined
        ColorGroup backgroundColor = undefined

        number width = drawable:GetWidth() * drawable:GetScaleX()
        number height = drawable:GetHeight() * drawable:GetScaleY()
        number aspectRatio = width / height

        boolean selected = false
        boolean renderBorders = false

        /*
        We want to get the control or layout properties used to define the element. We'll check these places, in order of priority:
        1. The drawable's shader data
        2. If the drawable itself is a Control
        3. If the drawable is a view, see if the view is attached to a Control
        */
        if drawable:GetShaderData() is LayoutProperties
            LayoutProperties properties = cast(LayoutProperties, drawable:GetShaderData())
            
            if properties:GetBorderThickness() > 0
                if properties:HasLeftBorder()
                    renderBorders = true
                    leftBorder = 1.0
                end
                if properties:HasTopBorder()
                    renderBorders = true
                    topBorder = 1.0
                end
                if properties:HasRightBorder()
                    renderBorders = true
                    rightBorder = 1.0
                end
                if properties:HasBottomBorder()
                    renderBorders = true
                    bottomBorder = 1.0
                end
            end

            bottomLeftRounding = properties:GetBottomLeftRounding()
            bottomRightRounding = properties:GetBottomRightRounding()
            topRightRounding = properties:GetTopRightRounding()
            topLeftRounding = properties:GetTopLeftRounding()

            borderThickness = (1.0 / height) * (properties:GetBorderThickness() * properties:GetInterfaceScale())
            if properties:GetBorderColor() not= undefined
                borderColor = properties:GetBorderColor():GetBottomLeft()
            end
            backgroundColor = properties:GetBackgroundColor()
        else
            Control control = undefined

            if drawable:GetShaderData() is Control
                control = cast(Control, drawable:GetShaderData())
            elseif drawable is Control
                control = cast(Control, cast(Object, drawable))
            elseif drawable is View2D
                View2D view = cast(View2D, cast(Object, drawable))
                Item2D item = view:GetItem()
                if item is Control
                    control = cast(Control, item)
                end
                if view is ToggleView
                    ToggleView toggle = cast(ToggleView, view)
                    selected = toggle:GetToggleState()
                end
            end

            if control not= undefined
                borderThickness = (1.0 / height) * (control:GetBorderThickness() * control:GetInterfaceScale())

                if control:GetBorderThickness() > 0
                    if control:HasLeftBorder()
                        renderBorders = true
                        leftBorder = borderThickness
                    end
                    if control:HasTopBorder()
                        renderBorders = true
                        topBorder = borderThickness
                    end
                    if control:HasRightBorder()
                        renderBorders = true
                        rightBorder = borderThickness
                    end
                    if control:HasBottomBorder()
                        renderBorders = true
                        bottomBorder = borderThickness
                    end
                end
    
                bottomLeftRounding = control:GetBottomLeftRounding()
                bottomRightRounding = control:GetBottomRightRounding()
                topRightRounding = control:GetTopRightRounding()
                topLeftRounding = control:GetTopLeftRounding()

                /*
                Determine what color the control should be, based on how the user is interacting with it.
                We check a hierarchy of potential events, falling through to the next case if the event
                wasn't fulfilled or if there was no color assigned to that event.
                */
                if control:IsRenderingMouseDown()
                    backgroundColor = control:GetMouseDownColor()
                end

                if control:IsRenderingSelection() or selected
                    if control:IsRenderingFocus() = false
                        if backgroundColor = undefined
                            backgroundColor = control:GetUnfocusedSelectionColor()
                        end
                        if borderColor = undefined
                            borderColor = control:GetUnfocusedSelectionBorderColor()
                        end
                    end

                    if backgroundColor = undefined
                        backgroundColor = control:GetSelectionColor()
                    end
                    if borderColor = undefined
                        borderColor = control:GetSelectionBorderColor()
                    end
                end

                if backgroundColor = undefined and control:IsRenderingMouseOver()
                    backgroundColor = control:GetMouseOverColor()
                end

                if control:IsFocused()
                    if backgroundColor = undefined
                        backgroundColor = control:GetFocusColor()
                    end

                    if borderColor = undefined
                        borderColor = control:GetFocusBorderColor()
                    end
                end

                if backgroundColor = undefined
                    backgroundColor = control:GetBackgroundColor()
                end

                // Get the border color, as well.
                if borderColor = undefined
                    borderColor = control:GetBorderColor()
                end
            end
        end

        if backgroundColor not= undefined
            drawable:GetDrawableShape():SetColor(backgroundColor)
        end

        integer vectorIndex = arrayIndex * 4

        roundingArray:Set(vectorIndex, bottomRightRounding)
        roundingArray:Set(vectorIndex + 1, topRightRounding)
        roundingArray:Set(vectorIndex + 2, bottomLeftRounding)
        roundingArray:Set(vectorIndex + 3, topLeftRounding)

        borderThicknessArray:Set(vectorIndex, leftBorder)
        borderThicknessArray:Set(vectorIndex + 1, topBorder)
        borderThicknessArray:Set(vectorIndex + 2, rightBorder)
        borderThicknessArray:Set(vectorIndex + 3, bottomBorder)

        aspectRatioArray:Set(arrayIndex, aspectRatio)
        antiAliasArray:Set(arrayIndex, 1.0 / height)

        if borderColor not= undefined
            if borderColor is Color
                Color border = cast(Color, borderColor)
                shape:SetDataForAllVertices(borderColorAttribute:offset, border:EncodeColorAsNumber())
            else
                Color bottomRight = borderColor:GetBottomRight()
                Color bottomLeft = borderColor:GetBottomLeft()
                Color topRight = borderColor:GetTopRight()
                Color topLeft = borderColor:GetTopLeft()
                shape:SetVertexData(borderColorAttribute:offset, bottomRight:EncodeColorAsNumber())
                shape:SetVertexData(borderColorAttribute:offset + 8, topRight:EncodeColorAsNumber())
                shape:SetVertexData(borderColorAttribute:offset + 16, bottomLeft:EncodeColorAsNumber())
                shape:SetVertexData(borderColorAttribute:offset + 24, topLeft:EncodeColorAsNumber())
            end
        else
            shape:SetDataForAllVertices(borderColorAttribute:offset, 0)
        end

        shape:SetDataForAllVertices(arrayIndexAttribute:offset, arrayIndex)

        arrayIndex = arrayIndex + 1
    end

    /*
    This action is called when it is time for the ShaderMapping to render its current batch with the given ShaderProgram.
    */
    action RenderBatch(ShaderProgram program, Camera camera)
        program:SetShaderInputArray("u_rounding[0]", roundingArray, 0, arrayIndex * 4)
        program:SetShaderInputArray("u_borderThickness[0]", borderThicknessArray, 0, arrayIndex * 4)
        program:SetShaderInputArray("u_aspectRatio[0]", aspectRatioArray, 0, arrayIndex)
        program:SetShaderInputArray("u_antiAliasDistance[0]", antiAliasArray, 0, arrayIndex)

        arrayIndex = 0

        parent:BatchingShaderMapping2D:RenderBatch(program, camera)
    end

end