package Libraries.Game.Graphics.Shaders

class BlockFragmentShader is Shader

    constant text SHADER_NAME = "Block Fragment Shader"

    on create
        SetName(SHADER_NAME)
        SetType(parent:Shader:FRAGMENT_SHADER)
        SetCode(GetVersionHeader() +
"
" +
"#ifdef GL_ES
    #define LOWP lowp
    precision mediump float;
#else
    #define LOWP 
#endif

in LOWP vec4 v_color;
in LOWP vec4 v_borderColor;
in vec2 v_texCoords;
in vec4 v_rounding;
in vec4 v_borderThickness;
in float v_aspectRatio;
in float v_antiAlias;
in vec3 v_notchSize;
in vec2 v_notchPositions;

uniform sampler2D u_texture;
uniform float u_notchHeight;

layout (location = 0) out vec4 outputColor;

// Calculate how far a point is from the outer shell of a rounded rectangle.
// The rounding vector contains edge roundness values for:
// x: top-right
// y: bottom-right
// z: top-left
// w: bottom-left
float signedDistanceRectangle(in vec2 point, in vec2 dimensions, in vec4 rounding)
{
    // We calculate as though the point is in the top-right quadrant.
    // If this isn't true, we need adjust which of the edge rounding values we use.
    rounding.xy = (point.x > 0.0) ? rounding.xy : rounding.zw;
    rounding.x = (point.y > 0.0) ? rounding.x : rounding.y;
    
    // We take the absolute value of the point to ensure we're calculating from the top-right quadrant
    vec2 calc = abs(point) - dimensions + rounding.x;
    return min(max(calc.x, calc.y), 0.0) + length(max(calc, 0.0)) - rounding.x;
}

// Signed distance function for trapezoids is adapted from work by Inigo Quilez on Shadertoy under the MIT License.
// https://www.shadertoy.com/view/MlycD3
// The MIT License
// Copyright ï¿½ 2018 Inigo Quilez
// Permission is hereby granted, free of charge, to any person obtaining a copy of this software and associated documentation files (the 'Software'), to deal in the Software without restriction, including without limitation the rights to use, copy, modify, merge, publish, distribute, sublicense, and/or sell copies of the Software, and to permit persons to whom the Software is furnished to do so, subject to the following conditions: The above copyright notice and this permission notice shall be included in all copies or substantial portions of the Software. THE SOFTWARE IS PROVIDED 'AS IS', WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.

// Get the length squared of the given vector via dot product.
float dot2(in vec2 v ) { return dot(v,v); }

// Calculate how far a point is from the outer shell of a trapezoid, aligned with the y-axis.
float signedDistanceTrapezoid(in vec2 p, in float r1, float r2, float he )
{
    vec2 k1 = vec2(r2,he);
    vec2 k2 = vec2(r2-r1,2.0*he);

    p.x = abs(p.x);
    vec2 ca = vec2(max(0.0,p.x-((p.y<0.0)?r1:r2)), abs(p.y)-he);
    vec2 cb = p - k1 + k2*clamp( dot(k1-p,k2)/dot2(k2), 0.0, 1.0 );
    
    float s = (cb.x < 0.0 && ca.y < 0.0) ? -1.0 : 1.0;
    
    return s*sqrt( min(dot2(ca),dot2(cb)) );
}

void main()
{
    vec4 rounding = v_rounding;
    vec4 borderThickness = v_borderThickness;
    float aspectRatio = v_aspectRatio;
    float antiAliasDistance = v_antiAlias;

    float notchBottomWidth = v_notchSize.x;
    float notchTopWidth = v_notchSize.z;
    float notchHeight = v_notchSize.y;

    vec4 borderColor = v_borderColor;
    bool renderBorders = (borderThickness.x + borderThickness.y + borderThickness.z + borderThickness.w) > 0.0;

    vec2 point = (v_texCoords.xy - 0.5) * 2.0;
    point.x *= aspectRatio;
    point.y += notchHeight;
    vec2 dimensions = vec2(aspectRatio, 1.0 - notchHeight);

    float distance = signedDistanceRectangle(point, dimensions, rounding);
    vec4 resultColor;

    if (renderBorders)
    {
        // The enabled borders vector stores values for the sides in the following order:
        // x: right
        // y: top (y-positive: technically the bottom due to how GLSL handles texture coordinates)
        // z: left
        // w: bottom (y-negative)
        vec2 fillPoint = point - vec2(borderThickness.x - borderThickness.z, borderThickness.y - borderThickness.w);
        vec2 fillDimensions = vec2(dimensions.x - borderThickness.x - borderThickness.z, dimensions.y - borderThickness.y - borderThickness.w);
        float fillDistance = signedDistanceRectangle(fillPoint, fillDimensions, rounding);

        float trapDistance = signedDistanceTrapezoid(vec2((v_notchPositions.x - 0.5) * 2.0 * aspectRatio - point.x, 1.0 - notchHeight - point.y), notchBottomWidth, notchTopWidth, 2.0 * notchHeight);
        float finalDistance = min(distance, trapDistance);
        float carveDistance = signedDistanceTrapezoid(vec2((v_notchPositions.y - 0.5) * 2.0 * aspectRatio - point.x, -1.0 + notchHeight - point.y), notchBottomWidth, notchTopWidth, 2.0 * notchHeight);
        finalDistance = max(finalDistance, -carveDistance);

        resultColor = mix(borderColor * texture(u_texture, v_texCoords), vec4(borderColor.rgb, 0.0), smoothstep(0.0, antiAliasDistance, finalDistance));
        resultColor = mix(v_color * texture(u_texture, v_texCoords), resultColor, smoothstep(0.0, antiAliasDistance, finalDistance + 2.0 * borderThickness.y));
    }
    else
    {
        resultColor = v_color * texture(u_texture, v_texCoords);
        resultColor = mix(resultColor, vec4(resultColor.rgb, 0.0), smoothstep(0.0, antiAliasDistance, distance));
    }

    outputColor = resultColor;
}")
    end

end