package Libraries.Game.Graphics.ModelData

use Libraries.Containers.Array
use Libraries.Containers.Iterator
use Libraries.Compute.Vector3
use Libraries.Compute.Matrix4
use Libraries.Compute.Quaternion
use Libraries.Game.BoundingBox

class Node 
    
    public text id = ""

    private Node parentNode = undefined
    private Array<Node> children
    public boolean inheritTransform = true

    boolean isAnimated = false

    public Vector3 translation
    public Quaternion rotation
    public Vector3 scale
    public Matrix4 localTransform
    public Matrix4 globalTransform

    public Array<NodePart> parts

    on create
        rotation:Set(0, 0, 0, 1)
        scale:Set(1, 1, 1)
    end

    /*
    This action will calculate the node's local transform based on its
    translation, scale, and rotation. It then returns the local transform.
    */
    action CalculateLocalTransform returns Matrix4
        if not isAnimated
            localTransform:Set(translation, rotation, scale)
        end
        return localTransform
    end

    /*
    This action will calculate the node's world transform, which is the product
    of its local transform and its parent's world transform. It then returns the
    world transform. In the case that this node has no parent, the local
    transform will be returned.
    */
    action CalculateWorldTransform returns Matrix4
        if inheritTransform and parentNode not= undefined
            globalTransform:Set(parentNode:globalTransform)
            globalTransform:Multiply(localTransform)
        else
            globalTransform:Set(localTransform)
        end
        return globalTransform
    end

    /*
    This action calculates the local and world transforms for this node. This
    does not calculate the transforms of children nodes -- to do that, use
    CalculateAllTransforms().
    */
    action CalculateTransforms
        CalculateLocalTransform()
        CalculateWorldTransform()
    end

    /*
    This action calculates the local and world transforms of the entire children
    hierarchy of this node.
    */
    private action CalculateChildrenTransforms
	Node child = undefined
	integer index = 0
	repeat while index < children:GetSize()
	    child = children:Get(index)
	    child:CalculateAllTransforms()
	    index = index + 1
	end
    end

    /*
    This action calculates the local and world transforms of this node and for 
    its entire children hiearchy.
    */
    action CalculateAllTransforms
        CalculateTransforms()
        CalculateChildrenTransforms()
    end

    /*
    public void calculateBoneTransforms (boolean recursive) {
		for (final NodePart part : parts) {
			if (part.invBoneBindTransforms == null || part.bones == null || part.invBoneBindTransforms.size != part.bones.length)
				continue;
			final int n = part.invBoneBindTransforms.size;
			for (int i = 0; i < n; i++)
				part.bones[i].set(part.invBoneBindTransforms.keys[i].globalTransform).mul(part.invBoneBindTransforms.values[i]);
		}
		if (recursive) {
			for (Node child : children) {
				child.calculateBoneTransforms(true);
			}
		}
	}
    */

    /*
    This action calculates the bone transforms for this node. This does not
    calculate the bone transforms of children nodes -- to do that, use
    CalculateAllBoneTransforms().
    */
    action CalculateBoneTransforms
        NodePart part = undefined
        integer index = 0
        repeat while index < parts:GetSize()
            part = parts:Get(index)
            if (part:invBoneBindTransforms not= undefined and part:bones not= undefined and part:invBoneBindTransforms:GetSize() = part:bones:GetSize())
                integer counter = 0
                Iterator<Node> keyIterator = part:invBoneBindTransforms:GetKeyIterator()
                Node key = undefined
                Matrix4 value = undefined
                repeat while keyIterator:HasNext()
                    key = keyIterator:Next()
                    value = part:invBoneBindTransforms:GetValue(key)
                    part:bones:Get(counter):Set(key:globalTransform)
                    part:bones:Get(counter):Multiply(value)
                    counter = counter + 1
                end
            end
            index = index + 1
        end
    end

    /*
    This action calculates the bone transforms of the entire children
    hierarchy of this node.
    */
    private action CalculateChildrenBoneTransforms
	Node child = undefined
	integer index = 0
	repeat while index < children:GetSize()
	    child = children:Get(index)
	    child:CalculateAllBoneTransforms()
	    index = index + 1
	end
    end

    /*
    This action calculates the bone transforms of this node and for 
    its entire children hiearchy.
    */
    action CalculateAllBoneTransforms
        CalculateBoneTransforms()
        CalculateChildrenBoneTransforms()
    end

    /*
    This action will add a new node to this node's children hierarchy.
    */
    action Add(Node node)
       Add(-1, node) 
    end

    /*
    This action will insert a new node in this node's children hierarchy at the
    given index.
    */
    action Add(integer index, Node node)
        Node temp = me
        integer counter = 0
        repeat while temp not= undefined
            if temp = node
                alert("Can't add a parent node as a child!")
            end
            temp = temp:GetParent()
        end

        temp = node:GetParent()
        if temp not= undefined and not temp:Remove(node)
            alert("Couldn't remove the new child from its current parent!")
        end
        if index < 0 or index >= children:GetSize()
            index = children:GetSize()
            children:Add(node)
        else
            children:Add(index, node)
        end
        node:SetParent(me)
    end

    /*
    This action removes a node from the children hierarchy.
    */
    action Remove(Node node) returns boolean
        if not children:Remove(node) 
            return false
        end
        node:SetParent(undefined)
        return true
    end

    /*
    This action returns this node's parent node.
    */
    action GetParent returns Node
        return undefined
    end

    /*
    This action will set the parent of this node. This information is
    automatically set when the node is added to another node.
    */
    action SetParent(Node node)
        parentNode = node
    end

    /*
    This action returns whether or not this node has a parent node.
    */
    action HasParent returns boolean
        return parentNode not= undefined
    end
    
    /*
    This action returns whether or not this node has any children nodes that
    have been added to it.
    */
    action HasChildren returns boolean
        return children:IsEmpty()
    end

    /*
    This action will return the number of nodes that are direct children of this
    node, i.e., they have been specifically added to this node.
    */
    action GetChildrenCount returns integer
        return children:GetSize()
    end

    /*
    This action returns the child stored at the given index in this node's
    children hierarchy.
    */
    action GetChild(integer index) returns Node
        return children:Get(index)
    end

    /*
    This action will return the child node with the given ID in this node's
    immediate children hierarchy. The case of the ID is ignored. If no node is
    found, undefined is returned.
    */
    action GetImmediateChild(text id) returns Node
        integer counter = 0
        repeat children:GetSize() times
            if children:Get(counter):id:EqualsIgnoringCase(id)
                return children:Get(counter)
            end
            counter = counter + 1
        end
        return undefined
    end

    /*
    This action will search the entire children hierarchy of this node, and 
    return the child node with the given ID. The case of the ID is ignored. 
    If no node is found, undefined is returned.
    */
    action GetChild(text id) returns Node
        integer counter = 0
        repeat children:GetSize() times
            if children:Get(counter):id:EqualsIgnoringCase(id)
                return children:Get(counter)
            end
            counter = counter + 1
        end
        counter = 0
        Node temp = undefined
        repeat children:GetSize() times
            temp = children:Get(counter):GetChild(id)
            if temp not= undefined
                return temp
            end
            counter = counter + 1
        end
        return undefined
    end

    /*
    This action returns an iterator containing all of the direct children of
    this node, i.e., they have been specifically added to this node.
    */
    action GetChildren returns Iterator<Node>
        return children:GetIterator()
    end

    /*
    This action will create a new BoundingBox object that contains the bounds of
    this node.
    */
    action CalculateBoundingBox returns BoundingBox
        BoundingBox box
        return CalculateBoundingBox(box)
    end

    /*
    This action will set the given BoundingBox to contain the bounds of this
    node. Any data that was in the BoundingBox before this action will be
    overwritten.
    */
    action CalculateBoundingBox(BoundingBox box) returns BoundingBox
        box:Invalidate()
        return ExtendBoundingBox(box)
    end

    /*
    This action will set the given BoundingBox to contain the bounds of this
    node. The boolean parameter will determine whether to apply the node's
    global transform to the BoundingBox. If the value is true, the transform
    will be applied.
    */
    action CalculateBoundingBox(BoundingBox box, boolean applyTransform) returns BoundingBox
        box:Invalidate()
        return ExtendBoundingBox(box, applyTransform)
    end

    /*
    This action will extend the given BoundingBox to contain the bounds of this
    node. The global transform of this node will be applied when calculating the
    bounds.
    */
    action ExtendBoundingBox(BoundingBox box) returns BoundingBox
        return ExtendBoundingBox(box, true)
    end

    /*
    This action will extend the given BoundingBox to contain the bounds of this
    node. The boolean parameter dictates whether or not to use the global
    transform - if the boolean is true, the bounds will be offset by the global
    transform. If it is false, the global transform will be ignored.
    */
    action ExtendBoundingBox(BoundingBox box, boolean applyTransform) returns BoundingBox
        integer partCount = parts:GetSize()
        integer counter = 0
        repeat partCount times
            NodePart part = parts:Get(counter)
            
            if part:enabled
                MeshPart meshPart = part:meshPart
                if applyTransform
                    meshPart:mesh:ExtendBoundingBox(box, meshPart:indexOffset, meshPart:verticesCount, globalTransform)
                else
                    meshPart:mesh:ExtendBoundingBox(box, meshPart:indexOffset, meshPart:verticesCount)
                end
            end

            counter = counter + 1
        end

        counter = 0
        repeat children:GetSize() times
            children:Get(counter):ExtendBoundingBox(box)
            counter = counter + 1
        end

        return box
    end

end

/*


	/** Adds the specified nodes as the currently last child of this node. If the node is already a child of another node, then it
	 * is removed from its current parent.
	 * @param nodes The Node to add as child of this Node
	 * @return the zero-based index of the first added child *-
	public <T extends Node> int addChildren (final Iterable<T> nodes) {
		return insertChildren(-1, nodes);
	}

	/** Insert the specified nodes as children of this node at the specified index. If the node is already a child of another node,
	 * then it is removed from its current parent. If the specified index is less than zero or equal or greater than
	 * {@link #getChildCount()} then the Node is added as the currently last child.
	 * @param index The zero-based index at which to add the child
	 * @param nodes The nodes to add as child of this Node
	 * @return the zero-based index of the first inserted child *-
	public <T extends Node> int insertChildren (int index, final Iterable<T> nodes) {
		if (index < 0 || index > children.size)
			index = children.size;
		int i = index;
		for (T child : nodes)
			insertChild(i++, child);
		return index;
	}
*/