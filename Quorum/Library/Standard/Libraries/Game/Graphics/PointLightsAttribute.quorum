package Libraries.Game.Graphics

use Libraries.Game.Graphics.Attribute
use Libraries.Game.Graphics.PointLight
use Libraries.Containers.Array

class PointLightsAttribute is Attribute
    
    public constant text POINT_LIGHTS_ALIAS = "pointLights"

    AttributeManager manager

    public constant integer POINT_LIGHTS = manager:Register(POINT_LIGHTS_ALIAS)

    /*
    An array of all point lights which are stored in this attribute.
    */
    public Array<PointLight> lights

    on create
        SetAttribute(GetPointLightsValue())
    end
    
    /*
    This action will copy the data from the given PointLightsAttribute into this
    object. Any data previously stored in this object will be replaced.
    */
    action CopyData(PointLightsAttribute copyFrom)
        lights:Empty()
        integer index = 0
        repeat copyFrom:lights:GetSize() times
            lights:Add(copyFrom:lights:Get(index))
            index = index + 1
        end
    end

    /*
    This action will return a new PointLightsAttribute object which is a copy
    of this object.
    */
    action Copy returns Attribute
        PointLightsAttribute new
        new:CopyData(me)
        return new
    end

    /*
    Returns the integer value representing a PointLightsAttribute.
    */
    action GetPointLightsValue returns integer
        return POINT_LIGHTS
    end

end