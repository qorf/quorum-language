package Libraries.Web.Page

use Libraries.Web.Page.WebTag
use Libraries.Web.Page.Attribute
use Libraries.Web.Page.Attributes

/*
The Option class represents HTML's (Hypertext Markup Language) option tag which 
is used to define an option item inside a select or datalist. 
You can find more information about this tag at:
<a href="http://www.w3schools.com/tags/tag_option.asp">The option attribute</a>.

Attribute: Example
use Libraries.Web.Page.all
class Main
   action main
      WebPage page
      
      Select select
      Option option
      option:SetLabel("Alabama")
      select:AddOption(option)

      page:AddToBody(select)
      output page:Generate()
   end
end
*/
class Option is WebTag
    constant text disabled = "disabled"
    constant text label = "label"
    constant text selected = "selected"
    constant text defaultValue = "value"

    /*
        This action gets wether the option in a datalist or select is disabled (true)
        or enabled(false).

        Attribute: Returns True if diabled and false if enabled. 

        Attribute: Example
        use Libraries.Web.Page.Option
        Option opt
        boolean result = opt:GetDisabled()
        
    */
    action GetDisabled returns boolean
        Attribute disable = GetAttribute(me:disabled)
        return disable:GetValue() = me:disabled
    end
    
    /*
        This action sets the option to be disabled if true and enabled if false.

        Attribute: Parameter value True to disable and false to enable the option. 

        Attribute: Example
        use Libraries.Web.Page.Option
        Option opt
        opt:SetDisabled(true)
        
    */
    action SetDisabled (boolean value)
        if value
            AddAttribute(disabled, disabled)
        else
            RemoveAttribute(disabled)
        end
    end

    /*
        This action gets the options labeling text.

        Attribute: Returns The label. 

        Attribute: Example
        use Libraries.Web.Page.Option
        use Libraries.Web.Page.Attribute
        Option opt
        Attribute result = opt:GetLabel()
        
    */
    action GetLabel returns Attribute
        return me:GetAttribute(label)
    end
    
    /*
        This action sets the options labeling text.

        Attribute: Parameter value The labeling text. 

        Attribute: Example
        use Libraries.Web.Page.Option
        Option opt
        opt:SetLabel("Alabama")
        
    */
    action SetLabel(text value)
        AddAttribute(label, value)
    end

    /*
        This action gets wether the option in a datalist or select is selected
        or not.

        Attribute: Returns The attribute containing the select information. 

        Attribute: Example
        use Libraries.Web.Page.Option
        Option opt
        boolean result = opt:GetSelected()
        
    */
    action GetSelected returns boolean
        Attribute selected = GetAttribute(me:selected)
        return selected:GetValue() = me:selected
    end
    
    /*
        This action sets the option to be selected if true and unselected if false.

        Attribute: Parameter value True to select and false to unselect the option. 

        Attribute: Example
        use Libraries.Web.Page.Option
        Option opt
        opt:SetSelected(true)
        
    */
    action SetSelected(boolean value)
        if value
            AddAttribute(selected, selected)
        else
            RemoveAttribute(selected)
        end
    end

    /*
        This action gets the default value of the option.

        Attribute: Returns The default value. 

        Attribute: Example
        use Libraries.Web.Page.Option
        use Libraries.Web.Page.Attribute
        Option opt
        Attribute result = opt:GetDefaultValue()
        
    */
    action GetDefaultValue returns Attribute
        return me:GetAttribute(defaultValue)
    end
    
    /*
        This action sets the option to have a default value that is sent to the server.

        Attribute: Parameter value The value to be sent to the server. 

        Attribute: Example
        use Libraries.Web.Page.Option
        Option opt
        opt:SetDefaultValue("my text")
        
    */
    action SetDefaultValue(text value)
        AddAttribute(defaultValue, value)
    end

    action Generate returns text
        text result = "<option " 
        Attributes attributes = parent:WebTag:GetAttributes()
        attributeText = attributes:Generate()
        result = result + attributeText + ">"
        result = result + me:GenerateNestedTags()
        result = result + "</option>"
        return result
    end
end