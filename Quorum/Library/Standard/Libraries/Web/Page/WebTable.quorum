package Libraries.Web.Page

use Libraries.Web.Page.WebTag
use Libraries.Containers.List
use Libraries.Containers.Iterator
use Libraries.Compute.Matrix
use Libraries.Compute.Statistics.DataFrameColumn
use Libraries.Compute.Statistics.DataFrame

/*
The WebTable class represents HTML's (Hypertext Markup Language) table tag which 
is a class that builds a table. This table contains any number
of rows that have been added to the table. 

Attribute: Parameter matrix This is the matrix we are converting to HTML on the web

Attribute: Example
use Libraries.Compute.Statistics.DataFrame
use Libraries.Web.Page.TableCaption
use Libraries.Web.Page.WebTable

DataFrame frame
    frame:LoadFromCommaSeparatedValue(
"A,B
1,2
3,4
5,6
7,8
9,10"
)
    WebTable web
    TableCaption caption
    caption:AddText("My Table")
    web:Add(caption)
    web:Add(frame)

    text value = Generate()
    output value
*/
class WebTable is WebTag
    constant text border = "border"

    /*
        This action creates a WebTable object from a two dimensional matrix created with the Matrix class.

        Attribute: Example

        class Main 
        action Main
            Matrix matrix
            matrix:Fill(3, 3, 1.1)

            WebTable table
            table:Add(matrix)
            output table:Generate()
        end
    */
    action Add(Matrix matrix)
        TableBody body
        row = 0
        repeat while row < matrix:GetRows()
            col = 0
            TableRow tr
            repeat while col < matrix:GetColumns()
                TableData data
                data:AddText("" + matrix:Get(row, col))
                tr:Add(data)
                col = col + 1
            end
            body:Add(tr)
            row = row + 1
        end
        me:Add(body)
    end

    action Add(DataFrame frame)
        TableBody body

        boolean hasRow = true
        if frame:GetSize() = 0
            return now
        end

        TableRow headerRow
        col = 0
        repeat while col < frame:GetSize()
            DataFrameColumn column = frame:GetColumn(col)
            text header = column:GetHeader()
            TableHeaderCell th
            if header = undefined
                th:AddText("")
            else
                th:AddText(header)
            end

            th:AddAttribute("scope", "col")
            headerRow:Add(th)
            col = col + 1
        end
        body:Add(headerRow)

        row = 0
        repeat while hasRow
            col = 0
            hasRow = false//assume empty until proven true
            TableRow tr
            repeat while col < frame:GetSize()
                DataFrameColumn column = frame:GetColumn(col)
                
                if row < column:GetSize()
                    text value = column:GetAsText(row)
                    TableData data
                    data:AddText(value)
                    data:AddAttribute("scope", "row")
                    tr:Add(data)
                end

                if row + 1 < column:GetSize()
                    hasRow = true
                end
                col = col + 1
            end
            row = row + 1
            body:Add(tr)
        end
        me:Add(body)
    end

    /*
        This action gets the border attribute of the web table and returns true
        if there is a border around the cells and false if it does not.

        Attribute: Returns True if the cells have a border and false if not. 

        Attribute: Example

        use Libraries.Web.Page.WebTable
        WebTable table
        boolean hasBorder = table:GetBorder()
    */
    action GetBorder returns boolean
            Attribute b = GetAttribute(border)
            if b = undefined
                return false
            elseif b:GetValue() = ""
                return false
            else
                return true
            end 
    end
    
    /*
        This action sets the border attribute of the web table. If set to true
        there is a border around the cells and if false the cells do not have a 
        a border.

        Attribute: Parameter value True for a border and false for no border. 

        Attribute: Example
        use Libraries.Web.Page.WebTable
        WebTable table
        table:SetBorder(true)
    */
    action SetBorder(boolean value) 
        if value
            AddAttribute(border,"1")
        else
            AddAttribute(border, "")
        end
    end

    /*
        This action places an TableCaption object inside of a WebTable object.

        Attribute: Parameter content specifies what will be added to the WebTable.

        Attribute: Example
        use Libraries.Web.Page.all

        class Main
            action Main
                WebPage page
                WebTable table
                TableCaption caption

                
                page:Add(table)
                caption:AddText("This is a table!")
                table:Add(caption)

                text value = page:Generate()
                output value
            end
        end
    */
    action Add(TableCaption content)
        parent:WebTag:tags:Add(content)
    end

    /*
        This action places an TableCaption object inside of a WebTable object.

        Attribute: Parameter content specifies what will be added to the WebTable.

        Attribute: Example
        use Libraries.Web.Page.all

        class Main
            action Main
                WebPage page
                WebTable table
                TableCaption caption

                
                page:Add(table)
                caption:AddText("This is a table!")
                table:Add(caption)

                text value = page:Generate()
                output value
            end
        end
    */
    action AddCaption(text caption)
        TableCaption cap
        cap:AddText(caption)
        Add(cap)
    end

    /*
        This action places an ColumnGroup object inside of a WebTable object.

        Attribute: Parameter content specifies what will be added to the WebTable.

        Attribute: Example
        use Libraries.Web.Page.all

        class Main
            action Main
                WebPage page
                WebTable table
                ColumnGroup group
                Column column

                
                page:Add(table)
                group:Add(column)
                table:Add(group)
                text value = page:Generate()
                output value
            end
        end
    */

    action Add(ColumnGroup content)
        parent:WebTag:tags:Add(content)
    end

    /*
        This action places an TableHeader object inside of a WebTable object.

        Attribute: Parameter content specifies what will be added to the WebTable.

        Attribute: Example
        use Libraries.Web.Page.all

        class Main
            action Main
                WebPage page
                WebTable table
                TableHeader header

                
                page:Add(table)
                header:SetDescription("My Table")
                table:Add(header)
                text value = page:Generate()
                output value
            end
        end
    */
    action Add(TableHeader content)
        parent:WebTag:tags:Add(content)
    end

    /*
        This action places an TableFooter object inside of a WebTable object.

        Attribute: Parameter content specifies what will be added to the WebTable.

        Attribute: Example
        use Libraries.Web.Page.all

        class Main
            action Main
                WebPage page
                WebTable table
                TableFooter footer

                
                page:Add(table)
                footer:SetDescription("Table 1: A list of things")
                table:Add(footer)
                text value = page:Generate()
                output value
            end
        end
    */

    action Add(TableFooter content)
        parent:WebTag:tags:Add(content)
    end

    /*
        This action places an TableBody object inside of a WebTable object.
        If a TableBody exists in a WebTable, then TableRows must only be declared
        within TableBody objects, rather than in the WebTable directly.

        Attribute: Parameter content specifies what will be added to the WebTable.

        Attribute: Example
        use Libraries.Web.Page.all

        class Main
            action Main
                WebPage page
                WebTable table
                TableBody tbody
                TableRow row

                
                page:Add(table)
                table:Add(tbody)
                tpage:Add(row)
                text value = page:Generate()
                output value
            end
        end
    */
    action Add(TableBody content)
        parent:WebTag:tags:Add(content)
    end

    /*
        This action places an TableRow object inside of a WebTable object.
        There can only be TableRow objects directly in a WebTable if no TableBody
        is present within the table.

        Attribute: Parameter content specifies what will be added to the WebTable.

        Attribute: Example
        use Libraries.Web.Page.all

        class Main
            action Main
                WebPage page
                WebTable table
                TableRow row

                
                page:Add(table)
                table:Add(row)
                text value = page:Generate()
                output value
            end
        end
    */
    action Add(TableRow content)
        parent:WebTag:tags:Add(content)
    end

    action Generate returns text
        validateTags()
        text result = "<table " 
        Attributes attributes = parent:WebTag:GetAttributes()
        attributeText = attributes:Generate()
        result = result + attributeText + ">"
        result = result + me:GenerateNestedTags()
        result = result + "</table>"
        return result
    end

//optional caption, colgroup*, optional thead and/or tfoot, (tbody* + tr+), optional tfoot (if it wasn't used before)
    private action validateTags
        List<WebTag> tags = parent:WebTag:tags
        boolean correct = true
        boolean hasTableFooterInStart = false

        Iterator<WebTag> iterator = tags:GetIterator()
        
        //can be empty
        if not iterator:HasNext()
            return now
        end

        WebTag value = iterator:Next()
        if value is TableCaption
            if iterator:HasNext()
                value = iterator:Next()
                if value is TableCaption
                    alert("The table caption was already declared; only one caption is allowed per table.")
                end
            else 
                return now
            end
        end

        repeat while value is ColumnGroup
            if iterator:HasNext()
                value = iterator:Next()
            else 
                return now
            end
        end

        if value is TableHeader
            if iterator:HasNext()
                value = iterator:Next()
                if value is TableHeader
                    alert("The table header was already declared; only one table header is allowed per table.")
                end
            else 
                return now
            end
        end

        if value is TableFooter
            hasTableFooterInStart = true
            if iterator:HasNext()
                value = iterator:Next()
                if value is TableFooter
                    alert("The table footer was already declared; only one table footer is allowed per table.")
                end
            else 
                return now
            end
        end

        boolean hasBody = false
        repeat while value is TableBody
            hasBody = true
            if iterator:HasNext()
                value = iterator:Next()
            else 
                return now
            end
        end

        //if there are no bodies, is the row issue satisfied?
        //You can add rows by themselves or as part of a TableBody element, but not both?
        if value is TableRow
            if not hasBody
                if iterator:HasNext()
                    value = iterator:Next()
                else 
                    return now
                end
            else
                alert("Since a table body was declared for the table, all table rows must be added to a table body rather than directly to the table itself.")
            end

            repeat while value is TableRow
                if iterator:HasNext()
                    value = iterator:Next()
                else 
                    return now
                end
            end
        end

        if value is TableFooter
            if not hasTableFooterInStart
                return now
            else
                alert("The Table Footer was already defined in the table, but it can only appear once.")
            end
        end

        alert("I found a tag with the HTML: " + value:Generate() + ", but this is not allowed at this point in the table.")
    end
end
