package Libraries.Web.Page

use Libraries.Containers.HashTable
use Libraries.Containers.List
use Libraries.Containers.Iterator
use Libraries.Web.Page.GlobalAttributeAccepter
use Libraries.Web.Page.PlainText

/*
The WebTag class is a generic helper class which manages Hypertext Markup Language 
(HTML) 5 tags. Tags are stored in a list of WebTags, thus, allowing tags to
be nested within eachother. This class should not be used to generate webpages.
It's subclasses should be used instead.


Attribute: Example

use Libraries.Web.Page.all

WebPage page

//bold is a WebTag
Bold bold
bold:SetDescription("Quorum")

//automatically add to the body of a WebPage object.
//if you want to share bodies between web pages, you can instantiate a body
//class and pass it to various WebPage objects.
page:AddToBody(bold)
output page:Generate()
*/
class WebTag is GlobalAttributeAccepter
    List<WebTag> tags

    /*
        This action adds a WebTag to the list of WebTags contained within the 
        current WebTag.

        Attribute: Parameter tag The WebTag to be added to the list of sub-WebTags. 

        Attribute: Example
        use Libraries.Web.Page.TableRow
        use Libraries.Web.Page.TableData


        //TableRow and TableData are both WebTags
        TableRow row
        TableData cell
        cell:SetDescription("name")
        row:Add(cell)
        
    */
    action AddNestedTag(WebTag tag)
        tags:Add(tag)
    end

    /*
        This action removes the first instance of a webtag found in the list
        of sub-WebTags.

        Attribute: Parameter tag The WebTag to remove from the list of WebTags. 

        Attribute: Example
        use Libraries.Web.Page.TableRow
        use Libraries.Web.Page.TableData

        //TableRow and TableData are both WebTags
        TableRow row
        TableData cell
        cell:SetDescription("name")
        row:Add(cell)
        row:Remove(cell)
        
    */
    action Remove(WebTag tag)
        tags:Remove(tag)
    end

    /*
        This action gets the number of nested tags within the current WebTag.

        Attribute: Returns The number of nested tags. 

        Attribute: Example
        use Libraries.Web.Page.TableRow
        use Libraries.Web.Page.TableData

        //TableRow and TableData are both WebTags
        TableRow row
        TableData cell
        cell:SetDescription("name")
        row:Add(cell)
        integer numTags = row:GetSize()
        
    */
    action GetNumberOfNestedTags returns integer
        return tags:GetSize()
    end

    /*
        This action gets the number of nested tags within the current WebTag.

        Attribute: Returns The number of nested tags. 

        Attribute: Example
        use Libraries.Web.Page.TableRow
        use Libraries.Web.Page.TableData

        //TableRow and TableData are both WebTags
        TableRow row
        TableData cell
        cell:SetDescription("name")
        row:Add(cell)
        WebTag tag = row:GetTag(0)
        
    */
    action GetTag(integer index) returns WebTag
        return tags:Get(index)
    end

    /*
        This action generates the HTML output text of all of the nested WebTags.

        Attribute: Returns The text including all webtags within this WebTag. 

        Attribute: Example
        use Libraries.Web.Page.TableRow
        use Libraries.Web.Page.TableData

        //TableRow and TableData are both WebTags
        TableRow row
        TableData cell
        cell:SetDescription("name")
        row:Add(cell)
        output row:GenerateNestedTags()
        
    */
    action GenerateNestedTags returns text
        text result = ""
        Iterator<WebTag> iterator = tags:GetIterator()
        repeat while iterator:HasNext()
            WebTag tag = iterator:Next()
            result = result + tag:Generate()
        end
        return result
    end

    /*
        This action sets the description text for this WebTag.

        Attribute: Parameter value The text contained in the WebTag. 

        Attribute: Example
        use Libraries.Web.Page.TableRow
        use Libraries.Web.Page.TableData

        //TableRow and TableData are both WebTags
        TableRow row
        TableData cell
        cell:AddDescription("name")
        row:Add(cell)
        
    */
    action AddText(text value)
        PlainText t
        t:SetText(value)
        tags:Add(t)
    end

    /*
        This action generates text for a particular web page. Subclasses
        with a Generate method should be sure to honor nested tags or attributes
        if it is appropriate for a particular tag.

        Attribute: Returns This returns text in hypertext markup language (HTML)
            representing the tag.

        Attribute: Example
        use Libraries.Web.Page.WebPage

        WebPage page
        text result = page:Generate()
        //output out the web page
        output result
    */
    blueprint action Generate returns text
end