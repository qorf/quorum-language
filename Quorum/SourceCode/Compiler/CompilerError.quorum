package Libraries.Language.Compile

use Libraries.System.File
use Libraries.Containers.List
use Libraries.Containers.Iterator
use Libraries.Language.Compile.Hints.Hint

class CompilerError is Location
    text message = ""
    CompilerErrorType errorType = undefined

    action GetCompilerErrorType returns CompilerErrorType
        if errorType not= undefined
            return errorType:Copy()
        end
        return undefined
    end

    action SetCompilerErrorType(CompilerErrorType type)
        if type not= undefined
            errorType = type:Copy()
        else 
            errorType = undefined
        end
    end

    action GetErrorMessage returns text
        return message
    end

    action GetDisplayName returns text
        File file = GetFile()
        text path = ""

        if file not= undefined
            path = file:GetAbsolutePath()
        end
        text line = "" + GetLineNumber()
        text column = "" + GetColumnNumber()
        return path + ", Line " + line + ", Column " + column + ": " + message
    end

    action GetShortDisplayName returns text
        text line = "" + GetLineNumber()
        text column = "" + GetColumnNumber()
        return "Line " + line + ", Column " + column + ": " + message
    end

    action SetErrorMessage(text value) 
        message = value
    end

    action GetAbsolutePath returns text
        File file = GetFile()
        if file not= undefined
            return file:GetAbsolutePath()
        end

        text value = ""
        return value
    end

    action GetStaticKey returns text
        return GetAbsolutePath()
    end
end