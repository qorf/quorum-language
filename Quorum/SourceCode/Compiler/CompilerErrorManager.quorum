package Libraries.Language.Compile

use Libraries.Containers.HashTable
use Libraries.Containers.List
use Libraries.System.File
use Libraries.Containers.Iterator
use Libraries.Language.Compile.Hints.Hint
use Libraries.Web.Page.Division
use Libraries.Web.Page.OrderedList
use Libraries.Data.Formats.JavaScriptObjectNotation

class CompilerErrorManager
    integer numberOfErrors = 0
    integer numberOfHints = 0

    /*
     * Records the errorsPerFile using the full path of the file as
     * the key and the list for that file as the value.
     */
    HashTable<text, List<CompilerError>> errorsPerFile
    HashTable<text, List<Hint>> hintsPerFile

    File currentFile = undefined
    //text errorKey = ""
    text fileNameKey = ""

    action Add(CompilerError error) 
        if error not= undefined
            numberOfErrors = numberOfErrors + 1
            List<CompilerError> l = errorsPerFile:GetValue(error:GetStaticKey())
            if(l not= undefined) 
                l:Add(error)
            else 
                List<CompilerError> l2
                l = l2
                l:Add(error)
                errorsPerFile:Add(error:GetStaticKey(), l)
            end
        end
    end

    action Add(Hint hint) 
        if hint not= undefined
            numberOfHints = numberOfHints + 1
            List<Hint> l = hintsPerFile:GetValue(hint:GetStaticKey())
            if(l not= undefined) 
                l:Add(hint)
            else 
                List<Hint> l2
                l = l2
                l:Add(hint)
                hintsPerFile:Add(hint:GetStaticKey(), l)
            end
        end
    end

    action ChangeErrorKey(text key, text newKey)
        List<CompilerError> list = errorsPerFile:RemoveKey(key)
        if list = undefined
            return now
        end
        Iterator<CompilerError> iterator = list:GetIterator()
        File file
        file:SetAbsolutePath(newKey)
        repeat while iterator:HasNext()
            CompilerError error = iterator:Next()
            error:SetFile(file)
        end

        errorsPerFile:Add(newKey, list)
    end

    action ChangeHintKey(text key, text newKey)
        List<Hint> list = hintsPerFile:RemoveKey(key)
        if list = undefined
            return now
        end
        Iterator<Hint> iterator = list:GetIterator()
        File file
        file:SetAbsolutePath(newKey)
        repeat while iterator:HasNext()
            Hint hint = iterator:Next()
            hint:SetFile(file)
        end

        hintsPerFile:Add(newKey, list)
    end

    action RemoveErrorsAtKey(text key) 
        if errorsPerFile:HasKey(key)
            List<CompilerError> list = errorsPerFile:RemoveKey(key)
            numberOfErrors = numberOfErrors - list:GetSize()
        end
    end

    action GetErrorsAtKey(text key) returns Iterator<CompilerError>
        if errorsPerFile:HasKey(key)
            List<CompilerError> list = errorsPerFile:GetValue(key)
            return list:GetIterator()
        end
        return undefined
    end

    action GetErrorCountAtKey(text key) returns integer
        if errorsPerFile:HasKey(key)
            List<CompilerError> list = errorsPerFile:GetValue(key)
            return list:GetSize()
        end
        return 0
    end

    action HasErrorsAtKey(text key) returns boolean
        if errorsPerFile:HasKey(key)
            return true
        end

        return false
    end

    action GetHintCountAtKey(text key) returns integer
        if hintsPerFile:HasKey(key)
            List<Hint> list = hintsPerFile:GetValue(key)
            return list:GetSize()
        end
        return 0
    end

    action RemoveHintsAtKey(text key) 
        if hintsPerFile:HasKey(key)
            List<Hint> list = hintsPerFile:RemoveKey(key)
            numberOfHints = numberOfHints - list:GetSize()
        end
    end

    action GetHintsAtKey(text key) returns Iterator<Hint>
        if hintsPerFile:HasKey(key)
            List<Hint> list = hintsPerFile:GetValue(key)
            return list:GetIterator()
        end
        return undefined
    end

    action HintsAtKey(text key) returns boolean
        if hintsPerFile:HasKey(key)
            return true
        end

        return false
    end

    action HasHints returns boolean
        return numberOfHints > 0
    end

    action HasErrors returns boolean
        return numberOfErrors > 0
    end

    action IsCompilationErrorFree returns boolean
        return numberOfErrors = 0
    end

    action GetErrorAmount returns integer
        return numberOfErrors
    end

    action GetHintIterator returns Iterator<Hint>
        HashListIterator<Hint> iterator
        iterator:SetHash(me:hintsPerFile)
        return iterator
    end
    
    action GetIterator returns Iterator<CompilerError>
        HashListIterator<CompilerError> iterator
        iterator:SetHash(me:errorsPerFile)
        return iterator
    end

    /*
     * Empties the list of compiler errorsPerFile
     */
    action Empty
        numberOfErrors = 0
        errorsPerFile:Empty()
    end

    action EmptyHints
        numberOfHints = 0
        hintsPerFile:Empty()
    end

    action GetCompilerErrorsAsJSON(text version) returns text
        JavaScriptObjectNotation total

        JavaScriptObjectNotation compiler
        compiler:Add("Name", "Quorum")
        compiler:Add("Version", version)
        total:Add("Compiler", compiler)

        text EOL = ""
        EOL = EOL:GetCarriageReturn() + EOL:GetLineFeed()

        total:Add("Intro", "This program did not compile. I have compiled a list of errors for you below:")
        JavaScriptObjectNotation jsonErrors
        jsonErrors:SetArray()
        
        Iterator<CompilerError> errors = GetIterator()
        repeat while errors:HasNext()
            CompilerError error = errors:Next()
            CompilerErrorType type = error:GetCompilerErrorType()
            text t = error:GetDisplayName()

            JavaScriptObjectNotation jsonError
            jsonError:Add("Display", FormatJSON(t))
            jsonError:Add("Type", type:GetCurrentType())
            jsonError:Add("Type Display", FormatJSON(type:GetMessage()))
            jsonError:Add("Message", FormatJSON(error:GetErrorMessage()))
            jsonError:Add("Line", error:GetLineNumber())
            jsonError:Add("Column", error:GetColumnNumber())
            jsonError:Add("Key", error:GetStaticKey())
            jsonErrors:Add(jsonError)
        end
        total:Add("Errors", jsonErrors)
        return total:ToText()
    end

    action FormatJSON(text value) returns text
        text sq = "'"
        text dq = sq:GetDoubleQuote()
        repeat while value:IndexOf(dq) > 0
            value = value:Replace(dq, sq)
        end
        value = value:Replace("'\'", "'\\'")
        return value
    end

    action GetCompilerErrorsAsText returns text
        text EOL = ""
        EOL = EOL:GetCarriageReturn() + EOL:GetLineFeed()
        text result =  "This program did not compile. I have compiled a list of errors for you below:" + EOL
        Iterator<CompilerError> errors = GetIterator()
        repeat while errors:HasNext()
            CompilerError error = errors:Next()
            text t = error:GetDisplayName()
            result = result + EOL + t
        end
        return result
    end

    action GetCompilerErrorsAsHTML returns text
        text EOL = ""
        EOL = EOL:GetCarriageReturn() + EOL:GetLineFeed()
        Iterator<CompilerError> errors = GetIterator()
        Division division
        division:SetClassAttribute("compilerErrorList")
        division:AddParagraph("I did not understand:")
        OrderedList list
        repeat while errors:HasNext()
            CompilerError error = errors:Next()
            text t = error:GetShortDisplayName()
            list:AddListItem(t)
        end
        division:Add(list)
        return division:Generate()
    end
end