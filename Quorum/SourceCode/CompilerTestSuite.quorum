package Libraries.Language.Compile.Test

use Libraries.Language.Compile.all
use Libraries.Containers.HashTable
use Libraries.Containers.Array
use Libraries.Language.Compile.Parser
use Libraries.System.File
use Libraries.Containers.Iterator
use Libraries.Language.Compile.Translate.JarGenerator
use Libraries.System.StackTraceItem
use Libraries.Compute.Math
use Libraries.System.DateTime

use Libraries.Language.Compile.Test.MatrixTester
use Libraries.Language.Compile.CompilerProfiler
use Libraries.Testing.Test
use Libraries.Testing.Tester
use Libraries.System.Process
use Libraries.System.ProcessEvent
use Libraries.System.ProcessListener
use Libraries.System.Properties
use Libraries.Language.Compile.Translate.TranslateConstants
use Libraries.Language.Support.TextBuilder

class CompilerTestSuite is ProcessListener
    integer total = 0
    integer totalBytecode = 0
    integer totalJavaScript = 0
    integer totalPython = 0

    integer bytecodePassed = 0
    integer javaScriptPassed = 0
    integer pythonPassed = 0
    integer passed = 0
    integer failed = 0
    integer previousPass = 0
    integer previousTotal = 0
    
    boolean testBytecode = true
    boolean testJavaScript = true
    boolean testPython = false
    Library library
    Tester tester
    text javascriptStandardLibraryText = ""
    text javaLocation = "java"
    constant text NODE_MAC = "/usr/local/bin/node"
    constant text NODE_WINDOWS = "C:\Program Files\nodejs\node.exe"
    text nodeLocation = undefined
    boolean javascriptError = false
    text javascriptOutput = ""
    text javascriptErrorOutput = ""
    integer javascriptExitCode = -1
    constant text JAVASCRIPT_DELIMITER = NODE_MAC:GetCarriageReturn()

    CompilerProfiler summaryProfiler
    Math math
    ExpressionsTester expressions
    TypeCheckTester typeChecks
    TypeCheckTesterGenerated typeChecksGenerated
    ChainingTester chainingChecks
    IfStatementTester ifStatementChecks
    LoopsTester loopsChecks
    ActionsTester actionsChecks
    InheritanceTester inheritanceChecks
    TemplatingTester templatingChecks
    TemplatingTesterGenerated templatingChecksGenerated
    ExceptionTester exceptionChecks
    ArrayTester arrayChecks
    StackTester stackChecks
    QueueTester queueChecks
    MathTester mathChecks
    ListTester listChecks
    HashTableTester hashChecks
    SeparatedValueTester svChecks
    DecompresserTester decompresserChecks
    UsePackageTester useChecks
    RandomTester randomChecks
    TableTester tableChecks
    BitwiseTester bitwiseChecks
    MatrixTester matrixChecks
    JsonTester jsonChecks
    BigIntegerTester bigIntegerChecks
    ByteArrayTester byteArrayChecks
    ScienceTester scienceChecks

    on create        
        expressions:SetCompilerTestSuite(me)
        typeChecks:SetCompilerTestSuite(me)
        typeChecksGenerated:SetCompilerTestSuite(me)
        chainingChecks:SetCompilerTestSuite(me)
        ifStatementChecks:SetCompilerTestSuite(me)
        loopsChecks:SetCompilerTestSuite(me)
        actionsChecks:SetCompilerTestSuite(me)
        inheritanceChecks:SetCompilerTestSuite(me)
        templatingChecks:SetCompilerTestSuite(me)
        templatingChecksGenerated:SetCompilerTestSuite(me)
        exceptionChecks:SetCompilerTestSuite(me)
        arrayChecks:SetCompilerTestSuite(me)
        stackChecks:SetCompilerTestSuite(me)
        queueChecks:SetCompilerTestSuite(me)
        mathChecks:SetCompilerTestSuite(me)
        listChecks:SetCompilerTestSuite(me)
        hashChecks:SetCompilerTestSuite(me)
        svChecks:SetCompilerTestSuite(me)
        decompresserChecks:SetCompilerTestSuite(me)
        useChecks:SetCompilerTestSuite(me)
        randomChecks:SetCompilerTestSuite(me)
        tableChecks:SetCompilerTestSuite(me)
        bitwiseChecks:SetCompilerTestSuite(me)
        matrixChecks:SetCompilerTestSuite(me)
        jsonChecks:SetCompilerTestSuite(me)
        bigIntegerChecks:SetCompilerTestSuite(me)
        byteArrayChecks:SetCompilerTestSuite(me)
        scienceChecks:SetTester(tester)

        //delete the output folder to start the testing fresh
        File rootTestFolder
        rootTestFolder:SetPath("Output")
        if rootTestFolder:Exists()
            rootTestFolder:Delete(true)
        end

        File outputLocation
        outputLocation:SetPath("Library/Compiled")
        Compiler compiler
        output "Starting Test Suite: " + compiler:GetVersion()
        library:SetLocation(compiler:GetStandardLibraryFolder())
        library:SetCachingLibraryOpcodes(true)
        library:SetOutputFolder(outputLocation)
        output "Scanning Standard Library"
        library:Scan()

        nodeLocation = FindNode()
        if nodeLocation not= undefined
            output "Reading JavaScript Standard Library"
            File jsStandardLibrary
            jsStandardLibrary:SetWorkingDirectory(compiler:GetStandardLibraryFolder():GetParentDirectory():GetAbsolutePath())
            jsStandardLibrary:SetPath("Compiled/Run/QuorumStandardLibrary.js")
            javascriptStandardLibraryText = jsStandardLibrary:Read()
        else
            output "Cannot find Node.js. Turning off JavaScript testing."
            testJavaScript = false
        end
        output "Loading Complete"
        output ""
    end

    private action GetCurrentTestPassText returns text
        integer currentPass = tester:GetPassingSize()
        integer currentTotal = tester:GetSize()

        integer passThisTest = currentPass - previousPass
        integer totalThisTest = currentTotal - previousTotal

        previousPass = tester:GetPassingSize()
        previousTotal = tester:GetSize()

        return ": " + passThisTest + "/" + totalThisTest
    end

    private action FindNode returns text
        text location = undefined
        
        Properties prop
        if prop:IsWindows()
            File nodeLoc
            nodeLoc:SetAbsolutePath(NODE_WINDOWS)
            if nodeLoc:Exists()
                location = NODE_WINDOWS
            end
        elseif prop:IsMac()
            File nodeLoc
            nodeLoc:SetAbsolutePath(NODE_MAC)
            if nodeLoc:Exists()
                location = NODE_MAC
            end
        end
        return location
    end

    action Main
        output "Testing"
        DateTime date
        start = date:GetEpochTime()
        expressions:Test()
        output expressions:GetName() + GetCurrentTestPassText()
        
        typeChecks:Test()
        output typeChecks:GetName() + GetCurrentTestPassText()
        
        typeChecksGenerated:Test()
        output typeChecksGenerated:GetName() + GetCurrentTestPassText()
        
        chainingChecks:Test()
        output chainingChecks:GetName() + GetCurrentTestPassText()
        
        ifStatementChecks:Test()
        output ifStatementChecks:GetName() + GetCurrentTestPassText()
        
        loopsChecks:Test()
        output loopsChecks:GetName() + GetCurrentTestPassText()
        
        actionsChecks:Test()
        output actionsChecks:GetName() + GetCurrentTestPassText()
        
        inheritanceChecks:Test()
        output inheritanceChecks:GetName() + GetCurrentTestPassText()
        
        templatingChecks:Test()
        output templatingChecks:GetName() + GetCurrentTestPassText()
        
        templatingChecksGenerated:Test()
        output templatingChecksGenerated:GetName() + GetCurrentTestPassText()
        
        exceptionChecks:Test()
        output exceptionChecks:GetName() + GetCurrentTestPassText()
        
        arrayChecks:Test()
        output arrayChecks:GetName() + GetCurrentTestPassText()
        
        stackChecks:Test()
        output stackChecks:GetName() + GetCurrentTestPassText()
        
        queueChecks:Test()
        output queueChecks:GetName() + GetCurrentTestPassText()
        
        mathChecks:Test()
        output mathChecks:GetName() + GetCurrentTestPassText()
        
        listChecks:Test()
        output listChecks:GetName() + GetCurrentTestPassText()

        hashChecks:Test()
        output hashChecks:GetName() + GetCurrentTestPassText()

        svChecks:Test()
        output svChecks:GetName() + GetCurrentTestPassText()

        decompresserChecks:Test()
        output decompresserChecks:GetName() + GetCurrentTestPassText()

        useChecks:Test()
        output useChecks:GetName() + GetCurrentTestPassText()

        randomChecks:Test()
        output randomChecks:GetName() + GetCurrentTestPassText()

        tableChecks:Test()
        output tableChecks:GetName() + GetCurrentTestPassText()

        bitwiseChecks:Test()
        output bitwiseChecks:GetName() + GetCurrentTestPassText()

        matrixChecks:Test()
        output matrixChecks:GetName() + GetCurrentTestPassText()

        jsonChecks:Test()
        output jsonChecks:GetName() + GetCurrentTestPassText()

        bigIntegerChecks:Test()
        output bigIntegerChecks:GetName() + GetCurrentTestPassText()

        byteArrayChecks:Test()
        output byteArrayChecks:GetName() + GetCurrentTestPassText()

        scienceChecks:Test()
        output scienceChecks:GetName() + GetCurrentTestPassText()
        

        text EOL = ""
        EOL = EOL:GetCarriageReturn() + EOL:GetLineFeed()

        DateTime date2
        finish = date2:GetEpochTime()
        number totalTime = finish - start
        totalTime = totalTime / 1000.0
        number bytecodePercentage = math:Round((bytecodePassed  * 100.0) / (totalBytecode * 1.0), 3)
        number javaScriptPercentage = math:Round((javaScriptPassed  * 100.0) / (totalJavaScript * 1.0), 3)

        output "Finished in " + totalTime + " seconds."
        output ""
        output "Summary"
        integer passedSuite = tester:GetPassingSize()
        integer sizeSuite = tester:GetSize()
        number percentSuite = math:Round((passedSuite  * 100.0) / (sizeSuite * 1.0), 3)
        output passedSuite + " of " + sizeSuite + ", " + percentSuite + "%."

        if passedSuite not= sizeSuite
            output "Outputting failed tests."
            Iterator<Test> iterator = tester:GetFailedTestsIterator()
            i = 1
            repeat while iterator:HasNext()
                Test test = iterator:Next()
                output i + ": " + test:GetName()
                i = i + 1
            end
        else
            output "All Tests Pass"
        end
    end

    
    action Test(Array<File> files, Array<text> reply)
        if testBytecode = false
            return now
        end
        CompilerTestResult result
        check            
            result = RunTestFile(files)
            TestResult(result, reply)
        detect e
            text message = e:GetErrorMessage()
            result:SetErrorMessage(message)
            result:stackTrace = e:GetStackTrace()
        end
        result:isBytecodeTest = true
        Add(result)
        tester:Add(result)
    end

    private action TestResult(CompilerTestResult result, Array<text> reply)
        result:Check(result:ranWithoutError, true)
        if result:ranWithoutError
            Array<text> lines = result:lines
            Iterator<text> lineIt = lines:GetIterator()
            integer n = lines:GetSize()
            integer i = 0
            result:passed = true
            if lines:GetSize() not= reply:GetSize()
                result:passed = false
            else
                result:passed = true
            end
            repeat n times
                if lines:Get(i) not= reply:Get(i)
                    //leaving this here as it is convenient for testing.
                    //output "Line '" + lines:Get(i) + "'" + "Answer '" + reply:Get(i) + "'"
                    result:passed = false
                end
                i = i + 1
            end
            result:Check(result:passed, true)
        end
    end

    action TestPython(Array<File> files, Array<text> reply)
        if testPython = false
            return now
        end
        CompilerTestResult result
        check
            result = RunPythonTestFile(files)
            TestResult(result, reply)
        detect e
            text message = e:GetErrorMessage()
            result:errorMessage = message
            result:stackTrace = e:GetStackTrace()
            File testFile 
            testFile = files:Get(0)
            result:SetName(testFile:GetPath())
        end
        result:isPythonTest = true
        Add(result)
        tester:Add(result)
    end

    action RunPythonTestFile(Array<File> files) returns CompilerTestResult
        CompilerTestResult result = undefined
        Compiler compiler  = GetDefaultCompiler()
        
        TranslateConstants constants
        constants:SetUserCodeAsynchronous(false)
        CompilerRequest request
        request:main = files:Get(0)
        request:isWebRequest = false
        request:library = library
        request:SetOutputType(request:PYTHON)
        request:files = files

        CompilerResult compilerResult = compiler:Compile(request)
        CompilerErrorManager manager = compilerResult:compilerErrorManager

        File testFile 
        testFile = files:Get(0)
        CompilerProfiler profiler = compiler:GetCompilerProfiler()
        summaryProfiler:Add(profiler)

        if manager:IsCompilationErrorFree() //run the program
            text convertedPython = compilerResult:convertedPython
            result = RunPython(convertedPython)
        else
            CompilerTestResult result2
            result = result2
            result:errorMessage = "Compilation had errors"
        end
        result:SetName(testFile:GetPath())
        return result
    end

    action TestJavaScript(Array<File> files, Array<text> reply)
        if testJavaScript = false
            return now
        end
        CompilerTestResult result
        check
            result = RunJavaScriptTestFile(files)
            TestResult(result, reply)
        detect e
            text message = e:GetErrorMessage()
            result:errorMessage = message
            result:stackTrace = e:GetStackTrace()
        end
        result:isJavaScriptTest = true
        Add(result)
        tester:Add(result)
    end

    private action GetDefaultCompiler returns Compiler
        Compiler defaultCompiler
        File root
        root:SetPath("Output")
        defaultCompiler:SetOutputFolder(root)
        defaultCompiler:SetOutputSpeech(false) //don't speak while in test mode
        return defaultCompiler
    end

    action RunTestFile (Array<File> files) returns CompilerTestResult
        CompilerTestResult result = undefined
        Compiler compiler = GetDefaultCompiler()
        
        CompilerRequest request
        request:main = files:Get(0)
        request:isWebRequest = false
        request:library = library
        request:files = files

        CompilerResult compilerResult = compiler:Compile(request)
        CompilerErrorManager manager = compilerResult:compilerErrorManager
        File testFile 
        testFile = files:Get(0)
        CompilerProfiler profiler = compiler:GetCompilerProfiler()
        summaryProfiler:Add(profiler)

        if manager:IsCompilationErrorFree()
            File jar = compiler:GetExecutable(request)
            result = RunClassFile(jar)
        else
            CompilerTestResult result2
            result = result2
            result:errorMessage = "Compilation had errors"
        end
        result:SetName(testFile:GetPath())
        return result
    end

    

    action RunJavaScriptTestFile(Array<File> files) returns CompilerTestResult
        CompilerTestResult result = undefined
        Compiler compiler  = GetDefaultCompiler()
        
        CompilerRequest request
        request:main = files:Get(0)
        request:isWebRequest = false
        request:library = library
        request:SetOutputType(request:JAVASCRIPT)
        request:files = files

        CompilerResult compilerResult = compiler:Compile(request)
        CompilerErrorManager manager = compilerResult:compilerErrorManager

        File testFile 
        testFile = files:Get(0)
        CompilerProfiler profiler = compiler:GetCompilerProfiler()
        summaryProfiler:Add(profiler)

        if manager:IsCompilationErrorFree() //run the program
            text convertedJavaScript = compilerResult:convertedJavaScript
            result = RunJavaScript(convertedJavaScript)
        else
            CompilerTestResult result2
            result = result2
            result:errorMessage = "Compilation had errors"
        end
        result:SetName(testFile:GetPath())
        return result
    end

    action RunClassFile(File file) returns CompilerTestResult
        CompilerTestResult result
        result:isJavaScriptTest = true

        //now run the output file in Node.js
        Process process
        process:SetName(javaLocation)
        
        File processDirectory
        processDirectory:SetAbsolutePath(file:GetParentDirectory():GetAbsolutePath())
        process:SetDirectory(processDirectory)

        Array<text> flags
        flags:Add("-jar")
        flags:Add("Default.jar")

        process:SetParameters(flags)
        process:AddListener(me)
        //now Run it
        process:Run()  

        Array<text> lines = javascriptOutput:Split(JAVASCRIPT_DELIMITER)  
        if lines not= undefined
            result:lines = lines
        end
        
        if javascriptError
            result:ranWithoutError = false
        else 
            result:ranWithoutError = true
        end
        result:returnCode = javascriptExitCode
        
        return result
    end

    action RunPython(text script) returns CompilerTestResult
        CompilerTestResult result
        result:isJavaScriptTest = true

        //First merge the script with the standard library
        File root
        root:SetPath("Output")

        File outFile
        outFile:SetWorkingDirectory(root:GetAbsolutePath())
        outFile:SetPath("Run/Result.py")

        text resultText = script
        if outFile:Exists()
            outFile:Delete()
        end

        outFile:GetParentDirectory():CreateDirectories()
        outFile:Write(resultText)

        //now run the output file in python3. It uses the same system as JavaScript
        Process process
        process:SetName("python3")
        
        File processDirectory
        processDirectory:SetAbsolutePath(outFile:GetParentDirectory():GetAbsolutePath())
        process:SetDirectory(processDirectory)

        Array<text> flags
        flags:Add("Result.py")

        process:SetParameters(flags)
        process:AddListener(me)
        //now Run it
        process:Run()  

        Array<text> lines = javascriptOutput:Split(JAVASCRIPT_DELIMITER)  
        if lines not= undefined
            result:lines = lines
        end
        
        if javascriptError
            result:ranWithoutError = false
        else 
            result:ranWithoutError = true
        end
        result:returnCode = javascriptExitCode
        
        return result
    end

    action RunJavaScript(text script) returns CompilerTestResult
        CompilerTestResult result
        result:isJavaScriptTest = true

        //First merge the script with the standard library
        File root
        root:SetPath("Output")

        File outFile
        outFile:SetWorkingDirectory(root:GetAbsolutePath())
        outFile:SetPath("Run/Result.js")

        TextBuilder builder
        builder:Append(javascriptStandardLibraryText)
        builder:Append("
")
        builder:Append(script)
        builder:Append("
Start();
")
        text resultText = builder:ToText()
//        text resultText = javascriptStandardLibraryText + "
//" + script 
//+ "
//Start();
//"
        if outFile:Exists()
            outFile:Delete()
        end

        outFile:GetParentDirectory():CreateDirectories()
        outFile:Write(resultText)

        //now run the output file in Node.js
        Process process
        process:SetName(nodeLocation)
        
        File processDirectory
        processDirectory:SetAbsolutePath(outFile:GetParentDirectory():GetAbsolutePath())
        process:SetDirectory(processDirectory)

        Array<text> flags
        flags:Add("Result.js")

        process:SetParameters(flags)
        process:AddListener(me)
        //now Run it
        process:Run()  

        Array<text> lines = javascriptOutput:Split(JAVASCRIPT_DELIMITER)  
        if lines not= undefined
            result:lines = lines
        end
        
        if javascriptError
            result:ranWithoutError = false
        else 
            result:ranWithoutError = true
        end
        result:returnCode = javascriptExitCode
        
        return result
    end

    
    action ProcessStarted(ProcessEvent event)
        javascriptError = false
        javascriptOutput = ""
        javascriptErrorOutput = ""
        javascriptExitCode = -1
    end

    action ProcessStopped(ProcessEvent event)
        javascriptExitCode = event:GetExit()
    end

    action Output(ProcessEvent event)
        javascriptOutput = javascriptOutput + event:GetOutput() + JAVASCRIPT_DELIMITER
    end

    action Error(ProcessEvent event)
        javascriptError = true
        javascriptErrorOutput = javascriptErrorOutput + event:GetOutput() + JAVASCRIPT_DELIMITER
    end

    action TestFail(Array<File> files)
        if testBytecode = false
            return now
        end
        Compiler compiler = GetDefaultCompiler()
        
        CompilerRequest request
        request:main = files:Get(0)
        request:isWebRequest = false
        request:library = library
        request:SetOutputType(request:JAVA_BYTECODE)
        request:files = files

        CompilerResult compilerResult = compiler:Compile(request)
        CompilerErrorManager manager = compilerResult:compilerErrorManager

        CompilerTestResult result
        File testFile
        testFile = files:Get(0)
        result:SetName(testFile:GetPath())
        result:isBytecodeTest = true    
        CompilerProfiler profiler = compiler:GetCompilerProfiler()
        summaryProfiler:Add(profiler)

        if manager:IsCompilationErrorFree()
            result:passed = false
            result:SetPassed(false)
            result:errorMessage = "Compilation was error free"
        else
            result:passed = true
            result:SetPassed(true)
        end
        Add(result)
        tester:Add(result)
    end

    /*
        This action is identical to the JavaScript implementation, except in Python mode. Generally, 
        you would expect if it passes one it passes the other, but tis is a test of such a hypothesis.
    */
    action TestPythonFail(Array<File> files)
        if testPython = false
            return now
        end
        Compiler compiler = GetDefaultCompiler()
        CompilerRequest request
        request:main = files:Get(0)
        request:isWebRequest = false
        request:library = library
        request:SetOutputType(request:PYTHON)
        request:files = files

        CompilerResult compilerResult = compiler:Compile(request)
        CompilerErrorManager manager = compilerResult:compilerErrorManager

        CompilerTestResult result
        File testFile 
        testFile = files:Get(0)
        result:SetName(testFile:GetPath())
        result:isJavaScriptTest = true
        CompilerProfiler profiler = compiler:GetCompilerProfiler()
        summaryProfiler:Add(profiler)

        if manager:IsCompilationErrorFree()
            result:passed = false
            result:SetPassed(false)
            result:errorMessage = "Compilation was error free"
        else
            result:SetPassed(true)
            result:passed = true
        end
        Add(result)
        tester:Add(result)
    end

    action TestJavaScriptFail(Array<File> files)
        if testJavaScript = false
            return now
        end
        Compiler compiler = GetDefaultCompiler()
        CompilerRequest request
        request:main = files:Get(0)
        request:isWebRequest = false
        request:library = library
        request:SetOutputType(request:JAVASCRIPT)
        request:files = files

        CompilerResult compilerResult = compiler:Compile(request)
        CompilerErrorManager manager = compilerResult:compilerErrorManager

        CompilerTestResult result
        File testFile 
        testFile = files:Get(0)
        result:SetName(testFile:GetPath())
        result:isJavaScriptTest = true
        CompilerProfiler profiler = compiler:GetCompilerProfiler()
        summaryProfiler:Add(profiler)

        if manager:IsCompilationErrorFree()
            result:passed = false
            result:SetPassed(false)
            result:errorMessage = "Compilation was error free"
        else
            result:SetPassed(true)
            result:passed = true
        end
        Add(result)
        tester:Add(result)
    end
    
    action Add(CompilerTestResult result)
        if result = undefined
            failed = failed + 1
        elseif result:passed
            passed = passed + 1
        else
            failed = failed + 1
        end
        total = total + 1
        text value = ""
        value = result:GetName() 

        if result:isBytecodeTest
            totalBytecode = totalBytecode + 1
            value = value + " (Bytecode)"
            if result:passed
                bytecodePassed = bytecodePassed + 1
            end
        end

        if result:isJavaScriptTest
            totalJavaScript = totalJavaScript + 1
            value = value + " (JavaScript)"
            if result:passed
                javaScriptPassed = javaScriptPassed + 1
            end
        end

        if result:isPythonTest
            totalPython = totalPython + 1
            value = value + " (Python)"
            if result:passed
                totalPython = totalPython + 1
            end
        end

        if result:exceptionThrown
            value = value + " ... " + "failed"
        elseif result:exceptionThrown and result:passed = false
            text error = result:errorMessage
            value = value + " ... " + "failed: exception thrown with message: " + error
            Array<StackTraceItem> stack = result:stackTrace
            if stack not= undefined
                if not stack:IsEmpty()
                    StackTraceItem item = stack:Get(0)
                    value = value + ", " + item:GetClassName() + ":" + item:GetMethodName() + ":" + item:GetLineNumber()
                end
            end
        elseif not result:exceptionThrown and result:passed = false
            if result:errorMessage not= ""
                value = value + "... fail: " + result:errorMessage
            else
                value = value + "... fail: Incorrect output."
            end
        else
            value = value + " ... " + "passed"
        end
    end

    action RunTestFail(text source) returns CompilerTestResult
        CompilerTestResult result = undefined
            //use TestFail instead
        return result
    end

    action RunJavaScriptTestFail(text source) returns CompilerTestResult
        CompilerTestResult result = undefined
            //use TestJavaScriptFail instead
        return result
    end

    action RunTest(text source) returns CompilerTestResult
        CompilerTestResult result = undefined
        Compiler compiler = GetDefaultCompiler()
        
        File file
        file:SetPath(source)

        Array<File> files
        files:Add(file)
        CompilerRequest request
        request:main = files:Get(0)
        request:isWebRequest = false
        request:library = library
        request:files = files

        CompilerResult compilerResult = compiler:Compile(request)
        CompilerErrorManager manager = compilerResult:compilerErrorManager

        CompilerProfiler profiler = compiler:GetCompilerProfiler()
        summaryProfiler:Add(profiler)
        
        if manager:IsCompilationErrorFree()
            File jar = compiler:GetExecutable(request)
            result = RunClassFile(jar)
        else
            CompilerTestResult result2
            result = result2
            result:errorMessage = "compilation had errors"
        end
        result:SetName(source)
        return result
    end

    action RunTest(text source, text reply)
        CompilerTestResult result
        result:SetName(source)
        check
            result = RunTest(result:GetName())
            if result:ranWithoutError
                Array<text> lines = result:lines
                if lines:GetSize() = 1
                    if lines:Get(0) = reply
                       result:passed = true
                    end
                end
            end
        detect e
            text message = e:GetErrorMessage()
            result:errorMessage = message
            result:stackTrace = e:GetStackTrace()
        end
        result:isBytecodeTest = true
        Add(result)
    end

    action RunTest(text source, text reply1, text reply2)
        CompilerTestResult result
        result:SetName(source)
        check
            result = RunTest(result:GetName())
            if result:ranWithoutError
                Array<text> lines = result:lines
                if lines:GetSize() = 2
                    if lines:Get(0) = reply1 and 
                       lines:Get(1) = reply2
                       result:passed = true
                    end
                end
            end
            
        detect e
            text message = e:GetErrorMessage()
            result:errorMessage = message
            result:stackTrace = e:GetStackTrace()
        end
        result:isBytecodeTest = true
        Add(result)
    end

    action RunTest(text source, text reply1, text reply2, text reply3)
        CompilerTestResult result
        result:SetName(source)
        check
            result = RunTest(result:GetName())
            if result:ranWithoutError
                Array<text> lines = result:lines
                if lines:GetSize() = 3
                    if lines:Get(0) = reply1 and 
                       lines:Get(1) = reply2 and
                       lines:Get(2) = reply3
                       result:passed = true
                    end
                end
            end
            
        detect e
            text message = e:GetErrorMessage()
            result:errorMessage = message
            result:stackTrace = e:GetStackTrace()
        end
        result:isBytecodeTest = true
        Add(result)
    end

    action RunTest(text source, text reply1, text reply2, text reply3, text reply4)
        CompilerTestResult result
        result:SetName(source)
        check
            result = RunTest(result:GetName())
            if result:ranWithoutError
                Array<text> lines = result:lines
                if lines:GetSize() = 4
                    if lines:Get(0) = reply1 and 
                       lines:Get(1) = reply2 and
                       lines:Get(2) = reply3 and
                       lines:Get(3) = reply4
                       result:passed = true
                    end
                end
            end
            
        detect e
            text message = e:GetErrorMessage()
            result:errorMessage = message
            result:stackTrace = e:GetStackTrace()
        end
        result:isBytecodeTest = true
        Add(result)
    end

    action RunTest(text source, text reply1, text reply2, text reply3, text reply4, text reply5)
        CompilerTestResult result
        result:SetName(source)
        check
            result = RunTest(result:GetName())
            if result:ranWithoutError
                Array<text> lines = result:lines
                if lines:GetSize() = 5
                    if lines:Get(0) = reply1 and 
                       lines:Get(1) = reply2 and
                       lines:Get(2) = reply3 and
                       lines:Get(3) = reply4 and
                       lines:Get(4) = reply5
                       result:passed = true
                    end
                end
            end
            
        detect e
            text message = e:GetErrorMessage()
            result:errorMessage = message
            result:stackTrace = e:GetStackTrace()
        end
        result:isBytecodeTest = true
        Add(result)
    end

    action RunJavaScriptTest(text source) returns CompilerTestResult
        CompilerTestResult result = undefined
        text quote = ""
        quote = quote:GetDoubleQuote()
        Compiler compiler = GetDefaultCompiler()
        
        File file
        file:SetPath(source)

        Array<File> files
        files:Add(file)

        CompilerRequest request
        request:main = files:Get(0)
        request:isWebRequest = false
        request:SetOutputType(request:JAVASCRIPT)
        request:library = library
        request:files = files

        CompilerResult compilerResult = compiler:Compile(request)
        CompilerErrorManager manager = compilerResult:compilerErrorManager

        CompilerProfiler profiler = compiler:GetCompilerProfiler()
        summaryProfiler:Add(profiler)

        if manager:IsCompilationErrorFree() //run the program
            text convertedJavaScript = compilerResult:convertedJavaScript
            result = RunJavaScript(convertedJavaScript)
        else
            CompilerTestResult result2
            result = result2
            result:errorMessage = "compilation had errors"
        end
        result:SetName(source)
        return result
    end

    action RunJavaScriptTest(text source, text reply)
        CompilerTestResult result
        result:SetName(source)
        check
            result = RunJavaScriptTest(result:GetName())
            if result:ranWithoutError
                Array<text> lines = result:lines
                if lines:GetSize() = 1
                    if lines:Get(0) = reply
                       result:passed = true
                    end
                end
            end
        detect e
            text message = e:GetErrorMessage()
            result:errorMessage = message
            result:stackTrace = e:GetStackTrace()
        end
        result:isJavaScriptTest = true
        Add(result)
    end

    action RunJavaScriptTest(text source, text reply1, text reply2)
        CompilerTestResult result
        result:SetName(source)
        check
            result = RunJavaScriptTest(result:GetName())
            if result:ranWithoutError
                Array<text> lines = result:lines
                if lines:GetSize() = 2
                    if lines:Get(0) = reply1 and 
                       lines:Get(1) = reply2
                       result:passed = true
                    end
                end
            end
        detect e
            text message = e:GetErrorMessage()
            result:errorMessage = message
            result:stackTrace = e:GetStackTrace()
        end
        result:isJavaScriptTest = true
        Add(result)
    end

    action RunJavaScriptTest(text source, text reply1, text reply2, text reply3)
        CompilerTestResult result
        result:SetName(source)
        check
            result = RunJavaScriptTest(result:GetName())
            if result:ranWithoutError
                Array<text> lines = result:lines
                if lines:GetSize() = 3
                    if lines:Get(0) = reply1 and 
                       lines:Get(1) = reply2 and
                       lines:Get(2) = reply3
                       result:passed = true
                    end
                end
            end
        detect e
            text message = e:GetErrorMessage()
            result:errorMessage = message
            result:stackTrace = e:GetStackTrace()
        end
        result:isJavaScriptTest = true
        Add(result)
    end

    action RunJavaScriptTest(text source, text reply1, text reply2, text reply3, text reply4)
        CompilerTestResult result
        result:SetName(source)
        check
            result = RunJavaScriptTest(result:GetName())
            if result:ranWithoutError
                Array<text> lines = result:lines
                if lines:GetSize() = 4
                    if lines:Get(0) = reply1 and 
                       lines:Get(1) = reply2 and
                       lines:Get(2) = reply3 and
                       lines:Get(3) = reply4
                       result:passed = true
                    end
                end
            end
        detect e
            text message = e:GetErrorMessage()
            result:errorMessage = message
            result:stackTrace = e:GetStackTrace()
        end
        result:isJavaScriptTest = true
        Add(result)
    end

    action RunJavaScriptTest(text source, text reply1, text reply2, text reply3, text reply4, text reply5)
        CompilerTestResult result
        result:SetName(source)
        check
            result = RunJavaScriptTest(result:GetName())
            if result:ranWithoutError
                Array<text> lines = result:lines
                if lines:GetSize() = 5
                    if lines:Get(0) = reply1 and 
                       lines:Get(1) = reply2 and
                       lines:Get(2) = reply3 and
                       lines:Get(3) = reply4 and
                       lines:Get(4) = reply5
                       result:passed = true
                    end
                end
            end
        detect e
            text message = e:GetErrorMessage()
            result:errorMessage = message
            result:stackTrace = e:GetStackTrace()
        end
        result:isJavaScriptTest = true
        Add(result)
    end

end
