package Libraries.Language.Compile.Translate

use Libraries.Language.Compile.Symbol.Type

class TextPrimitiveToObjectCast is ExplicitCastOpcode
    JavaBytecodeOpcodes opcodes

    action Write
        JavaBytecodeMethodWriter methodWriter = GetMethodWriter()
        VisitLineNumber()
        QuorumOpcode opcodeToCast = GetOpcodeToCast()
        if opcodeToCast not= undefined
            opcodeToCast:Write()
        end
        methodWriter:VisitMethodInvoke(opcodes:GetInvokeStatic(), "plugins/quorum/Libraries/Language/Types/Text", "ConvertTextToObject",
                "(Ljava/lang/String;)Lquorum/Libraries/Language/Object_;", false)
    end
    
    action WriteJavaScript returns text
        text result = "new quorum_Libraries_Language_Types_Text_("
        QuorumOpcode opcodeToCast = GetOpcodeToCast()
        if opcodeToCast not= undefined
            result = result + "false, " + opcodeToCast:WriteJavaScript()
        end
        return result + ")"
    end

    action GetTemplateCopy returns QuorumOpcode
        TextPrimitiveToObjectCast op
        op:SetType(me:GetType())
        return op
    end
end