package Libraries.Language.Compile.Translate

use Libraries.Language.Compile.Symbol.Type

class IntegerPrimitiveToNumberObjectCast is ExplicitCastOpcode
    JavaBytecodeOpcodes opcodes

    action Write
        JavaBytecodeMethodWriter methodWriter = GetMethodWriter()
        VisitLineNumber()
        QuorumOpcode opcodeToCast = GetOpcodeToCast()
        if opcodeToCast not= undefined
            opcodeToCast:Write()
        end
        methodWriter:VisitMethodInvoke(opcodes:GetInvokeStatic(), "plugins/quorum/Libraries/Language/Types/Integer", "ConvertIntegerPrimitiveToNumberObject",
                "(I)Lquorum/Libraries/Language/Types/Number_;", false)
    end
    
    action WriteJavaScript returns text
        text result = "new quorum_Libraries_Language_Types_Number_("
        QuorumOpcode opcodeToCast = GetOpcodeToCast()
        if opcodeToCast not= undefined
            result = result + "false, " + opcodeToCast:WriteJavaScript()
        end
        return result + ")"
    end

    action GetTemplateCopy returns QuorumOpcode
        IntegerPrimitiveToNumberObjectCast op
        op:SetType(me:GetType())
        return op
    end
end