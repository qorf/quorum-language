package Libraries.Language.Compile.Translate

use Libraries.Language.Compile.Symbol.Type
use Libraries.Language.Compile.Location
use Libraries.Language.Compile.Symbol.Variable
use Libraries.Language.Interpreter.Runtime
use Libraries.Language.Interpreter.Result

class QuorumOpcode 
    Type type = undefined
    boolean isIfExpression = false
    JavaBytecodeMethodWriter methodWriter = undefined
    Location location = undefined
    integer bytecodeOffset = 0

    blueprint action SetBytecodeWriteOffsetToAllChildren(integer offset)

    action SetBytecodeWriteOffset(integer offset)
        bytecodeOffset = offset
    end

    action GetBytecodeOffset returns integer
        return bytecodeOffset
    end

    action CalculateBytecodeOffset(Variable variable) returns integer
        if variable:IsDefinedInAlwaysBlock()
            return GetBytecodeOffset()
        else
            return 0
        end
    end

    /*
        Some opcodes can be evaluated in the context of a runtime environment
    */
    action Evaluate(Runtime runtime)

    end

    blueprint action Write

    blueprint action WriteJavaScript returns text

    action GetType returns Type
        return type
    end

    action SetType(Type type)
        me:type = type
    end

    action GetLocation returns Location
        return location
    end

    action SetLocation(Location loc)
        location = loc
    end

    action GetIsIfExpression returns boolean
        return isIfExpression
    end

    action SetIsIfExpression(boolean isInIf)
        isIfExpression = isInIf
    end

    action GetMethodWriter returns JavaBytecodeMethodWriter
        return methodWriter
    end

    action SetMethodWriter(JavaBytecodeMethodWriter writer)
        methodWriter = writer
    end

    action SetMethodWriterToAllChildren(JavaBytecodeMethodWriter writer)
        methodWriter = writer
    end

    action VisitLineNumber()
        JavaBytecodeMethodWriter liner = GetMethodWriter()
        if location not= undefined and liner not= undefined
            JavaBytecodeLabel start
            liner:VisitLabel(start)
            liner:VisitLine(location:GetLineNumber(), start)
        end
    end

    /*
        This action copies only that information provided by the type
        checker. It guarantees that the object is of the same type,
        but opcode lists are not copied, nor is any class particular information.
        For example, a plus opcode would be copied, as this is static 
        information, but what it is adding would not.
    */
    blueprint action GetTemplateCopy returns QuorumOpcode
end