package Libraries.Web

use Libraries.Web.WebTag
use Libraries.Web.Attribute
use Libraries.Web.Attributes
/*
The ProgressBar class represents HTML's (Hypertext Markup Language) progress tag which is
used to show a progress bar to display the progress of a task.
You can find more information about this tag at: 
        <a href="http://www.w3schools.com/tags/tag_progress.asp">The progress attribute</a>.

Attribute: Example
use Libraries.Web.all

class Main
    action main
      WebPage page

      ProgressBar bar
      bar:SetMaxValue("100")

      page:AddToBody(bar)
      output page:Generate()
    end
end
*/
class ProgressBar is a WebTag
    constant text max = "max"
    constant text currentValue = "value"

    /*
        This action sets the max value or work in total for the progress bar.
        

        Attribute: Parameter value Sets max value for the progress bar.

        Attribute: Example

        ProgressBar bar
        bar:SetMaxValue("100")
        
    */
    action SetMaxValue(text value)
        AddAttribute(max, value)
    end

    /*
        This action gets the max value or work in total for the progress bar.
        

        Attribute: Returns The max value for the progress bar.

        Attribute: Example

        ProgressBar bar
        bar:SetMaxValue("100")
        Attribute maxValue = bar:GetMaxValue()
        
    */
    action GetMaxValue returns Attribute
        return me:GetAttribute(max)
    end

    /*
        This action sets the current value or work completed to this point for the progress bar.
        

        Attribute: Parameter value Sets the current value for the progress bar.

        Attribute: Example

        ProgressBar bar
        bar:SetCurrentValue("45")
        
    */
    action SetCurrentValue(text value)
        AddAttribute(currentValue, value)
    end

    /*
        This action gets the current value or work completed to this point for the progress bar.
        

        Attribute: Returns The current value for the progress bar.

        Attribute: Example

        ProgressBar bar
        bar:SetCurrentValue("45")
        Attribute currentValue = bar:GetCurrentValue()
        
    */
    action GetCurrentValue returns Attribute
        return me:GetAttribute(currentValue)
    end

    action Generate returns text
        text result = "<progress " 
        Attributes attributes = parent:WebTag:GetAttributes()
        attributeText = attributes:Generate()
        result = result + attributeText + ">"
        result = result + me:GenerateNestedTags()
        result = result + GetDescription() + "</progress>"
        return result
    end
end