package Libraries.Web

use Libraries.Web.WebTag
use Libraries.Web.Attribute
use Libraries.Web.Attributes

/*
The Inserted class represents HTML's (Hypertext Markup Language) ins tag which is
used to show text that has been inserted into a document. This underlines the 
inserted text. Citation of the text can be added, as well as, a time stamp of when 
the text was inserted.

You can find more information about this tag at: 
        <a href="http://www.w3schools.com/tags/tag_ins.asp">The ins attribute</a>.

Attribute: Example
use Libraries.Web.All
class Main
   action main
      WebPage page
      Paragraph p
      Deleted deleted
      Inserted inserted
      deleted:SetDescription("apples")
      inserted:SetDescription("bananas")
      p:SetDescription("I love " + deleted:Generate() + inserted:Generate())
      
      page:AddToBody(p)
      output page:Generate()
   end
end
*/
class Inserted is WebTag
    constant text cite = "cite"
    constant text timeStamp = "dateTime"

    /*
        This action sets the citation URL or web address.
        
        Attribute: Parameter value The citation URL or web address.

        Attribute: Example

        Inserted inserted
        inserted:SetCitation("http://www.w3schools.com/tags/tag_ins.asp")
        
    */
    action SetCitation(text value)
        AddAttribute(cite, value)
    end

    /*
        This action gets the citation URL or web address.
        

        Attribute: Returns The citation address.

        Attribute: Example

        Inserted inserted
        inserted:SetCitation("http://www.w3schools.com/tags/tag_ins.asp")
        Attribute address = inserted:GetCitation()
        
    */
    action GetCitation returns Attribute
        return me:GetAttribute(cite)
    end

    /*
        This action sets the inserted time of the text.
        
        Attribute: Parameter value The date and time.

        Attribute: Example

        Inserted inserted
        inserted:SetInsertedTime("2011-11-15T22:55:03Z")
        
    */
    action SetInsertedTime(text value)
        AddAttribute(timeStamp, value)
    end

    /*
        This action gets the set inserted time of the text.
        

        Attribute: Returns The date time of the inserted text.

        Attribute: Example

        Inserted inserted
        inserted:SetInsertedTime("2011-11-15T22:55:03Z")
        Attribute dateTime = inserted:GetInsertedTime()
        
    */
    action GetInsertedTime returns Attribute
        return me:GetAttribute(timeStamp)
    end

    action Generate returns text
        text result = "<ins " 
        Attributes attributes = parent:WebTag:GetAttributes()
        attributeText = attributes:Generate()
        result = result + attributeText + ">"
        result = result + me:GenerateNestedTags()
        result = result + GetDescription() + "</ins>"
        return result
    end
end