package Libraries.Web

use Libraries.Web.WebTag
use Libraries.Web.Attribute
use Libraries.Web.Attributes
/*
The BiDirectionalOverride class represents HTML's (Hypertext Markup Language) 
bdo tag which is used to set the direction that should be displayed

You can find more information about this tag at: 
.<a href="http://www.w3schools.com/tags/tag_bdo.asp">The bdo attribute</a>.

Attribute: Example

use Libraries.Web.All
class Main
   action main
      WebPage page
      Paragraph p
      Paragraph p2

      BiDirectionalOverride bdo
      bdo:SetDirection(bdo:leftToRight)
      bdo:SetDescription("This will have the BiDirectionalOverride going left to right on it")
      p:Add(bdo)

      BiDirectionalOverride bdo2
      bdo2:SetDirection(bdo:rightToLeft)
      bdo2:SetDescription("This will have the BiDirectionalOverride going right to left on it")
      p2:Add(bdo2)
      
      page:AddToBody(p)
      page:AddToBody(p2)
      output page:Generate()
   end
end
*/
class BiDirectionalOverride is WebTag
    constant text direction = "dir"
    public constant text leftToRight = "ltr"
    public constant text rightToLeft = "rtl"

    /*
        This action sets the diection attribute of the area. 
        The diection is used to set on what side of the page the text should be set to.
        Use the constants leftToRight and rightToLeft when setting.

        Attribute: Example

        BiDirectionalOverride bdo
      bdo:SetDirection(bdo:leftToRight)
    */
    action SetDirection(text value)
        AddAttribute(direction, value)
    end

    /*
        This action gets the direction attribute of the BiDirectionalOverride.

        Attribute: Returns The coordinates of specified area 

        Attribute: Example

        BiDirectionalOverride bdo
        Attribute dir = area:GetDirection()
        if dir not= undefined
            text direction = dir:GetValue()
        end
    */
    action GetDirection returns Attribute
        return me:GetAttribute(direction)
    end

    action Generate returns text
        text result = "<bdo " 
        Attributes attributes = parent:WebTag:GetAttributes()
        attributeText = attributes:Generate()
        result = result + attributeText + ">"
        result = result + me:GenerateNestedTags()
        result = result + GetDescription() + "</bdo>"
        return result
    end

end