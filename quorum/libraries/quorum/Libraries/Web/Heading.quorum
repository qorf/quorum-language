package Libraries.Web

use Libraries.Web.WebTag
use Libraries.Web.Attributes

/*
The Heading class represents HTML's (Hypertext Markup Language) h1-h6 (headings) 
tag which is used add titles to a page.

Headings vary in size from a scale of 1-6. Where 1 is the largest and 6 is the 
smallest. Use SetScale to set which level you want to use. 

You can find more information about this tag at: 
        <a href="http://www.w3schools.com/tags/tag_hn.asp">The heading attribute</a>.

Attribute: Example
use Libraries.Web.all

class Main
    action main
      WebPage page

      Heading heading
      heading:SetScale(1)
      heading:SetDescription("H1")

      page:AddToBody(heading)
      output page:Generate()
    end
end
*/
class Heading is WebTag
    private text scale = "6"

    /*
        This action sets the scale of the header. A scale of 1 is equivalent to the 
        tag h1 and 6 is equivalent to h6.
        

        Attribute: Parameter value The scale of the header.

        Attribute: Example

        Heading heading
        heading:SetScale(1)
        
    */
    action SetScale(text value)
        scale = value
    end

    action Generate returns text
        text result = "<h"+scale+" "
        Attributes attributes = parent:WebTag:GetAttributes()
        attributeText = attributes:Generate()
        result = result + attributeText + ">"
        result = result + me:GenerateNestedTags()
        result = result + GetDescription() + "</h"+scale+">"
        return result
    end
end