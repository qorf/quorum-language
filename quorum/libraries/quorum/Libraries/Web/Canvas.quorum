package Libraries.Web

use Libraries.Web.WebTag
use Libraries.Web.Attribute
use Libraries.Web.Attributes

/*
The Canvas class represents HTML's (Hypertext Markup Language) canvas tag which is
used to place a canvas on the page that is used as a container for grapics.
A canvas should have a id so that graphics can be added to it specifically.

You can find more information about this tag at: 
        <a href="http://www.w3schools.com/tags/tag_canvas.asp">The canvas attribute</a>.

Attribute: Example
use Libraries.Web.All
class Main
   action main
      WebPage page
      
      Canvas canvas
      canvas:SetIdentifier("myCanvas")

      Script script
      script:SetType("text/javascript")
      script:SetCode("var canvas=document.getElementById('myCanvas');
                      var ctx=canvas.getContext('2d');
                      ctx.fillStyle='#FF0000';
                      ctx.fillRect(0,0,80,100);")
      page:AddToBody(canvas)
      page:AddToBody(script)
      output page:Generate()
   end
end
*/
class Canvas is WebTag
    constant text height = "height"
    constant text width = "width"
    constant text canvasId = "id"


    /*
        This action sets the height attribute of the canvas.

        Attribute: Parameter value The height in pixel to set to the canvas. 

        Attribute: Example

        Canvas canvas
        canvas:SetHeight("150")
        
    */
    action SetHeight(text value)
        AddAttribute(height, value)
    end

    /*
        This action gets the height attribute of the canvas.

        Attribute: Returns The height in pixels of the canvas. 

        Attribute: Example

        Canvas canvas
        Attribute heightInPixels = canvas:GetAddress()
        if heightInPixels not= undefined
            text height = heightInPixels:GetValue()
        end
    */
    action GetHeight returns Attribute
        return me:GetAttribute(height)
    end

    /*
        This action sets the width attribute of the canvas.

        Attribute: Parameter value The width in pixel to set to the canvas. 

        Attribute: Example

        Canvas canvas
        canvas:SetWidth("150")
        
    */
    action SetWidth(text value)
        AddAttribute(width, value)
    end

    /*
        This action gets the width attribute of the canvas.

        Attribute: Returns The width in pixels of the canvas. 

        Attribute: Example

        Canvas canvas
        Attribute widthInPixels = canvas:GetAddress()
        if widthInPixels not= undefined
            text width = widthInPixels:GetValue()
        end
    */
    action GetWidth returns Attribute
        return me:GetAttribute(width)
    end

    action Generate returns text
        text result = "<canvas " 
        Attributes attributes = parent:WebTag:GetAttributes()
        attributeText = attributes:Generate()
        result = result + attributeText + ">"
        result = result + me:GenerateNestedTags()
        result = result + GetDescription() + "</canvas>"
        return result
    end
end