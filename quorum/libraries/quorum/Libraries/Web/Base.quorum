package Libraries.Web
use Libraries.Web.WebTag
use Libraries.Web.Attribute
use Libraries.Web.Attributes
/*
Base should only be used once and only in the WebPageHeader. The purpose of this
is to set a base location for the browser to look for links that will be put 
into the page.

This class represents HTML's (Hypertext Markup Language) base tag. You can find
    more information about this tag at: 
    <a href="http://www.w3schools.com/tags/tag_base.asp">The base attribute</a>.
Attribute: Example
use Libraries.Web.All
class Main
   action main
      WebPage page
      WebPageHeader header
      
      Base base
      base:SetAddress("http://images.free-extras.com/pics/s/")

      header:Add(base)
      page:SetWebPageHeader(header)

      Image image
      image:SetAddress("smile-1613.jpg")
      image:SetWidth("150")
      image:SetHeight("150")

      page:AddToBody(image)
      
      output page:Generate()
   end
end

*/
class Base is WebTag
    constant text addressTag = "href"
    constant text target = "target"

    public constant text newTab = "_blank"
    public constant text sameFrame = "_self"
    public constant text parentFrame = "_parent"
    public constant text sameWindow = "_top"


    /*
        This action sets the address attribute of the base. The address is 
        the location of the linked webpage or document.

        Attribute: Parameter value The address of a webpage or document. 

        Attribute: Example

        Base base
        base:SetAddress("http://www.google.com")
        
    */
    action SetAddress(text value)
        AddAttribute(addressTag, value)
    end
    /*
        This action gets the address attribute of the base. The address is
        the location of the linked webpage or document.

        Attribute: Returns The address of the link. 

        Attribute: Example

        Base base
        Attribute address = base:GetAddress()
        if address not= undefined
            text href = base:GetValue()
        end
    */
    action GetAddress returns Attribute
        return me:GetAttribute(addressTag)
    end

    /*
        This action sets the target attribute of the base. The target tag
        specifies where the default target is to be for all Links. These include: _blank, _self,
        _parent, _top, framename

        Attribute: Parameter value The target of the area.

        Attribute: Example

        Base base
        base:SetTarget("_top")
        
    */
    action SetTarget(text value)
        AddAttribute(target, value)
    end

    /*
        This action gets the target attribute of the base. The target tag
        specifies what the default target will be for all Links.

        Attribute: Returns The place the area will be opened. 

        Attribute: Example

        Base base
        Attribute target = base:GetTarget()
        if target not= undefined
            text tar = base:GetValue()
        end
    */
    action GetTarget returns Attribute
        return me:GetAttribute(target)
    end

    action Generate returns text
        text result = "<base " 
        Attributes attributes = parent:WebTag:GetAttributes()
        attributeText = attributes:Generate()
        result = result + attributeText + ">"
        result = result + me:GenerateNestedTags()
        result = result + GetDescription() + "</base>"
        return result
    end

end