package Libraries.Web
use Libraries.Web.WebTag
use Libraries.Web.Attribute
use Libraries.Web.Attributes
/*
The Abbrevation class represents HTML's (Hypertext Markup Language) abbr tag which
is used to create tooltips of the full text of an abbrevation.
You can find more information about this tag at: 
    <a href="http://www.w3schools.com/tags/tag_abbr.asp">The abbr attribute</a>.
Attribute: Example
use Libraries.Web.All
class Main
   action main
      WebPage page
      
      Abbreviation abbr
      abbr:SetTitle("National Football League")
      abbr:SetDescription("NFL")

      Bold boldAbbrText
      boldAbbrText:SetDescription("The "+abbr:Generate()+" is fun")

      page:AddToBody(boldAbbrText)
      output page:Generate()
   end
end
*/

class Abbreviation is WebTag

    constant text title = "title"

    /*
       This action gets the title attribute of the Abberviation.

        Attribute: Returns The full title of the abbreviation

        Attribute: Example

        Abbreviation abbr
        Attribute abbrTitle = abbr:GetTitle()
        if title not= undefined
            text title = abbrTitle:GetValue()
        end
    */
    action GetTitle() returns Attribute
        return me:GetAttribute(title)
    end

    /*
        This action sets the title attribute of the Abberviation. 
        The title is the unabbreviated form of the abbreviation.
        
        Attribute: Parameter value The unabbreviated form of the abbreviation.

        Attribute: Example

        Abbreviation abbr
        abbr:SetTitle("National Football League")
    */
    action SetTitle(text abbrTitle)
        AddAttribute(title, abbrTitle)
    end


    action Generate returns text
        text result = "<abbr " 
        Attributes attributes = parent:WebTag:GetAttributes()
        attributeText = attributes:Generate()
        result = result + attributeText + ">"
        result = result + me:GenerateNestedTags()
        result = result + GetDescription() + "</abbr>"
        return result
    end
end