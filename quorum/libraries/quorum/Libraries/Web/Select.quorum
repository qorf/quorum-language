package Libraries.Web

use Libraries.Web.WebTag
use Libraries.Web.Attribute
use Libraries.Web.Attributes

/*
The Select class represents HTML's (Hypertext Markup Language) select tag which 
is a selection list of options or items. 
You can find more information about this tag at: 
<a href="http://www.w3schools.com/tags/tag_select.asp">The select attribute</a>.

Attribute: Example
use Libraries.Web.All
class Main
   action main
      WebPage page
      
      Select select
      OptionGroup og
      og:SetLabel("States")
      select:AddOptionGroup(og)

      page:AddToBody(select)
      print page:Generate()
   end
end
*/
class Select is a WebTag
    constant text autofocus = "autofocus"
    constant text disabled = "disabled"
    constant text formId = "form"
    constant text multiple = "multiple"
    constant text name = "name"
    constant text size = "size"

    /*
        This action gets whether the Select list is automatically got focus (true)
        or not(false).

        Attribute: Returns True if auto focused and false if not auto focused. 

        Attribute: Example

        boolean result = select:GetAutofocus()
        
    */
    action GetAutofocus returns boolean
        Attribute focus = GetAttribute(autofocus)
        return focus:GetValue() = autofocus
    end
    
    /*
        This action sets the select list to be auto focused if true and not if false.

        Attribute: Parameter value True to auto focused and false to not auto focus the select. 

        Attribute: Example

        Select s
        s:SetAutofocus(true)
        
    */
    action SetAutofocus(boolean value)
        if value
            AddAttribute(autofocus, autofocus)
        else
            RemoveAttribute(autofocus)
        end
    end

    /*
        This action gets whether the Select list is disabled (true)
        or enabled(false).

        Attribute: Returns True if diabled and false if enabled. 

        Attribute: Example

        boolean result = select:GetDisabled()
        
    */
    action GetDisabled returns boolean
        Attribute disable = GetAttribute(me:disabled)
        return disable:GetValue() = me:disabled
    end
    
    /*
        This action sets the select list to be disabled if true and enabled if false.

        Attribute: Parameter value True to disable and false to enable the select. 

        Attribute: Example

        Select s
        s:SetDisabled(true)
        
    */
    action SetDisabled (boolean value)
        if value
            AddAttribute(disabled, disabled)
        else
            RemoveAttribute(disabled)
        end
    end

    /*
        This action gets a form that the select belongs to.

        Attribute: Returns The form a select list belongs to. 

        Attribute: Example

        Attribute result = select:GetFormId()
        
    */
    action GetFormId returns Attribute
        return me:GetAttribute(formId)
    end

    /*
        This action sets the form a select list is associated with.

        Attribute: Parameter value The name of the associated form. 

        Attribute: Example

        Select s
        s:SetFormId("myForm")
        
    */
    action SetFormId(text value)
        AddAttribute(formId, value)
    end

    /*
        This action gets whether the Select list can have more than one option selected (true)
        or not(false).

        Attribute: Returns True if more than one can be selected and false if not. 

        Attribute: Example

        boolean result = select:GetMultiple()
        
    */
    action GetMultiple returns boolean
        Attribute m = GetAttribute(multiple)
        return m:GetValue() = multiple
    end

    /*
        This action sets the select list to be able to select more than one 
        option if true and not if false.

        Attribute: Parameter value True to multiple select and false to not allow 
        multiple option to be selected. 

        Attribute: Example

        Select s
        s:SetMultiple(true)
        
    */
    action SetMultiple(boolean value)
        if value
            AddAttribute(multiple, multiple)
        else
            RemoveAttribute(multiple)
        end
    end

    /*
        This action gets the name of the select list.

        Attribute: Returns Name of the select list. 

        Attribute: Example

        Attribute result = select:GetName()
        
    */
    action GetName returns Attribute
        return me:GetAttribute(name)
    end

    /*
        This action sets the name of the select list.

        Attribute: Parameter value The name of the select list. 

        Attribute: Example

        Select s
        s:SetName("mySelect")
        
    */
    action SetName(text value)
        AddAttribute(name, value)
    end

    /*
        This action gets the number of visible options in the select list.

        Attribute: Returns The number of options. 

        Attribute: Example

        Attribute result = select:GetSize()
        
    */
    action GetSize returns Attribute
        return me:GetAttribute(size)
    end

    /*
        This action sets the number of visible options in the select list.

        Attribute: Parameter value The size. 

        Attribute: Example

        Select s
        s:SetName("5")
        
    */
    action SetSize(text value)
        AddAttribute(size, value)
    end

    /*
        This action adds an option to the slecet list.

        Attribute: Parameter value The option to be added to the list. 

        Attribute: Example

        Select s
        s:SetName("States")
        Option option
        option:SetLabel("Alabama")
        s:AddOption(option)
        
    */
    action AddOption(Option option)
        Add(option)
    end

    /*
        This action removes an option from the slecet list.

        Attribute: Parameter value The option to be removed from the list. 

        Attribute: Example

        Select s
        s:SetName("States")
        Option option
        option:SetLabel("Alabama")
        s:AddOption(option)

        s:RemoveOption(option)
        
    */
    action RemoveOption(Option option)
        Remove(option)
    end

    /*
        This action adds an option group to the slecet list.

        Attribute: Parameter value The option groupt to be added to the list. 

        Attribute: Example

        Select s
        s:SetName("States")
        OptionGroup og
        og:SetLabel("Alabama")
        s:AddOptionGroup(og)
        
    */
    action AddOptionGroup(OptionGroup optionGroup)
        Add(optionGroup)
    end

    /*
        This action removes an option group from the slecet list.

        Attribute: Parameter value The option group to be removed from the list. 

        Attribute: Example

        Select s
        s:SetName("States")
        OptionGroup og
        og:SetLabel("Alabama")
        s:AddOptionGroup(og)

        s:RemoveOptionGroup(og)
        
    */
    action RemoveOptionGroup(OptionGroup optionGroup)
        Remove(optionGroup)
    end
    

    action Generate returns text
        text result = "<select " 
        Attributes attributes = parent:WebTag:GetAttributes()
        attributeText = attributes:Generate()
        result = result + attributeText + ">"
        result = result + me:GenerateNestedTags()
        result = result + GetDescription() + "</select>"
        return result
    end
end