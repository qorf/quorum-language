package Libraries.Web

use Libraries.Web.WebTag
use Libraries.Web.Attribute
use Libraries.Web.Attributes

/*
The CalculationOutput class represents HTML's (Hypertext Markup Language) output 
tag which is used display a calculation that was preformed by a script.
You can find more information about this tag at: 
        <a href="http://www.w3schools.com/tags/tag_output.asp">The output attribute</a>.

Attribute: Example
use Libraries.Web.All
class Main
   action main
      WebPage page
      
      Form form
      form:SetOnInput("x.value=parseInt(a.value)+parseInt(b.value)")
      form:SetDescription("0")

      Input formInput
      formInput:SetType("range")
      formInput:SetIdentifier("a")
      formInput:SetDefaultValue("50")
      formInput:SetDescription("100 +")

      Input formInput2
      formInput2:SetType("number")
      formInput2:SetIdentifier("b")
      formInput2:SetDefaultValue("50")
      formInput2:SetDescription("= ")

      CalculationOutput calcOutput
      calcOutput:SetName("x")
      calcOutput:SetElementIds("a b")
      
      form:Add(formInput)
      form:Add(formInput2)
      form:Add(calcOutput)

      page:AddToBody(form)
      output page:Generate()
   end
end
*/
class CalculationOutput is WebTag
    constant text elementIds = "for"
    constant text formId = "form"
    constant text name = "name"

    /*
        This action sets the name of the CalculationOutput so that it can be 
        referencd other tags.

        Attribute: Parameter value The name value of the CalculationOutput.

        Attribute: Example

        CalculationOutput calcOutput
        calcOutput:SetName("x")
        
    */
    action SetName(text value)
        AddAttribute(name, value)
    end

    /*
        This action gets the name of the CalculationOutput.

        Attribute: Example

        CalculationOutput calcOutput
        Attribute nameValue = calcOutput:GetName()
        if nameValue not= undefined
            text name = nameValue:GetValue()
        end
        
    */
    action GetName returns Attribute
        return me:GetAttribute(name)
    end

    /*
        This action sets the element ids. The element ids are used to specify 
        the relationship between the result of the calculation, and the 
        elements used in the calculation.

        Attribute: Parameter value The element ids of the for the 
        CalculationOutput to use.

        Attribute: Example

        CalculationOutput calcOutput
        calcOutput:SetElementIds("a b")
        
    */
    action SetElementIds(text value)
        AddAttribute(elementIds, value)
    end

    /*
        This action gets the element ids of the CalculationOutput.

        Attribute: Example

        CalculationOutput calcOutput
        Attribute elementIdsValue = calcOutput:GetElementIds()
        if elementIdsValue not= undefined
            text elementIds = elementIdsValue:GetValue()
        end
        
    */
    action GetElementIds returns Attribute
        return me:GetAttribute(elementIds)
    end

    action Generate returns text
        text result = "<output " 
        Attributes attributes = parent:WebTag:GetAttributes()
        attributeText = attributes:Generate()
        result = result + attributeText + ">"
        result = result + me:GenerateNestedTags()
        result = result + GetDescription() + "</output>"
        return result
    end
end