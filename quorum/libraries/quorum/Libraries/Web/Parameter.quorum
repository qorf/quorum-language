package Libraries.Web

use Libraries.Web.WebTag
use Libraries.Web.Attribute
use Libraries.Web.Attributes
/*
The Parameter class represents HTML's (Hypertext Markup Language) param tag which is
used to define parameter to pass to plugins that are in an EmbedContent class.
You can find more information about this tag at: 
        <a href="http://www.w3schools.com/tags/tag_param.asp">The param attribute</a>.

Attribute: Example
use Libraries.Web.all

class Main
    action main
      WebPage page

      EmbededContent content

      Parameter param
      param:SetName("myParam")
      param:SetValue("100")
      
      content:Add(param)

      page:AddToBody(content)
      output page:Generate()
    end
end
*/
class Parameter is WebTag
    constant text name = "name"
    constant text paramValue = "value"

    /*
        This action sets the name of a parameter.
        

        Attribute: Parameter value Sets the name of the parameter.

        Attribute: Example

        Parameter param
        param:SetName("test")
        
    */
    action SetName(text value)
        AddAttribute(name, value)
    end

    /*
        This action gets the name of a parameter.
        

        Attribute: Returns The name of the parameter.

        Attribute: Example

        Parameter param
        param:SetName("test")
        Attribute name = param:GetName()
        
    */
    action GetName returns Attribute
        return me:GetAttribute(name)
    end

    /*
        This action sets the value of a parameter.
        

        Attribute: Parameter value Sets the value of the parameter.

        Attribute: Example

        Parameter param
        param:SetValue("50")
        
    */
    action SetValue(text value)
        AddAttribute(paramValue, value)
    end

    /*
        This action gets the value of a parameter.
        

        Attribute: Returns The value of the parameter.

        Attribute: Example

        Parameter param
        param:SetValue("50")
        Attribute value = param:GetValue()
        
    */
    action GetValue returns Attribute
        return me:GetAttribute(paramValue)
    end

    action Generate returns text
        text result = "<param " 
        Attributes attributes = parent:WebTag:GetAttributes()
        attributeText = attributes:Generate()
        result = result + attributeText + ">"
        result = result + me:GenerateNestedTags()
        result = result + GetDescription()
        return result
    end
end