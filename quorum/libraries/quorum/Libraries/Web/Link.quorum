package Libraries.Web

use Libraries.Containers.HashTable
use Libraries.Web.WebTag
use Libraries.Web.Attribute
use Libraries.Web.Attributes
/*
The Link class represents HTML's (Hypertext Markup Language) a tag which 
is a way to add links to other web pages to your page.
You can find more information about this tag at: 
<a href="http://www.w3schools.com/tags/tag_a.asp">The a attribute</a>.

Attribute: Example
use Libraries.Web.All
class Main
   action main
      WebPage page
      
      Link link
      link:SetAddress("http://www.google.com")
      link:SetDescription("Google")

      page:AddToBody(link)
      output page:Generate()
   end
end
*/
class Link is a WebTag

    /*  This is equivalent to the href tag.
    */
    constant text address = "href"
    constant text languageCode = "hreflang"
    constant text media = "media"
    constant text relationship = "relationship"
    constant text target = "target"
    constant text type = "type"
    public constant text newTab = "_blank"
    public constant text sameFrame = "_self"
    public constant text parentFrame = "_parent"
    public constant text sameWindow = "_top"
    
    /*
        This action gets the address attribute of the link. The address is
        the location of the linked webpage or document.

        Attribute: Returns The address of the link. 

        Attribute: Example

        Link link
        Attribute address = link:GetAddress()
    */
    action GetAddress returns Attribute
        return me:GetAttribute(address)
    end

    /*
        This action sets the address attribute of the link. The address is 
        the location of the linked webpage or document. 
        
        This needs to have a http:// or https:// at the start of the address.

        Attribute: Parameter value The address of a webpage or document. 

        Attribute: Example

        Link link
        link:SetAddress("http://www.google.com")
        
    */
    action SetAddress(text value)
        AddAttribute(address, value)
    end
    
    /*
        This action gets the language code attribute of the link. The language 
        code is the two letter code corresponding to a language. See the 
        Libraries.Web.LanguageCode class.

        Attribute: Returns The two letter language code. 

        Attribute: Example

        Link link
        Attribute code = link:GetLanguageCode()
    */
    action GetLanguageCode returns Attribute
        return me:GetAttribute(languageCode)
    end

    /*
        This action sets the language code attribute of the link. The language 
        code is the two letter code corresponding to a language. See the 
        Libraries.Web.LanguageCode class.

        Attribute: Parameter value The two letter language code. 

        Attribute: Example

        Link link
        LanguageCode language
        link:SetLanguageCode(language:english)
        
    */
    action SetLanguageCode(text value)
        AddAttribute(languageCode, value)
    end

    /*
        This action gets the media attribute of the link. The media tag
        specifies the type of media the link will be displayed on (e.g. handheld). 

        Attribute: Returns The type of media the page will be displayed on (braille, handheld, aural, print, screen, etc.) 

        Attribute: Example

        Link link
        Attribute media = link:GetMedia()
    */
    action GetMedia returns Attribute
        return me:GetAttribute(media)
    end

    /*
        This action sets the media attribute of the link. The media tag
        specifies the type of media the link will be displayed on (e.g. handheld).

        Attribute: Parameter value The type of media the page will be displayed on (braille, handheld, aural, print, screen, etc.)

        Attribute: Example

        Link link
        link:SetMedia("handheld")
        
    */
    action SetMedia(text value)
        AddAttribute(media, value)
    end

    /*
        This action gets the relationship attribute of the link. The relationship tag
        specifies the type of relationship between the link and the current page. 

        Attribute: Returns The type of relationship between the current page and link. 

        Attribute: Example

        Link link
        Attribute relationship = link:GetRelationship()
    */
    action GetRelationship returns Attribute
        return me:GetAttribute(relationship)
    end

    /*
        This action sets the relationship attribute of the link. The relationship tag
        specifies the type of relationship between the link and the current page. These
        relationships include: alternate, author, help, bookmark, license, next, 
        nofollow, noreferrer, prefetch, prev, search, tag

        Attribute: Parameter value The type of relationship between the current page and the linked page.

        Attribute: Example

        Link link
        link:SetRelationship("next")
        
    */
    action SetRelationship(text value)
        AddAttribute(relationship, value)
    end

    /*
        This action gets the target attribute of the link. The target tag
        specifies where the link is to be opened.

        Attribute: Returns The place the link will be opened. 

        Attribute: Example

        Link link
        Attribute target = link:GetTarget()
    */
    action GetTarget returns Attribute
        return me:GetAttribute(target)
    end

    /*
        This action sets the target attribute of the link. The target tag
        specifies where the link is to be opened. These include: _blank, _self,
        _parent, _top, framename

        Attribute: Parameter value The target of the link.

        Attribute: Example

        Link link
        link:SetTarget("_top")
        
    */
    action SetTarget(text value)
        AddAttribute(target, value)
    end

    /*
        This action gets the MIME type attribute of the link. The type attribute
        maps to a MIME type of the linked document.

        Attribute: Returns The MIME type attribute of the link. 

        Attribute: Example

        Link link
        Attribute type = link:GetType()
    */
    action GetType returns Attribute
        return me:GetAttribute(type)
    end

    /*
        This action sets the Type attribute of the link. The type attribute
        maps to a MIME type of the linked document

        Attribute: Parameter value The MIME type of the link.

        Attribute: Example

        Link link
        link:SetType("text/html")
        
    */
    action SetType(text value)
        AddAttribute(type, value)
    end

    action Generate returns text
        text result = "<a " 
        Attributes attributes = parent:WebTag:GetAttributes()
        attributeText = attributes:Generate()
        result = result + attributeText + ">"
        result = result + me:GenerateNestedTags()
        result = result + GetDescription() + "</a>"
        return result
    end
end
