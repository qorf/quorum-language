package Libraries.Web

use Libraries.Web.WebTag
use Libraries.Web.Attribute
use Libraries.Web.Attributes
/*
The Column class represents HTML's (Hypertext Markup Language) col tag which allows an 
entire column to be formatted and controlled from one element instead of each 
cell in a table. 
You can find more information about this tag at: 
    <a href="http://www.w3schools.com/tags/tag_col.asp">The col attribute</a>.
Attribute: Example

use Libraries.Web.All
class Main
   action main
      //make a web page
      WebPage page
      
      //create a table
      Table table
      //create a column group
      ColumnGroup cg
      //create a column
      Column col
      //setup that column
      col:SetSpan(2)
      //add the column to the column group
      cg:Add(col)
      //add the column group to the table
      table:SetColumnGroup(cg)
      page:AddToBody(table)
   end
end
*/
class Column is a WebTag
    constant text span = "span"

    /*
        This action gets the span attribute of the column. The span is
        the number of columns group encompasses.

        Attribute: Returns The span of the column group. 

        Attribute: Example

        Column col
        Attribute span = col:GetSpan()
    */
    action GetSpan returns Attribute
        return me:GetAttribute(span)
    end

    /*
        This action sets the span attribute of the column. The span attribute
        is the number of column the group encompasses.

        Attribute: Parameter value The span.

        Attribute: Example

        Column col
        col:SetSpan(2)
        
    */
    action SetSpan(integer value)
        me:AddAttribute(span, value)
    end

    action Generate() returns text
        text result = "<col " 
        Attributes attributes = parent:WebTag:GetAttributes()
        attributeText = attributes:Generate()
        result = result + attributeText + ">"
        return result
    end

end