package Libraries.Web

use Libraries.Containers.List
use Libraries.Containers.Blueprints.Iterator
use Libraries.Web.WebGenerator
use Libraries.Web.GlobalAttributeAccepter
use Libraries.Web.WebPageHeader
use Libraries.Web.DocumentType
use Libraries.Web.Body

/*
The WebPage class represents HTML's (Hypertext Markup Language) option tag which 
defines the document as an html document. 
You can find more information about this tag at: 
<a href="http://www.w3schools.com/tags/tag_html.asp">The html attribute</a>.

Attribute: Example
use Libraries.Web.All
class Main
   action main
      WebPage page
      
      Body body
      page:SetBody(body)

      page:AddToBody(body)
      print page:Generate()
   end
end
*/
class WebPage is a WebGenerator, GlobalAttributeAccepter
    DocumentType documentType
    WebPageHeader header
    Body body
    constant text manifest = "manifest"
    /*
        This action gets the address of the documents cache.

        Attribute: Returns The document cache address. 

        Attribute: Example

        WebPage page
        text result = page:GetManifest()
        
    */
    action GetManifest returns text
        text textValue = ""
        Attribute value = me:GetAttribute(manifest)
        if value not= undefined
            textValue = value:GetValue()
        end
        return textValue
    end

    /*
        This action sets the address of the documents cache.

        Attribute: Parameter value The document cache address. 

        Attribute: Example

        WebPage page
        page:SetManifest("test.appcache")
        
    */
    action SetManifest(text value)
        Attribute attribute
        attribute:SetName(manifest)
        attribute:SetValue(value)
        me:Add(attribute)
    end

    /*
        This action gets the body of the webpage.

        Attribute: Returns The body of the webpage. 

        Attribute: Example

        WebPage page
        Body result = page:GetBody()
        
    */
    action GetBody returns Body
        return body
    end

    /*
        This action gets the type of document.

        Attribute: Returns The type of document. 

        Attribute: Example

        WebPage page
        DocumentType result = page:GetDocumentType()
        
    */
    action GetDocumentType returns DocumentType
        return documentType
    end

    /*
        This action sets the DocumentType of the webpage.

        Attribute: Parameter doc The document type. 

        Attribute: Example

        WebPage page
        DocumentType document
        page:SetDocumentType(document)
        
    */
    action SetDocumentType(DocumentType doc)
        documentType = doc
    end

    /*
        This action gets the webpages title.

        Attribute: Returns The title. 

        Attribute: Example

        WebPage page
        text result = page:GetWebPageTitle()
        
    */
    action GetWebPageTitle returns text             // GetTitle already exists in GlobalAttributeAccepter
        return header:GetTitleHeaderValue()
    end

    /*
        This action sets the title of the webpage.

        Attribute: Parameter value The title. 

        Attribute: Example

        WebPage page
        page:SetWebPageTitle("My First Webpage")
        
    */
    action SetWebPageTitle(text value)
        header:SetTitleHeaderValue(value)
    end

    /*
        This action gets the header of this webpage.

        Attribute: Returns The header. 

        Attribute: Example

        WebPage page
        WebPageHeader result = page:GetWebPageHeader()
        
    */
    action GetWebPageHeader returns WebPageHeader
        return header
    end

    /*
        This action sets the address of the documents cache.

        Attribute: Parameter header The header. 

        Attribute: Example

        WebPage page
        WebPageHeader header
        page:SetWebPageHeader(header)
        
    */
    action SetWebPageHeader(WebPageHeader header) 
        me:header = header
    end

    /*
        This action sets the body of the webpage.

        Attribute: Parameter body The body. 

        Attribute: Example

        WebPage page
        Body body
        page:SetManifest(body)
        
    */
    action SetBody(Body body)
        me:body = body
    end

    action Generate returns text
        text result = documentType:Generate()
        result = result + 
"
"
        +   "<html "
        Attributes attributes = me:GetAttributes()
        result = result + attributes:Generate()
        result = result + ">"
        
        if header not= undefined
            result = result + header:Generate()
        end

        result = result + body:Generate()
        return result + 
"
"       + "</html>"
    end

    /*
        This action add to the body of the webpage directly and in order.

        Attribute: Parameter tag The web tag object that should be added to the body of this webpage. 

        Attribute: Example

        WebPage page
        Link link
        page:AddToBody(link)
        
    */
    action AddToBody(WebTag tag)
        body:Add(tag)
    end
end