package Libraries.Web

use Libraries.Web.WebTag
use Libraries.Web.Attribute
use Libraries.Web.Attributes
/*
The InlineFrame class represents HTML's (Hypertext Markup Language) iframe tag which is
used to add an another page into the current webpage.
You can find more information about this tag at: 
        <a href="http://www.w3schools.com/tags/tag_iframe.asp">The iframe attribute</a>.

Attribute: Example
use Libraries.Web.All
class Main
   action main
      WebPage page
      
      InlineFrame inlineFrame
      inlineFrame:SetAddress("http://www.w3schools.com")
      inlineFrame:SetHeight("550")
      inlineFrame:SetWidth("550")
      inlineFrame:SetSeamless(true)
      
      page:AddToBody(inlineFrame)
      print page:Generate()
   end
end
*/
class InlineFrame is a WebTag
    constant text height = "height"
    constant text width = "width"
    constant text name = "name"
    constant text sandbox = "sandbox"
    constant text seamless = "seamless"
    constant text source = "src"

    
    /*
        This action sets the height attribute of the inline frame.

        Attribute: Parameter value The height in pixel to set to the inline frame. 

        Attribute: Example

        InlineFrame inlineFrame
        inlineFrame:SetHeight("150")
        
    */
    action SetHeight(text value)
        AddAttribute(height, value)
    end

    /*
        This action gets the height attribute of the inline frame.

        Attribute: Returns The height in pixels of the inline frame. 

        Attribute: Example

        InlineFrame inlineFrame
        Attribute heightInPixels = inlineFrame:GetHeight()
        if heightInPixels not= undefined
            text height = heightInPixels:GetValue()
        end
    */
    action GetHeight returns Attribute
        return me:GetAttribute(height)
    end

    /*
        This action sets the width attribute of the inline frame.

        Attribute: Parameter value The width in pixel to set to the inline frame. 

        Attribute: Example

        InlineFrame inlineFrame
        inlineFrame:SetWidth("150")
        
    */
    action SetWidth(text value)
        AddAttribute(width, value)
    end

    /*
        This action gets the width attribute of the inline frame.

        Attribute: Returns The width in pixels of the inline frame. 

        Attribute: Example

        InlineFrame inlineFrame
        Attribute widthInPixels = inlineFrame:GetWidth()
        if widthInPixels not= undefined
            text width = widthInPixels:GetValue()
        end
    */
    action GetWidth returns Attribute
        return me:GetAttribute(width)
    end

    /*
        This action sets the name attribute of the inline frame.

        Attribute: Parameter value The name to set to the inline frame. 

        Attribute: Example

        InlineFrame inlineFrame
        inlineFrame:SetName("someFrame")
        
    */
    action SetName(text value)
        AddAttribute(name, value)
    end

    /*
        This action gets the name attribute of the inline frame.

        Attribute: Returns The name of the inline frame. 

        Attribute: Example

        InlineFrame inlineFrame
        Attribute nameOfFrame = inlineFrame:GetName()
        if nameOfFrame not= undefined
            text name = nameOfFrame:GetValue()
        end
    */
    action GetName returns Attribute
        return me:GetAttribute(name)
    end

    /*
        This action sets the sanbox attribute of the inline frame. This is used 
        to add extra restrictions to the inlined frame.

        Attribute: Parameter value The type of restriction to set to the inline frame.
                   These can be - "" (Applies all restricions)
                                - allow-same-origin (Allows the iframe content to be treated as being from the same origin as the containing document)
                                - allow-top-navigation (Allows the iframe content to navigate (load) content from the containing document)
                                - allow-forms (Allows form submission)
                                - allow-scipts (Allows script execution)
        Attribute: Example

        InlineFrame inlineFrame
        inlineFrame:SetSandBox("allow-scripts")
        
    */
    action SetSandbox(text value)
        AddAttribute(sandbox, value)
    end

    /*
        This action gets the sandbox attribute of the inline frame.

        Attribute: Returns The sandbox restriction on the inline frame. 

        Attribute: Example

        InlineFrame inlineFrame
        Attribute sandboxFrame = inlineFrame:GetSandBox()
        if sandboxFrame not= undefined
            text sandbox = sandboxFrame:GetValue()
        end
    */
    action GetSandbox returns Attribute
        return me:GetAttribute(sandbox)
    end

        /*
        This action sets the seamless attribute of the inline frame. Set this to 
        be true if you want the inline frame to have no borders around it.

        Attribute: Parameter value The seamless to set to the inline frame. 

        Attribute: Example

        InlineFrame inlineFrame
        inlineFrame:SetSeamless(true)
        
    */
    action SetSeamless (boolean isSeamless)
        if isSeamless
            AddAttribute(seamless, seamless)
        else
            RemoveAttribute(seamless)
        end
    end

    /*
        This action returns true if the inline frame has been set to be seamless 
        meaning it will have no border between it and the rest of the page.

        Attribute: Returns This returns true if it is seamless

        Attribute: Example
        use Libraries.Web.All

        InlineFrame inlineFrame
        boolean isSeemless = inlineFrame:GetSeamless()
    */
    action GetSeamless returns boolean
        Attribute isSeamless = GetAttribute(seamless)
        if isSeamless not= undefined
            return isSeamless:GetValue() = seamless
        end
        return false
    end

    /*
        This action sets the address attribute of the inline frame. The address is 
        the location of the linked webpage or document.

        Attribute: Parameter value The address of a webpage or document. 

        Attribute: Example

        InlineFrame inlineFrame
        inlineFrame:SetAddress("http://www.google.com")
        
    */
    action SetAddress(text value)
        AddAttribute(source, value)
    end

    /*
        This action gets the address attribute of the inline frame. The address is
        the location of the linked webpage or document.

        Attribute: Returns The address of the inline frame. 

        Attribute: Example

        InlineFrame inlineFrame
        Attribute address = inlineFrame:GetAddress()
        if address not= undefined
            text href = address:GetValue()
        end
    */
    action GetAddress returns Attribute
        return me:GetAttribute(source)
    end

    action Generate returns text
        text result = "<iframe " 
        Attributes attributes = parent:WebTag:GetAttributes()
        attributeText = attributes:Generate()
        result = result + attributeText + ">"
        result = result + me:GenerateNestedTags()
        result = result + GetDescription() + "</iframe>"
        return result
    end
end